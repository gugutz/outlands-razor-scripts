#################################
# CREATE ALL LISTS FOR CONFIG
#################################
#
if not varexist super_space_state_lists_first_run_done
    createlist 'super_space_state_lists'
    pushlist 'super_space_state_lists' 'packies'

    foreach list in 'super_space_state_lists'
        if not listexists list
            createlist list
        endif
    endfor

    #################################
    # CONFIGURATION
    #################################
    setvar! action_delay 350

    # moongate gump ids
    setvar! public_moongate_gump 3773199800
    setvar! magery_moongate_gump 3773199800
    setvar! omni_moongate_gump 687547349

    # moongate btns
    setvar! corpse_creek_moongate_btn 17
    setvar! regular_moongate_confirm_btn 2
    setvar! omni_moongate_confirm_btn 10

    # moongate graphics and colors
    setvar! chivalry_moongate_graphic 27754
    setvar! chivalry_moongate_hue 2091

    setvar! ship_abilities_gump 1774462345
    setvar! ship_targetting_mode_gump 3329337894

    setvar! super_space_state_lists_first_run_done 1
endif

###############################
# ENTER MOONGATE
###############################

if gumpexists magery_moongate_gump
    gumpresponse 2 magery_moongate_gump
endif
if gumpexists omni_moongate_gump
    gumpresponse 2 omni_moongate_gump
endif

if findtype 3948|27754 ground any any 2
    script 'actions\enter-moongate'
endif


###############################
# ENTER/HIT FACTION GATE
###############################


if findtype 36317|36327 ground any any 1 as found
    dclick found
endif

###############################
# SOLVE CHAR OVERWEIGHT
###############################


if stam < 5
    if weigth >= maxweigth
        if findtype 'white potion' backpack
            sysmsg 'DRINKING STR POT' 90
            potion 'strength'
            wait action_delay
        else
            sysmsg 'CANT FIND STR POT TO INCREASE WEIGHT!' 54
        endif
    endif


    if findtype 'red potion' backpack
        sysmsg 'DRINKING REFRESH'
        potion 'refresh'
        wait action_delay
    else
        sysmsg 'CANT FIND REFRESH POTION TO INCREASE STAMINA!' 54
    endif
endif

if not targetexists 'any'
    interrupt
endif



###############################
# HANDLE PARALYZE
###############################

if findbuff 'paralyze'
    if not findtype pouch backpack 38
        overhead '┃ NO TRAPPED POUCHES ┃' 39
        stop
    endif

    say '[pouch'
    overhead '*using trap pouch*' 52
endif


# the idea here is to execute only 1 action per key press
clearsysmsg


###############################
# EQUIP INTERRUPT WEAPON AND ATTACK
###############################

if skill 'archery' >= 80 and lhandempty and findtype 'bow' backpack as weap
    if noto lasttarget == hostile
        overhead 'attacking!' 41 'last'
        overhead 'attacking lasttarget!' 41
        hotkey 'Attack Last Target'
    endif
    overhead 'equipping bow' 74

    if targetexists
        sysmsg 'LIFT'
        hotkey 'Drop What You Are Currently Holding'
        lift weap 1
        wait 50
        while queued
        endwhile

        drop 'self' LeftHand
    else
        sysmsg 'DCLICK'
        dclick weap
    endif

    stop
elseif skill 'swordsmanship' >= 80 and rhandempty and findtype 'katana' backpack as weap
    if noto lasttarget == hostile
        overhead 'attacking!' 41 'last'
        overhead 'attacking lasttarget!' 41
        hotkey 'Attack Last Target'
    endif
    overhead 'equipping katana' 74

    if targetexists
        sysmsg 'LIFT'
        hotkey 'Drop What You Are Currently Holding'
        lift weap 1
        wait 50
        while queued
        endwhile

        drop 'self' RightHand
    else
        sysmsg 'DCLICK'
        dclick weap
    endif

    stop
endif

if targetexists 'harmful'
    script 'targetting\last-target-bow-shot'
endif


if insysmsg 'disturbed'
    overhead 'disrupted before first run!'
    stop
endif

###############################
# TOGGLE BOATING MODE IF CAV TORCH IS AT HAND AND TURNED OFF
###############################


setvar! boating_mode 0
if findtype 2578 self
    sysmsg 'BOATING MODE ON'
    setvar! boating_mode 1
elseif findtype 3947 self
    sysmsg 'BOATING MODE OFF'
    setvar! boating_mode 0
endif

###############################
# grab stools on ground blocking the way
###############################

# if findtype 'foot stool' ground any any 2 as stool
if findtype 'stool' ground any any 1 as stool
    overhead '*GRABBING STOOL*' 55
    hotkey "Grab Item"
    wft 600
    target stool
    stop
endif

# if insysmsg 'disturbed'
#     overhead 'disrupted before first run!'
#     stop
# endif

###############################
# confirm ress if gump present
###############################

if dead and gumpexists 2957810225
    overhead 'RESSING...' 10
    gumpresponse 1 2957810225
    wait action_delay
    if not dead
        if findtype 'yellow potion' backpack
            hotkey "Drink Heal"
            wait action_delay
            while insysmsg 'you must wait'
                clearsysmsg
                hotkey "Drink Heal"
            endwhile
        endif

        script 'actions\recall-home'
    endif
    stop
endif

###############################
# ress followers if gump present
###############################

if followers > 0 and gumpexists 1277013995
    overhead 'RESSING FOLLOWERS...' 10
    gumpresponse 10 1277013995
    waitforgump 1277013995
    gumpresponse 11 1277013995
    waitforgump 1277013995
    gumpresponse 12 1277013995
    waitforgump 1277013995
    gumpresponse 2 1277013995
    stop
endif

###############################
# grab stools on ground blocking the way
###############################


if insysmsg 'disturbed'
    overhead 'disrupted before first run!'
    stop
endif


###############################
# first run actions
###############################

if not dead 'self' and not targetexists 'any'
    if diffhits < 10 and not varexist first_run_done
        clearsysmsg

        # we only need to open the backpack first if we cant find a bag, which might indicate char isnt seeing whats inside his backpack
        if not findtype 'bag' backpack
            if backpack_opened_once != 1
                if findlayer self 'backpack' as char_backpack
                    sysmsg 'Opening backpack first time...' 91
                    overhead 'opening backpack...' 91
                    dclick char_backpack
                    wait 550
                    if insysmsg 'You must wait to perform another action'
                        overhead 'failed to open backpack!' 41
                        stop
                    endif
                    setvar! backpack_opened_once 1
                endif
            endif
        endif

        if loadout_bag_opened_once != 1
            if findtype 'bag' backpack as reg_bag
                sysmsg 'Opening reagent bag first time...' 91
                overhead 'opening main bag...' 91
                dclick reg_bag
                wait 550
                if insysmsg 'You must wait to perform another action'
                    overhead 'failed to open reg bag!' 41
                    stop
                endif
                setvar! loadout_bag_opened_once 1
            endif
        endif

        overhead 'FIRST RUN FINISHED' 91
        setvar! first_run_done 1
    endif
endif


#################################
# RESTOCK & RESSUPLY IF SHELF NEARBY (2 tiles)
#################################


if findtype 'storage shelf' ground any any 2 as storageshelf
    # first find all char packies to unload them too
    removelist 'packies'
    createlist 'packies'

    while findtype 292 ground any any 2 as packie
        if noto packie = noto 'self'
            pushlist 'packies' packie
        endif
        @ignore packie
    endwhile

    while findtype 291 ground any any 2 as packie
        if noto packie = noto 'self'
            pushlist 'packies' packie
        endif
        @ignore packie
    endwhile
    clearignore


    @setvar storage_shelf storageshelf
    #overhead 'using shelf' 68 'shelf'
    sysmsg 'Restocking Storage Shelf...' sysmsgs_color
    menu storage_shelf 0
    wft 600
    if targetexists 'neutral'
        target 'self'
    endif
    waitforgump 3232825965 600
    gumpclose 3232825965

    # restock shelf from packies
    if list 'packies' > 0 and varexist storage_shelf
        foreach packanimal in 'packies'
            sysmsg '> Restocking Shelf From Pack Animal...' sysmsgs_color
            menu storage_shelf 0
            wft 600
            if targetexists 'neutral'
                overhead "unloading on shelf" 56 packanimal
                target packanimal
            endif
        endfor
    endif

    sysmsg '> Ressuplying From Storage Shelf...'
    menu storage_shelf 1

    waitforgump 3232825965 600

    wait 100
    if insysmsg 'You have exceeded your maximum item limit'
        sysmsg 'ITEM LIMIT REACHED. RESSUPLYING AGAIN TO GET ALL LOADOUT' 53
        menu shelf 1
    endif

    gumpclose 3232825965

    @removelist 'packies'
    @clearignore
    @clearsysmsg
    gumpclose
endif



if insysmsg 'disturbed'
    overhead 'disrupted after ress!'
    stop
endif



# ###############################
# # open door if one nearby (1 tile)
# ###############################

# if findtype 'wooden door' ground any any 1 as doorx
#     overhead 'clicking door...' 53
#     useobject doorx
#     stop
# elseif findtype 'metal door' ground any any 1 as doorx
#     overhead 'clicking door...' 53
#     useobject doorx
#     stop
# endif


###############################
# destroy caravans close to char
# before they spawn mobs
###############################

# if findtype 'status bar' ground any any 2 as caravan or findtype 28114 ground any any 2 as caravan
# if boating_mode == 0
#     if findtype 'status bar' ground any any 2 as bar
#         while findtype 'status bar' ground any any 2 as bar
#             overhead 'PUNCHING STATUS BAR' 90
#             dclick bar
#         endwhile
#     endif
# endif

###############################
# DESTROY TOKENS BOSSES DROP
# ON GROUND THAT SPAWNS ADS
###############################

# TYPES:
# hand that locks char mount petram mini statue graphic ttype 27782
# mount petram mini statue graphic ttype 13807
# ossuary boss tombstones - 29449
# shadowspire statues - 'REKT 5K Statue'

if findtype "13807|29449|'REKT 5K Statue'" ground any any 2 as found
    while find found ground any any 2
        guild 'STONE/TOMB/STATUE ON ME!!'
        sysmsg '*PUNCHING BOSS TRAP*' 91
        overhead '>>> TRAP! HIT! <<< ' 92 stone_serial

        dclick stone_serial
        wait 600

    endwhile

    if find found ground any any 20
        sysmsg '!!! STONE IN ROOM !!!' 39
        overhead '!! FOUND STONE !!' 39
        overhead '!!! STONE IN ROOM !!!' 39 stone_serial
    endif
endif






######################################
# IF NOTHING TO DO, AND NOT INSIDE ARENA,
# DROP BOTTLES TO LOOSE WEIGHT
######################################

if not find '0x40000ECB' ground any any 20
    if findtype 3854 'backpack' as bottles and counttype 3854 > 3
        overhead '✩ droping bottles ✩' 55
        wait action_delay
        lift bottles 999
        wait 50
        while queued
        endwhile

        droprelloc 1 1
        stop
    endif
endif


###########################################
# FROM DOWN HERE, ONLY NON IMPORTANT / NON VITAL ACTIONS
# THAT WONT HOLD DOWN THE CHAR
# IN CASE OF DANGER





# if skill 'chivalry' > 10
#     script 'loops\survival'
# endif
# hotkey "Set Last Target"
# overhead 'nothing to do...' 55
# script 'loops\survival'
