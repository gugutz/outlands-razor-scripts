###############################
# MUSHROOM
###############################

# set this variable according to the level of Create Food in your grimoire

if not timerexists 'use_mushroom_timer'
    sysmsg 'Creating mushroom timer...'
    createtimer 'use_mushroom_timer'
    settimer 'use_mushroom_timer' 60001
endif


if mana = maxmana
    overhead 'no need for mushroom!' 55
    stop
endif

if mana >= 70
    overhead 'over mana limit for mushroom'
endif

if mana < 70
    if timer 'use_mushroom_timer' < 60000
        overhead '! mushroom cooldown !' 55
        stop
    endif

    if not findtype 'mushroom' 'backpack'
        overhead 'no mushrooms!' 37
        while counttype 'mushroom' < 5 and mana > 11
            cast 'create food'
        endwhile
    else
        usetype 'mushroom' backpack
        wait 600
        # waitforsysmsg 'you consume' 600
        if insysmsg 'you consume'
            sysmsg '>>> Used mushroom! Resetting timer...' 55
            settimer 'use_mushroom_timer' 0
            clearsysmsg
            stop
        endif
    endif
endif


###############################
# MEDIDATE
###############################


if not timerexists 'medit_msg'
    createtimer 'medit_msg'
    settimer 'medit_msg' 0
endif

if mana == maxmana
    overhead 'already max mana'
    stop
endif

if findbuff 'actively meditating'
    overhead '*already meditating*' 55
    while findbuff 'actively meditating'
        if timer 'medit_msg' > 2000
            settimer 'medit_msg' 0
            overhead 'meditating...' 55
        endif
    endwhile
    stop
endif

while mana < maxmana and not findbuff 'actively meditating'
    skill 'meditation'
    waitforsysmsg 'You enter a meditative trance' 600
    if findbuff 'actively meditating' or insysmsg 'You enter a meditative trance'
        overhead '*begin meditating*' 90
        settimer 'medit_msg' 0
        while findbuff 'actively meditating'
            if timer 'medit_msg' > 2000
                settimer 'medit_msg' 0
                overhead 'meditating...' 55
            endif
        endwhile
    endif
endwhile
