##################################
# Recall Home
# v0.2
# by gugutz
##################################
#
# DESCRIPTION
#
# This script will help you recall to your home, by following a few rules:
#
#   1- It will always look first for a runetome, and if it cant find one, searches for a runebook
#   2- If you have recall regs on your backpack, it will cast recall and target the runetome/runebook
#   3- If you dont have recall regs on your backpack, it will open the runetome/runebook and use a charge to recall to the first rune
#
# PS: MAKE SURE YOUR HOME RUNE IS THE FIRST RUNE OF YOUR RUNETOME/RUNEBOOK
##################################


##################################
# CONFIGURATION
##################################

setvar! runetome_rune_btn_number 100
setvar! runebook_rune_btn_number 2


##################################
# SCRIPT VARIABLES
##################################

setvar! runetome_gump_id 167090027
setvar! runebook_gump_id 1551740969
setvar! recall_book_color 0

setvar! recall_travel_time 2800
setvar! use_scroll 0
setvar! use_charges 0


if not timerexists 'medit_msg_warning_timer'
    createtimer 'medit_msg_warning_timer'
endif


if not timerexists 'waiting_spell_timer'
    createtimer 'waiting_spell_timer'
    settimer 'waiting_spell_timer'  0
endif


#############################
# SET THE HOME BOOK
#############################


# search runetomes first
if not varexist recall_book
    while findtype 'runetome' backpack as found
        getlabel found rtome_label
        if 'home' in rtome_label or 'Home' in rtome_label or 'HOME' in rtome_label
            sysmsg 'FOUND HOME RUNETOME {{rtome}}' 54
            setvar! recall_book found
            setvar! recall_book_gump_id 167090027
            setvar! home_rune_btn_number runetome_rune_position

            setvar! recall_book rtome
            # setvar! recall_by_charge_btn_number runetome_rune_position
            setvar! recall_by_charge_btn_number 100

        endif

        @ignore found
    endwhile
endif
@clearignore

# if we couldnt find a home runetome, search for a home runebook
if not varexist recall_book
    while findtype 'runebook' backpack as found
        getlabel found rbook_label
        if 'home' in rbook_label or 'Home' in rbook_label or 'HOME' in rbook_label
            sysmsg 'FOUND HOME RUNEBOOK {{rtome}}' 54
            setvar! recall_book found
            setvar! recall_book_gump_id 1551740969
            setvar! home_rune_btn_number runetome_rune_position
            setvar! recall_by_charge_btn_number 2
        endif

        @ignore found
    endwhile
    @clearignore
endif

if not varexist recall_book
    sysmsg "YOU NEED A RUNEBOOK OR RUNETOME NAMED 'HOME' WITH YOUR HOME RUNE SET TO DEFAULT TO RUN THIS SCRIPT" 54
    stop
endif


###################################
# PACK ANIMALS
###################################


if not listexists 'packies'
    createlist 'packies'
endif
clearlist 'packies'

while findtype 292 ground any any 2 as packie
    if noto packie = friend
        pushlist 'packies' packie
    endif
    @ignore packie
endwhile

while findtype 291 ground any any 2 as packie
    if noto packie = friend
        pushlist 'packies' packie
    endif
    @ignore packie
endwhile
clearignore

##################################
# CODE
##################################





if followers > 0
    say 'all follow me'
endif

if mana < 11
    overhead '*no mana for recall*' 37


    while mana < 11 and not findbuff 'actively meditating'
        skill 'meditation'
        wait action_delay
    endwhile

    if mana < 11 and findbuff 'actively meditating'

        overhead '*begin meditating*' 90
        settimer 'medit_msg_warning_timer' 0

        while mana < 11 and findbuff 'actively meditating'
            if timer 'medit_msg_warning_timer' > 2000
                settimer 'medit_msg_warning_timer' 0
                overhead 'meditating...' 55
            endif
        endwhile
    endif

endif

# if findtype 'runebook' backpack or findtype 'runetome' backpack
if counttype "Black Pearl%s%" == 0 or counttype "Blood Moss" == 0 or counttype "Mandrake Root%s%" == 0
    if counttype "Recall" backpack
        overhead 'no regs. trying scroll...'
        setvar! use_scroll 1
    else
        overhead 'no regs. trying charges...'
        setvar! use_charges 1
    endif
endif

# pick which book should script recall to
# if findtype 'runetome' backpack recall_book_color as rtome
#     setvar! recall_book rtome
#     setvar! recall_book_gump_id 167090027
#     # setvar! recall_by_charge_btn_number runetome_rune_position
#     setvar! recall_by_charge_btn_number 100
# elseif findtype 'runebook' backpack recall_book_color as rbook
#     setvar! recall_book rbook
#     setvar! recall_book_gump_id 1551740969
#     # setvar! recall_by_charge_btn_number runebook_rune_position
#     setvar! recall_by_charge_btn_number 2
# endif


if use_charges == 1 and gumpexists recall_book_gump_id
    # waitforgump recall_book_gump_id action_delay
    gumpresponse recall_by_charge_btn_number recall_book_gump_id
    stop
endif

if use_charges == 0
    sysmsg '>> Recalling Home via Spell...' 56
    overhead '*going home using spell*' 56
    setvar! use_charges 0

    clearsysmsg
    cast 'Recall'
    hotkey 'Clear Target Queue'

    @settimer 'waiting_spell_timer' 0
    while not targetexists 'neutral' and timer 'waiting_spell_timer' < 3500
        wait 100

        if insysmsg 'You have recently committed an aggressive action and must wait'
            overhead "AGGRESSION CD! REPLAYING..." 54
            replay
        endif

        if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
            sysmsg 'BREAKING WHILE WAITING FOR SPELL'
            wait 150
            break
        endif

        if targetexists 'harmful'
            # sysmsg 'reseting spell timer 1...'
            settimer 'waiting_spell_timer' 0
            break
        endif
    endwhile

    target recall_book
else
    sysmsg '>> Recalling Home via Charges...' 56
    overhead '*going home using charges*' 56
    dclick recall_book
    waitforgump recall_book_gump_id action_delay

    if insysmsg 'You have recently committed an aggressive action and must wait'
        overhead "AGGRESSION CD! REPLAYING..." 54
        replay
    endif

    gumpresponse recall_by_charge_btn_number recall_book_gump_id
endif

wait recall_travel_time
sysmsg 'FINISHED RECALLING...' 54


#################################
# RESTOCK RESOURCE STOCKPILE AFTER RECALLING
#################################

if findtype 'resource container' ground any any 2 as stockpile
    menu stockpile 0
    wft action_delay
    if targetexists 'neutral'
        if verbose_mode  == 1
            sysmsg '> Restocking Resource Stockpile...' sysmsgs_color
        endif

        target 'self'
    endif

    # restock stockpile from packies
    if list 'packies' > 0
        foreach packanimal in 'packies'
            if noto packanimal = noto 'self'
                if verbose_mode  == 1
                    sysmsg '> Restocking Stockpile From Pack Animal...' sysmsgs_color
                endif

                menu stockpile 0
                wft 600
                if targetexists 'neutral'
                    overhead "unloading on stockpile" 56 packanimal
                    target packanimal
                endif
            endif
        endfor
    endif
    if gumpexists 1859005118
        gumpclose 1859005118
    endif
endif
