# FISHING v0.1.0
# by gugutz

clearsysmsg

@setvar! fishing_skill_cooldown 10000
setvar! success_msg_color 65
setvar! warning_msg_color 54
setvar! dockmaster_gump_id 3287496917
setvar! action_delay 600
setvar! deposit_catch_delay 60000
setvar! auto_dock_when_attacked 1

if not timerexists fishing_timer
    createtimer fishing_timer
    settimer fishing_timer fishing_skill_cooldown
endif

if not timerexists deposit_catch_timer
    createtimer deposit_catch_timer
    settimer deposit_catch_timer deposit_catch_delay
endif

cooldown FISHING fishing_skill_cooldown

if not cooldown FISHING
    overhead "COOLDOWN NOT FOUND. READ INSTRUCTIONS!" 34
    sysmsg "The required cooldown was not found set in this client." 34
    sysmsg "Please setup a cooldown named 'FISHING', with a 10s default timer and the following triggers:" 34
    sysmsg "Add these with 10 second cooldown" 66
    sysmsg "sysmsg: fish"
    sysmsg "sysmsg: You retrieve a"
    sysmsg "sysmsg: Harvesting is not allowed in this area"
    sysmsg "sysmsg: before you may begin harvesting"
    sysmsg "overhead: You catch"
    sysmsg "overhead: Captcha successful"
    stop
endif



if not listexists 'dockmasters_serials'
    createlist 'dockmasters_serials'
    clearlist 'dockmasters_serials'
    # shelter docks Uriah the Dockmaster
    pushlist 'dockmasters_serials' 0x11A32F
    # Horseshoe Bay Beaumont the Dockmaster
    pushlist 'dockmasters_serials' 0x80600
    # Prevalia Kisa the Dockmaster
    pushlist 'dockmasters_serials' 0x80788
    # Prevalia Vanda the Dockmaster
    pushlist 'dockmasters_serials' 0x80786
    # Prevalia Meta the Dockmaster
    pushlist 'dockmasters_serials' 0x80782
    # Ivar the Dockmaster (GH)
    pushlist 'dockmasters_serials' 0x19E66B
    # Calandra Granite Docks dockmaster
    pushlist 'dockmasters_serials' 0x54B3
    # Winona Terran Dockmaster
    pushlist 'dockmasters_serials' 0x805EF
    # Marvin Terran Dockmaster
    pushlist 'dockmasters_serials' 0xA3F
    # Winona Terran Dockmaster
    pushlist 'dockmasters_serials' 0x805EF
    # Keane Winter Harbor dockmaster
    pushlist 'dockmasters_serials' 0x5A40
    # a dockmaster from the void (NW corner of the map in a floating concrete platform)
    pushlist 'dockmasters_serials' 0x26290
endif





overhead "Target organizer 20 hotbag"
organizer 20 set
while not insysmsg "Container set successfully"
    wait 250
endwhile


while not dead

    # first make sure its the chars boat, and not a friend boat
    if auto_dock_when_attacked == 1
        if gumpexists dockmaster_gump_id
            if diffhits > 0
                # if it is, check full hull hp and see if it matches chat boat hull (EDIT HERE)
                sysmsg '>>> UNDER ATTACK! DOCKING SHIP!' warning_msg_color
                gumpresponse 7
                waitforgump dockmaster_gump_id action_delay
                waitforgump 4216593270
                waitforgump dockmaster_gump_id action_delay
            endif
        else
            sysmsg 'AUTO DOCK ENABLED BUT DOCKMASTER GUMP NOT FOUND' 39
        endif
    endif

    if lhandempty
        if findtype "fishing pole" backpack as item
            lift item
            wait action_delay
            drop self lefthand
            wait action_delay
        else
            overhead "Out of poles!" 34
            stop
        endif
    endif


    if timer fishing_timer >= fishing_skill_cooldown
        if cooldown FISHING
            hotkey 'Use item in hand'
            wait action_delay
            settimer fishing_timer 0
            cooldown FISHING 0
        else
            if not cooldown FISHING
                overhead 'Captcha break!' 34
                overhead 'Awaiting Captcha...' 34
                wait 1000
                if insysmsg 'Captcha successful'
                    overhead 'Success - continue on!' 88
                endif
            endif
        endif
    endif

    # save catches and special fishing nets in the hatch
    if timer 'deposit_catch_timer' > deposit_catch_delay
        if findtype 'Crab SOUTH' backpack or findtype 'fishing net' backpack 2814 or findtype 'cooked fish' backpack
            sysmsg 'DEPOSITING CATCH IN HATCH' 54
            organizer 20
            wait 250

            while queued
                wait 50
            endwhile

            settimer 'deposit_catch_timer' 0
        endif
    endif

    if insysmsg 'You retrieve a special fishing net'
        sysmsg 'FISHING NET!' 90
        settimer 'fishing_timer' fishing_skill_cooldown
    elseif insysmsg 'You have recently traveled and must wait'
        overhead '! travel harvest cooldown !' 43
        settimer 'fishing_timer' fishing_skill_cooldown
    elseif insysmsg 'you catch'
        settimer 'fishing_timer' fishing_skill_cooldown
    endif

endwhile
