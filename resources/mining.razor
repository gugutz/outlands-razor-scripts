sysmsg '#########################' 90
sysmsg '>>> MINING BOT' 55
sysmsg 'by gugutz' 45
sysmsg 'version 0.0.7' 31
sysmsg '########################' 90

#################################
# CHANGELOG
#################################
#
# v0.0.7
# - Adds missing variables containing rune positions in runetome and runebook
#
# v0.0.6
# - Adds survival routine to defend char against closed-ranged combat creatures
# - Adds sysmsg check to see if char got far from forge while inside the smelting loop
#
# v0.0.5
# - Stops double clicking packie to open packpack when unloading ores while heavy
# - Adds wait timer after recalling and before unloading when getting home
#
# v0.0.4
# - Adds setting to choose if script will auto smelt ores when close to forge
#
# v0.0.3
# - Makes sysmsgs checks even when gump is detected (so char can escape PKs even when answering gumps)
#
# v0.0.2
# - Adds checks for several map-forges around the world
# - Adds option to not smelt colored ores (makes it hard for PKS to take them)
#
# v0.0.1
# - Initial release


#################################
# CREATE ALL LISTS FOR CONFIG
#################################

sysmsg 'Creating script lists'

if not listexists 'mining_config_lists'
    createlist 'mining_config_lists'
else
    clearlist 'mining_config_lists'
endif

pushlist 'mining_config_lists' 'recall_to_escape_pks'
pushlist 'mining_config_lists' 'auto_smelt_ores_when_close_to_forges'
pushlist 'mining_config_lists' 'dont_smelt_colored_ores'
pushlist 'mining_config_lists' 'auto_walk_randomly'
pushlist 'mining_config_lists' 'auto_walk_to_direction'
pushlist 'mining_config_lists' 'direction_to_walk'

# routine-related lists
pushlist 'mining_config_lists' 'packies'
pushlist 'mining_config_lists' 'pack_heavy'
pushlist 'mining_config_lists' 'full_packies'
pushlist 'mining_config_lists' 'reset_last_sysmsg_timer'
pushlist 'mining_config_lists' 'smelt_ores_now'
pushlist 'mining_config_lists' 'smelt_this_ore'
pushlist 'mining_config_lists' 'use_charges'
pushlist 'mining_config_lists' 'use_recall_scroll'

foreach list in 'mining_config_lists'
    if not listexists list
        createlist list
    else
        clearlist list
    endif
endfor


###################################
# SCRIPT CONFIGURATION
###################################

sysmsg 'Applying configuration...'

@setvar action_delay_var 600
# wait this much in milliseconds for sysmsgs to appear
# if we dont get any sysmsg for this much time, we probably got a gump
@setvar max_wait_for_last_sysmsg_to_use_pickaxe  1000
@setvar max_wait_for_last_sysmsg_to_suspect_gump 7000
@setvar max_ores_in_packie 225

@setvar max_diffhits_before_healing_with_potion 20
@setvar max_diffhits_before_healing_with_bandages 10
@setvar max_diffhits_before_healing_with_magery 20

@setvar max_diffweight_before_unloading 50

# rune positions in either runetome or runebook
@setvar runetome_rune_position 100
@setvar runebook_rune_position 2

pushlist 'auto_walk_randomly' 'no'
pushlist 'auto_walk_to_direction' 'no'
pushlist 'auto_smelt_ores_when_close_to_forges' 'yes'
pushlist 'direction_to_walk' 'right'

pushlist 'recall_to_escape_pks' 'yes'


# PRINT CONFIGURATION
# foreach setting_list in 'mining_config_lists'
#     sysmsg "interpol {{setting_list}}"
#     if atlist setting_list 0 as value
#         sysmsg "{{setting_list}}: {{setting_value}}"
#     endif
# endfor


###################################
# TIMERS
###################################

sysmsg 'Creating timers...'

createlist 'mining_script_timers'
pushlist 'mining_script_timers' 'waiting_save'
pushlist 'mining_script_timers' 'last_sysmsg_timer'
pushlist 'mining_script_timers' 'waiting_target_timer'
pushlist 'mining_script_timers' 'bandage_timer'

foreach timer in 'mining_script_timers'
    if not timerexists timer
        createtimer timer
        settimer timer 0
    endif
endfor


###################################
# PACK ANIMALS
###################################

sysmsg 'Searching for pack animals...' 87

while findtype 292 ground any any 2 as packie
    if noto packie = friend
        pushlist 'packies' packie
        overhead '*opening bag*' 56 packie
        # dclick packie
        # wait action_delay_var
    endif
    @ignore packie
endwhile

while findtype 291 ground any any 2 as packie
    if noto packie = friend
        pushlist 'packies' packie
        overhead '*opening bag*' 56 packie
        # dclick packie
        # wait action_delay_var
    endif
    @ignore packie
endwhile
clearignore


###################################
# PREPARING TO START
###################################

# set bandage_delay based on char dex
if skill 'healing' >= 50
    if dex >= 100
        sysmsg 'Setting bandage_delay to 10s' 53
        setvar bandage_delay 10000
    elseif dex <= 80
        sysmsg 'Setting bandage_delay to 14s' 53
        setvar bandage_delay 14000
    endif
endif

if list 'packies' >= 1
    say 'all follow me'
endif

if list 'packies' == 0
    overhead '!COULDNT FIND ANY PACKIES!' 43
    overhead '!COULDNT FIND ANY PACKIES!' 43
    sysmsg '!COULDNT FIND ANY PACKIES!' 43
    sysmsg '!COULDNT FIND ANY PACKIES!' 43
endif

# check and equip pickaxe if needed before starting so we dont waist the main timer equiping the axe
# which was causing the timer to be greater then the mining value and the char wouldnt mine when that happened
#
if not rhandempty and not findtype 'pickaxe' righthand
    overhead 'undressing hands for pickaxe'
    hotkey "Undress Both Hands"
    wait action_delay_var
endif

if rhandempty
    if findtype 'pickaxe' backpack
        dclicktype 'pickaxe'
        overhead "✩equipped new pickaxe✩"
    else
        overhead "✩ You are out of pickaxes✩"
        stop
    endif
endif

###################################
# TRACKING - BEGIN HUNTING
#
setvar tracking_gump_id 4267467659

# if not findbuff 'Tracking'
while not gumpexists tracking_gump_id
    sysmsg 'Opening tracking gump...' 53
    skill 'tracking'
    waitforgump tracking_gump_id 600
endwhile

if gumpexists tracking_gump_id
    if not ingump 'Stop Hunting' 4267467659
        overhead 'STARTING HUNTING' 65
        gumpresponse 6
        gumpclose
    else
        overhead 'already hunting' 55
    endif
endif
# endif



###################################
sysmsg 'Starting...' 19
wait action_delay_var

clearsysmsg
settimer 'last_sysmsg_timer' 0

while not dead 'self'

    if timer 'last_sysmsg_timer' > max_wait_for_last_sysmsg_to_suspect_gump
        overhead "✩ GUMP or LAG ✩" 33
    endif


    if timer 'last_sysmsg_timer' < max_wait_for_last_sysmsg_to_suspect_gump
        # check and equip pickaxe if needed
        if rhandempty
            if findtype 'pickaxe' true
                dclicktype 'pickaxe'
                overhead "✩equipped new pickaxe✩"
            else
                overhead "✩ You are out of pickaxes✩"
                stop
            endif
        endif

        # mine
        if timer 'last_sysmsg_timer' < max_wait_for_last_sysmsg_to_use_pickaxe and not rhandempty and diffweight > 10
            # sysmsg '>>> Mining... ' 87
            hotkey 'Use item in Hand'
            wait max_wait_for_last_sysmsg_to_use_pickaxe
        endif

        #############################################
        # SMELTING ROUTINE
        #
        # smelt if close to a forge

        if inlist 'auto_smelt_ores_when_close_to_forges' 'yes'
            if findtype 'forge' ground any any 2 as forg
                pushlist 'smelt_ores_now' 'yes'
                overhead 'debug 1113'
            elseif findtype 0x1996 ground any any 2 as forg
                pushlist 'smelt_ores_now' 'yes'
                overhead 'debug 1223'

            #
            # shelter island forge
            elseif position 1979 2554
                pushlist 'smelt_ores_now' 'yes'
            # mining cave near andaria
            elseif position 1916 744 or position 1916 743
                pushlist 'smelt_ores_now' 'yes'
            # placeholder
            elseif position 1916 744 or position 1916 743
                pushlist 'smelt_ores_now' 'yes'
            # brigand camp 2 mine 1, near cambria
            elseif position 1531 2708 or position 1530 2709 or position 1532 2710
                pushlist 'smelt_ores_now' 'yes'
            elseif position 1524 2699 or position 1525 2698
                pushlist 'smelt_ores_now' 'yes'
            # brigand camp 2 mine 2, near cambria
            elseif position 1560 2670 or position 1561 2672
                pushlist 'smelt_ores_now' 'yes'
            elseif position 1564 2654 or position 1563 2653
                pushlist 'smelt_ores_now' 'yes'
            # necromancers swamp nearby mines
            elseif position 1916 713 or position 1916 714 or position 1915 715
                pushlist 'smelt_ores_now' 'yes'
            elseif position 1935 700 or position 1937 700 or position 1937 699
                pushlist 'smelt_ores_now' 'yes'
            elseif position 1889 739 or position 1889 740 or position 1888 740
                pushlist 'smelt_ores_now' 'yes'
            # orc fort mines (near orc fort / honor shrine)
            elseif position 1194 1066 or position 1194 1065 or position 1193 1065
                pushlist 'smelt_ores_now' 'yes'
            elseif position 1340 1189 or position 1340 1188 or position 1339 1188
                pushlist 'smelt_ores_now' 'yes'

            endif

            if inlist 'smelt_ores_now' 'yes'
                clearlist 'smelt_ores_now'
                overhead 'close to forge!'

                # smelt ores in backpack
                while findtype 6585 backpack as ores
                    if insysmsg 'You do not see any nearby forges to smelt this ore on'
                        overhead 'got far from forge!' 41
                        sysmsg 'Got Far From Forge! Breaking smelting loop...' 41
                        break
                    endif

                    if inlist 'dont_smelt_colored_ores' 'yes'
                        if hue ores == 0
                            pushlist 'smelt_this_ore' 'yes'
                        endif
                    else
                        pushlist 'smelt_this_ore' 'yes'
                    endif

                    if inlist 'smelt_this_ore' 'yes'
                        clearlist 'smelt_this_ore'
                        overhead 'smelting!'
                        dclick ores
                        wait action_delay_var
                        pushlist 'reset_last_sysmsg_timer' 'yes'
                    endif
                endwhile

                # smelt ores in pack animals
                foreach packanimal in 'packies'
                    dclick packanimal
                    wait action_delay_var

                    if findtype 6585 packanimal as ores and diffweight > 10
                        overhead 'smelting ores in animal' 56 packanimal
                        while findtype 6585 packanimal as ores and diffweight > 10
                            if insysmsg 'You do not see any nearby forges to smelt this ore on'
                                overhead 'got far from forge!' 41
                                sysmsg 'Got Far From Forge! Breaking smelting loop...' 41
                                break
                            endif

                            if inlist 'dont_smelt_colored_ores' 'yes'
                                if hue ores == 0
                                    pushlist 'smelt_this_ore' 'yes'
                                endif
                            else
                                pushlist 'smelt_this_ore' 'yes'
                            endif

                            if inlist 'smelt_this_ore' 'yes'
                                clearlist 'smelt_this_ore'
                                sysmsg 'smelting ore in packie' 56
                                overhead 'smelting ores' 56 packanimal
                                dclick ores
                                wait action_delay_var
                                pushlist 'reset_last_sysmsg_timer' 'yes'
                            endif
                        endwhile
                    endif
                endfor
            endif
        endif

        ##############################################
        # GRAB ORES FROM GROUND
        #
        while findtype 6585 ground any any 1 as ore and diffweight > 50
            hotkey 'Grab Item'
            wft action_delay_var
            if targetexists 'neutral'
                sysmsg 'Getting ore from ground' 51
                overhead '* ore from ground *' 51
                target ore
            endif
            pushlist 'reset_last_sysmsg_timer' 'yes'
            # settimer 'last_sysmsg_timer' 0
        endwhile


        ##############################################
        # UNLOADING ROUTINE
        # if heavy and there are packies nearby, unload
        if diffweight <= max_diffweight_before_unloading and findtype 6585 backpack

            if list 'packies' > 0
                foreach packanimal in 'packies'

                    clearlist 'pack_heavy'
                    # check packie weight to determine if its full or not
                    if counttype 6585 packanimal >= max_ores_in_packie
                        pushlist 'pack_heavy' 'yes'
                    elseif counttype 6585 packanimal >= 100 and counttype 7154 >= 500
                        sysmsg 'debug 001324'
                        pushlist 'pack_heavy' 'yes'
                    endif

                    # add or remove packie to full 0packies list based on if its heavy or not
                    if inlist 'pack_heavy' 'yes'
                        if not inlist 'full_packies' packanimal
                            overhead 'adding to full list' 54 packanimal
                            pushlist 'full_packies' packanimal
                        endif
                        # sysmsg 'PACKIE HAS MORE THAN 100 ORES' 40
                        # overhead 'PACKIE HAS MORE THAN 100 ORES' 40
                        overhead '! heavy !' 34 packanimal
                    else
                        if inlist 'full_packies' packanimal
                            overhead 'removing from full list' 52 packanimal
                            poplist 'full_packies' packanimal
                        endif
                    endif

                    if not inlist 'full_packies' packanimal
                        # getlabel packanimal packie_label
                        # sysmsg packie_label 99


                        # call packies if they are not within 2 tiles
                        while not find packanimal ground any any 2
                            say 'all follow me'
                            wait action_delay_var
                        endwhile

                        # dclick packanimal
                        # wait action_delay_var
                        # if this pet is already full, we move to the next one

                        while findtype 6585 backpack as ores and counttype 6585 packanimal < max_ores_in_packie
                            sysmsg '>>> MOVING ORES TO PACKIE' 56
                            overhead '*unloading ores*' 56
                            overhead '*unloading ores*' 56 packanimal
                            lift ores 999
                            wait action_delay_var
                            drop packanimal -1 -1 0
                            wait action_delay_var
                            if findtype 6585 ground any any 1
                                # put this animal to the end of the list so we use the next one next time
                                overhead 'heavy?? moving to end of list' 53 packanimal
                                sysmsg 'Found ore on ground. Pet is probably heavy... Moving To End Of List' 53
                                pushlist 'full_packies' packanimal
                            endif

                        endwhile

                        # ingots. this isnt pretty, its duplicated from above. ill refactor it later
                        while findtype 7154 backpack as ingots and counttype 6585 packanimal < 200
                            sysmsg '>>> MOVING INGOTS TO PACKIE' 56
                            overhead '*unloading ingots*' 56
                            overhead '*unloading ingots*' 56 packanimal
                            lift ingots 999
                            wait action_delay_var
                            drop packanimal -1 -1 0
                            wait action_delay_var
                            if findtype 7154 ground any any 1
                                # put this animal to the end of the list so we use the next one next time
                                overhead 'heavy?? moving to end of list' 53 packanimal
                                sysmsg 'Found ingots on ground. Pet is probably heavy... Moving To End Of List' 53
                                pushlist 'full_packies' packanimal
                            endif

                        endwhile


                    endif
                endfor

                pushlist 'reset_last_sysmsg_timer' 'yes'
                # settimer 'last_sysmsg_timer' 0

            else
                overhead '! HEAVY !' 90
            endif
        endif
    endif


    ###############################################
    # JOURNAL MESSAGES CHECKS

    # we need to first check for pks or else the other msgs might not allow us to get to this check
    if insysmsg 'now tracking'
            sysmsg '>>> PK...' 54
            sysmsg '>>> PK...' 54
            sysmsg '>>> PK...' 54
            overhead '!! PK !!' 54
            overhead '!! PK !!' 54
            overhead '!! PK !!' 54

        if inlist 'recall_to_escape_pks' 'yes'
            sysmsg '>>> ESCAPING PK!!!' 41
            overhead '!! ESCAPING PK !!' 41

            if skill 'magery' == 0 or counttype "Black Pearl%s%" backpack == 0 or counttype "Blood Moss" backpack == 0 or counttype "Mandrake Root%s%" backpack == 0
                if counttype "Recall" backpack
                    overhead 'no regs. trying scroll...'
                    pushlist 'use_recall_scroll' 'yes'
                else
                    overhead 'no regs. trying charges...'
                    pushlist 'use_charges' 'yes'
                endif
            endif

            if findtype 'runetome' backpack as rtome
                if not inlist 'use_charges' 'yes'
                    hotkey 'Recall'
                    waitfortarget 3500
                    target rtome
                elseif inlist 'use_charges' 'yes'
                    dclick rtome
                    waitforgump 167090027 600
                    gumpresponse runetome_rune_position
                endif
            elseif findtype 'runebook' backpack as rbook
                if not inlist 'use_charges' 'yes'
                    hotkey 'Recall'
                    waitfortarget 3500
                    target rbook
                elseif inlist 'use_charges' 'yes'
                    dclick rbook
                    waitforgump 167090027 600
                    gumpresponse runebook_rune_position
                endif
            endif


            wait 2500


            # after escaping, smelt ores from char
            while findtype 6585 backpack as ores_from_backpack
                if findtype 'forge' ground any any 2 or findtype 'large forge' ground any any 2
                    overhead '*smelting ores*' 49 packanimal
                    sysmsg '> Smelting Ores In Pack Animal' 49
                    dclick ores_from_backpack
                    wait object_delay
                else
                    sysmsg 'Found Ores in backpack but no forge nearby to smelt it...' 40
                    break
                endif
            endwhile


            # after escaping, smelt ores from all packies
            if list 'packies' > 0
                foreach packanimal in 'packies'

                    dclick packanimal
                    wait action_delay_var
                    # smelt ores
                    while findtype 6585 packanimal any any 2 as ores_from_packie
                        if findtype 'forge' ground any any 2 or findtype 'large forge' ground any any 2
                            overhead '*smelting ores*' 49 packanimal
                            sysmsg '> Smelting Ores In Pack Animal' 49
                            dclick ores_from_packie
                            wait object_delay
                        else
                            sysmsg 'Found Ores in pack animal but no forge nearby to smelt it...' 40
                            overhead 'found ores but no forge' 40 packanimal
                            break
                        endif
                    endwhile
                endfor
            endif


            #
            # ##########################
            # RESTOCK RESOURCE STOCKPILE
            if findtype 'resource container' ground any any 2 as stockpile
                @setvar resource_stockpile stockpile
                menu resource_stockpile 0
                wft 600
                if targetexists 'neutral'
                    sysmsg '> Restocking Resource Stockpile...' sysmsgs_color
                    target 'self'
                endif

                # restock stockpile from packies
                if list 'packies' > 0 and varexist resource_stockpile
                    foreach packanimal in 'packies'
                        if noto packanimal = friend
                            sysmsg '> Restocking Stockpile From Pack Animal...' sysmsgs_color
                            menu resource_stockpile 0
                            wft 600
                            if targetexists 'neutral'
                                overhead "unloading on stockpile" 56 packanimal
                                target packanimal
                            endif
                        endif
                    endfor
                endif
                if gumpexists 1859005118
                    gumpclose
                endif
            endif


            stop
        endif



    # handle world saves
    elseif insysmsg "The world is saving"
        overhead '*WORLD SAVING*' 90
        while not insysmsg 'World save complete'
            wait 100
            if timer 'waiting_save' >= 5000
                sysmsg 'BREAKING WAITING FOR SAVE' 49
                break
            endif
        endwhile

        if insysmsg 'World save complete'
            overhead '*save complete!*' 90
            pushlist 'reset_last_sysmsg_timer' 'yes'
        endif



    # no resources where char is
    elseif insysmsg "You do not see any harvestable resources nearby"
        if inlist 'auto_walk_to_direction' 'yes'
            foreach direction in 'direction_to_walk'
                walk direction
                break
            endfor
        endif

        pushlist 'reset_last_sysmsg_timer' 'yes'
        overhead '| NO ORES HERE |' 54

        if inlist 'auto_walk_randomly' 'yes'
            random 8
            if insysmsg 'Random: 1'
                walk 'south'
                walk 'south'
                walk 'south'
            elseif insysmsg  'Random: 2'
                walk 'down'
                walk 'down'
                walk 'down'
            elseif insysmsg  'Random: 3'
                walk 'east'
                walk 'east'
                walk 'east'
            elseif insysmsg  'Random: 4'
                walk 'right'
                walk 'right'
                walk 'right'
            elseif insysmsg  'Random: 5'
                walk 'north'    
                walk 'north'
                walk 'north'
            elseif insysmsg  'Random: 6'
                walk 'up'
                walk 'up'
                walk 'up'
            elseif insysmsg  'Random: 7'
                walk 'west'
                walk 'west'
                walk 'west'
            elseif insysmsg  'Random: 8'
                walk 'left'
                walk 'left'
                walk 'left'
            endif
        endif


    # mining fail message
    elseif insysmsg "loosen some rocks but fail"
        sysmsg 'PEGOU LOOSEN SOME ROCKS'
        pushlist 'reset_last_sysmsg_timer' 'yes'
        overhead ': fail :' 43


    # when char is on cooldown for having traveled recently
    elseif insysmsg "You have recently traveled"
        pushlist 'reset_last_sysmsg_timer' 'yes'
        overhead '! travel harvest cooldown !' 43


    # action cooldown
    elseif insysmsg "You must wait to perform another action"
        pushlist 'reset_last_sysmsg_timer' 'yes'
        overhead '! action cooldown !' 54


    # action cooldowns (when sysmsg prints 'You must wait...') will break the macro, so we need to get them as well
    elseif insysmsg "Harvesting is not allowed in this area"
        pushlist 'reset_last_sysmsg_timer' 'yes'
        overhead 'harvest not allowed. move!' 53


    # I TRIED LOOPING THROUGH A LIST INSTEAD OF USING MULTIPLE IF/ELSE AND DIDNT WORK, THE LOOP BREAKS THE MACRO
    elseif insysmsg 'Iron'
        overhead '⥏ iron ⥑' 15
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Dull Copper'
        overhead '✩ dull cooper ✩' 245
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'shadow Iron'
        overhead '✩ shadow iron ✩' 193
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Copper'
        overhead '✩ copper ✩' 248
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Bronze'
        overhead '✩ bronze ✩' 248
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Gold'
        overhead '✩ gold ✩' 53
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Agapite'
        overhead '✩ agapite ✩' 58
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Verite'
        overhead '✩ verite ✩' 70
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Valorite'
        overhead '✩ valorite ✩' 80
        pushlist 'reset_last_sysmsg_timer' 'yes'
    elseif insysmsg 'Avarite'
        overhead '✩ avarite ✩' 17
        pushlist 'reset_last_sysmsg_timer' 'yes'


    # catch captcha successful msgs
    elseif insysmsg "Captcha successful"
        overhead 'captcha anwsered succesfully' 90
        sysmsg 'Captcha Anwsered Succesfully!' 90


    # catch captcha failure msgs
    elseif insysmsg "Due to previous captcha failures"
        overhead '! resource locked !' 33
        sysmsg 'CAPTCHA FAILED AND CHAR IS RESOURCE LOCKED. STOPPING MACRO...' 33
        stop


    endif


    # HANDLE POISON
    if poisoned
        if findtype 'orange potion' backpack
            sysmsg 'Curing poison with potion...' 90
            hotkey 'Drink Cure'
        elseif magery >= 50 and counttype 'Garlic' > 0 and counttype 'Ginseng' > 0
            sysmsg 'Curing poison with magery...' 90
            cast 'Cure'

            @settimer 'waiting_target_timer' 0
            while not targetexists 'beneficial' and timer 'waiting_target_timer' < 1500
                wait 100
                if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
                    sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                    break
                endif
            endwhile

            if targetexists 'beneficial'
                target 'self'
            endif
        elseif healing >= 50 and counttype 'clean bandage%s%' > 0 and timer 'bandage_timer' >= bandage_delay
            settimer 'bandage_timer' 0
            hotkey 'Bandage Self'
        endif
    endif

    # MAINTAIN CHAR HP
    if diffhits > 0
        # heal with pots
        if diffhits > max_diffhits_before_healing_with_potion
            if findtype 'yellow potion' backpack
                hotkey 'Drink Heal'
                wait action_delay_var
            endif
        endif

        # heal with healing
        if diffhits > max_diffhits_before_healing_with_bandages and skill 'healing' >= 50 and timer 'bandage_timer' >= bandage_delay
            settimer 'bandage_timer' 0
            hotkey 'Bandage Self'
        # heal with magery
        elseif diffhits > max_diffhits_before_healing_with_magery and skill 'magery' >= 50 and mana >= 11
            cast 'greater heal'

            @settimer 'waiting_target_timer' 0
            while not targetexists 'beneficial' and timer 'waiting_target_timer' < 3500
                wait 100
                if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
                    sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                    break
                endif
            endwhile

            if targetexists 'beneficial'
                hotkey 'Target Self'
            endif
        endif
    endif

    # DEFEND AGAINST MONSTERS ROUTINE
    if diffhits > 0
        hotkey 'Next Non-Friendly Monster Target'
        wait action_delay_var
        settimer 'bandage_timer' bandage_delay

        while find 'last' ground any any 2 and not dead 'last'

            # equip weapon if needed
            if skill 'swordsmanship' >= 50 and lhandempty
                if not findtype 'hatchet' backpack
                    overhead ":: You are out of hatchets::"
                elseif findtype 'hatchet' backpack as weap
                    dclick weap
                    overhead "::equipped swords weapon::"
                    wait action_delay_var
                endif
            elseif skill 'mace fighting' >= 50 and rhandempty
                if not findtype 'club' backpack and not findtype 'war mace' backpack
                    overhead ":: You are out of macing weapons::"
                elseif findtype 'war mace' backpack as weap or findtype 'club' backpack as weap
                    dclick weap
                    overhead "::equipped macing weapon::"
                    wait action_delay_var
                endif
            endif

            if not rhandempty or not lhandempty
                sysmsg 'Attacking last target'
                hotkey 'Attack Last Target'
            endif

            # MAINTAIN CHAR HP
            if diffhits > 0
                # heal with pots
                if diffhits > max_diffhits_before_healing_with_potion
                    if findtype 'yellow potion' backpack
                        hotkey 'Drink Heal'
                        wait action_delay_var
                    endif
                endif

                # heal with healing
                if diffhits > max_diffhits_before_healing_with_bandages and skill 'healing' >= 50 and timer 'bandage_timer' >= bandage_delay
                    settimer 'bandage_timer' 0
                    hotkey 'Bandage Self'
                # heal with magery
                elseif diffhits > max_diffhits_before_healing_with_magery and skill 'magery' >= 50 and mana >= 11
                    cast 'greater heal'

                    @settimer 'waiting_target_timer' 0
                    while not targetexists 'beneficial' and timer 'waiting_target_timer' < 3500
                        wait 100
                        if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
                            sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                            break
                        endif
                    endwhile

                    if targetexists 'beneficial'
                        hotkey 'Target Self'
                    endif
                endif
            endif

            if diffhits == 0 or insysmsg 'You finish applying the bandages' and timer 'bandage_timer' >= bandage_delay
                sysmsg 'RESETING BANDAGE TIMER'
                settimer 'bandage_timer' bandage_delay
            endif

        endwhile
    endif

    # check if we got any sysmsg that should reset the timer in each iteration
    if inlist 'reset_last_sysmsg_timer' 'yes'
        clearlist 'reset_last_sysmsg_timer'
        # RESET THE TIMER
        settimer 'last_sysmsg_timer' 0
        clearsysmsg
    endif


endwhile
