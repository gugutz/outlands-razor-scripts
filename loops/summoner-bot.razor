sysmsg '#########################' 90
overhead 'ðŸ¤– SUMMONER BOT ðŸ¤–' 55
overhead 'by gugutz' 55
overhead 'version 1.0' 56
sysmsg '>>> SUMMONER BOT' 55
sysmsg 'by gugutz' 55
sysmsg 'version 1.0' 56
sysmsg '########################' 90

if not varexist summoner_bot_first_run_done
    setvar! summoner_bot_first_run_done true


    sysmsg '>>> Setting Pack Animal Unload Configuration...' 90
    setvar! move_gold true
    setvar! move_resources true
    setvar! move_gems true
    setvar! move_food true
    setvar! move_reagents true
    setvar! min_reagents_to_leave_on_char 160
    setvar! amount_of_regs_to_move 40


    sysmsg '>>> Setting General Configuration...' 90
    setvar! debug_mode true
    setvar! use_script_overhead_messages true
    setvar! auto_get_new_targets true
    # setvar! get_humanoids_on_auto_target true
    setvar! kill_close_to_loot true
    setvar! auto_attack_targets true
    setvar! auto_summon true
    setvar! auto_spirit_speak true
    setvar! auto_heal true
    setvar! auto_cure_poison true
    setvar! auto_meditate true
    setvar! auto_skin_corpses true
    setvar! auto_all_guard true
    setvar! auto_escape_pks true
    setvar! auto_unload_items_in_pack_animal true
    setvar! auto_unload_reagents_in_pack_animal true


    sysmsg '>>> Setting Necromancer Related Configuration...' 90
    setvar! auto_vampiric_embrace true


    sysmsg '>>> Setting Skill Buffs Configuration...' 90
    # SKILL BUFFS
    setvar! auto_campfire_buff true
    setvar! auto_tasteid_buff true
    setvar! auto_delectable true


    sysmsg '>>> Setting Spell Buffs Configuration...' 90
    # SPELL BUFFS
    setvar! auto_reactive_armor_buff true
    setvar! auto_magic_reflection_buff true
    setvar! auto_protection_buff true
    # setvar! auto_arch_protection_buff true
    setvar! auto_strength_buff true


    sysmsg '>>> Setting Grimoire Spells Used...' 90
    setvar! use_mana_drain true
    setvar! use_curse true
    setvar! use_ma true
    setvar! use_harm true
    # setvar! use_fireball true
    setvar! use_lightning true
    # setvar! use_eb true
    setvar! use_mind_blast true
    setvar! auto_unload_items_in_pack_animal true
    setvar! auto_unload_reagents_in_pack_animal true


    sysmsg '>>> Configuring General Delays And Limits...' 90
    setvar! action_delay 600
    setvar! all_guard_delay 5000
    setvar! use_mushroom_delay 60000
    setvar! min_mana_to_attack 52
    setvar! mana_limit_to_medit 40
    setvar! min_diffhits_to_attack 10
    setvar! mana_limit_to_eat_mushroom 70
    setvar! find_last_target_max_range 18
    setvar! find_summon_range 6


    sysmsg '>>> Configuring Spell Delays...' 90
    setvar! mana_drain_delay 60000
    setvar! curse_delay 60000
    setvar! eb_delay 10000
    setvar! ma_delay 60000
    setvar! harm_delay 60000
    setvar! fireball_delay 60000
    setvar! lightning_delay 60000


    sysmsg '>>> Setting up Char Summons...' 90
    if not listexists 'summons'
        createlist 'summons'
        pushlist 'summons' 'an earth elemental'
        pushlist 'summons' 'a fire elemental'
        pushlist 'summons' 'summ. creature'
    endif



    ################################################

    sysmsg '>>> Setting Max Summons Limit...'  90
    # if summ creature is one of players summons, change limit to 5 so even with 4 followers, we still make the char cast to summon the fifth creature
    # if not, then we just want 2 elementals so limit should be 4, since they take up 2 slots each and the fifth wouldnt be possible
    if inlist 'summons' 'summ. creature'
        setvar! summon_max_limit 5
    else
        setvar! summon_max_limit 4
    endif

    sysmsg '>>> Creating Summon Spell Lists...' 90
    if not listexists 'summonspells'
        createlist 'summonspells'
    else
        clearlist 'summonspells'
    endif

    if inlist 'summons' 'an air elemental'
        @pushlist 'summonspells' 'air elemental'
    endif
    if inlist 'summons' 'an earth elemental'
        @pushlist 'summonspells' 'earth elemental'
    endif
    if inlist 'summons' 'a water elemental'
        @pushlist 'summonspells' 'water elemental'
    endif
    if inlist 'summons' 'a fire elemental'
        @pushlist 'summonspells' 'fire elemental'
    endif
    if inlist 'summons' 'a daemon'
        @pushlist 'summonspells' 'summon daemon'
    endif
    if inlist 'summons' 'an energy vortex'
        @pushlist 'summonspells' 'energy vortex'
    endif
    if inlist 'summons' 'a blade spirit'
        @pushlist 'summonspells' 'blade spirits'
    endif
    if inlist 'summons' 'summ. creature'
        @pushlist 'summonspells' 'summ. creature'
    endif
    
    ################################################
    # UNLOAD ITEMS IN PACKIE SETUP
    
    
    sysmsg '>>> Creating pack animal unloading lists...' 90
    
    if not listexists 'open_pack_animal_backpack'
        createlist 'open_pack_animal_backpack'
    else
        clearlist 'open_pack_animal_backpack'
    endif
    


    sysmsg '>>> Populating pack animal unload item lists...' 90
    #####################################
    # CREATE ITEMS TO MOVE TO PACKIE LISTS
    #####################################

    if not listexists 'items_to_move_to_llama'
        @createlist 'items_to_move_to_llama'

        if varexist move_gold
            @pushlist 'items_to_move_to_llama' 'gold coin'
        endif

        if varexist move_resources
            @pushlist 'items_to_move_to_llama' 'cut up leather'
            @pushlist 'items_to_move_to_llama' 'log%s'
            @pushlist 'items_to_move_to_llama' 'board%s'
            @pushlist 'items_to_move_to_llama' 'ingot%s'
        endif

        # gems
        if varexist move_gems
            @pushlist 'items_to_move_to_llama' 'rub%ies/y%'
            @pushlist 'items_to_move_to_llama' 'tourmaline%s%'
            @pushlist 'items_to_move_to_llama' 'emerald%s%'
            @pushlist 'items_to_move_to_llama' 'diamond%s%'
            @pushlist 'items_to_move_to_llama' 'piece%s% of amber'
            @pushlist 'items_to_move_to_llama' 'amethyst%s%'
            @pushlist 'items_to_move_to_llama' 'star sapphire%s%'
            @pushlist 'items_to_move_to_llama' 'citrine%s%'
            @pushlist 'items_to_move_to_llama' 'sapphire%s%'
        endif
        #
        # foods
        if varexist move_food
            @pushlist 'items_to_move_to_llama' 'sliced ham'
            @pushlist 'items_to_move_to_llama' 'raw chicken leg%s%'
            @pushlist 'items_to_move_to_llama' 'raw leg%s% of lamb'
            @pushlist 'items_to_move_to_llama' 'slab%s% of bacon'
            @pushlist 'items_to_move_to_llama' 'sausage%s%'
            @pushlist 'items_to_move_to_llama' 'ham%s%'
            @pushlist 'items_to_move_to_llama' 'cut%s% of raw ribs'
        endif
        #
        # foods
    endif

    if varexist move_reagents and not listexists 'reagents'
        @createlist 'reagents'
        @pushlist 'reagents' 'Blood Moss'
        @pushlist 'reagents' 'Black Pearl%s%'
        @pushlist 'reagents' 'Garlic'
        @pushlist 'reagents' 'Mandrake Root%s%'
        @pushlist 'reagents' 'Ginseng'
        @pushlist 'reagents' 'Nightshade'
        @pushlist 'reagents' 'Sulfurous Ash'
        @pushlist 'reagents' "Spider's Silk"
    endif





    ################################################
    # UNLOAD ITEMS IN PACKIE SETUP
    #
    # lists to control which corpses have been drained and skinned
    # update: with necro you dont need to be draining corpses, summons loose hp until they die instead of lasting a duration in time

    # used to control which mobs i already used herding on
    @createlist 'herdened_mobs'

    if skill 'necromancy' == 0
        if skill 'spirit' > 0 and not listexists 'drained_corpses'
            sysmsg '>>> Creating list to control drained corpses...' 74
            createlist 'drained_corpses'
        endif
    endif
    
    if skill 'forensic' > 0 and not listexists 'skinned_corpses'
        sysmsg '>>> Creating list to control skinned corpses...' 74
        createlist 'skinned_corpses'
    endif


    # if summ creature is one of players summons, change limit to 5 so even with 4 followers, we still make the char cast to summon the fifth creature
    # if not, then we just want 2 elementals so limit should be 4, since they take up 2 slots each and the fifth wouldnt be possible
    if inlist 'summons' 'summ. creature'
        setvar! summon_max_limit 5
    else
        setvar! summon_max_limit 4
    endif



# END FIRST RUN
# HAVE TO BREAK FIRST RUN INTO TWO PARTS HERE
# CAUSE THE atlist VARIABLES BEFORE HAVE TO BE
# INITIALIZED ON EVERY RUN
endif




if not listexists 'first_run_completed'

    #################################
    # CREATE ALL TIMERS
    #################################
    
    sysmsg '>>> Creating And Setting Timers...' 74
    
    createlist 'summoner_bot_timers'
    pushlist 'summoner_bot_timers' 'loop_on_warning_msg'
    pushlist 'summoner_bot_timers' 'waiting_save'
    pushlist 'summoner_bot_timers' 'last_sysmsg_timer'
    pushlist 'summoner_bot_timers' 'out_of_item_warning_msg'
    pushlist 'summoner_bot_timers' 'bandage_timer'
    pushlist 'summoner_bot_timers' 'waiting_dress_timer'
    pushlist 'summoner_bot_timers' 'script_running_warning_msg_timer'
    pushlist 'summoner_bot_timers' 'spirit_speak_cooldown_timer'
    pushlist 'summoner_bot_timers' 'use_mushroom_timer'
    pushlist 'summoner_bot_timers' 'mana_drain_timer'
    pushlist 'summoner_bot_timers' 'curse_timer'
    pushlist 'summoner_bot_timers' 'ma_timer'
    pushlist 'summoner_bot_timers' 'harm_timer'
    pushlist 'summoner_bot_timers' 'fireball_timer'
    pushlist 'summoner_bot_timers' 'lightning_timer'
    pushlist 'summoner_bot_timers' 'eb_timer'
    pushlist 'summoner_bot_timers' 'medit_msg_timer'
    pushlist 'summoner_bot_timers' 'auto_get_new_targets_timer'
    pushlist 'summoner_bot_timers' 'get_new_target_msg'
    pushlist 'summoner_bot_timers' 'all_guard_timer'
    pushlist 'summoner_bot_timers' 'reset_all_timers_once'
    pushlist 'summoner_bot_timers' 'holding_target_warning_msg'
    pushlist 'summoner_bot_timers' 'waiting_summon_timer'
    pushlist 'summoner_bot_timers' 'waiting_spell_timer'
    pushlist 'summoner_bot_timers' 'waiting_target_timer'
    pushlist 'summoner_bot_timers' 'bring_mob_closer_timer'
    pushlist 'summoner_bot_timers' 'low_mana_for_summoning_warning_msg_timer'

    
    
    foreach timer in 'summoner_bot_timers'
        if not timerexists timer
            createtimer timer
            settimer timer 99999
        endif
    endfor
    
    
    # SET ALL TIMERS DEFAULT VALUES
    settimer 'mana_drain_timer' mana_drain_delay
    settimer 'curse_timer' curse_delay
    settimer 'ma_timer' ma_delay
    settimer 'harm_timer' harm_delay
    settimer 'fireball_timer' fireball_delay
    settimer 'lightning_timer' lightning_delay
    settimer 'eb_timer' eb_delay
    settimer 'medit_msg_timer' 1000
    settimer 'auto_get_new_targets_timer' 1000
    settimer 'get_new_target_msg' 120000
    settimer 'all_guard_timer' all_guard_delay
    settimer 'reset_all_timers_once' 0
    settimer 'holding_target_warning_msg' 0
    settimer 'waiting_summon_timer' 0
    settimer 'waiting_spell_timer' 0
    settimer 'waiting_target_timer' 0
    settimer 'bring_mob_closer_timer' 5000
    settimer 'script_running_warning_msg_timer' 2000
    settimer 'use_mushroom_timer' use_mushroom_delay
    settimer 'spirit_speak_cooldown_timer' 1000
    
    
    # --------------------------------------------
    # NECROMANCY TIMERS
    if skill 'necromancy' > 0
        sysmsg 'Creating Necro Spell Timers...' 95
        if not timerexists 'vengeful_spirit_timer'
            createtimer 'vengeful_spirit_timer'
            settimer 'vengeful_spirit_timer' 30001
        endif
    
        createtimer 'vampiric_embrace_cooldown_timer'
        settimer 'vampiric_embrace_cooldown_timer' 1000
    endif

    sysmsg '>> FINISHED FIRST RUN SETUP!' 55
    createlist 'first_run_completed'
# END FIRST RUN
endif


#######################################################
## BEGIN MAIN ROUTINE


sysmsg '>>> STARTING...' 75

# this is because sometimes i turn on the macro in the middle of a fight
# i want my pets to immeditally attack my last target
say 'all guard me'

# open necro hotbar on start
# @setvar necromancy_hotbar_gump_id 622436516

if skill 'necromancy' > 0 and not gumpexists 622436516
    sysmsg 'Opening Necro Hotbar...' 100
    say '[necromancyhotbar'
endif


if varexist pack_animal
    if not find pack_animal ground 0 any 5
        setvar! recreate_packies_list true
    endif
else
    setvar! recreate_packies_list true
endif

if varexist recreate_packies_list
    unsetvar! recreate_packies_list
    unsetvar! pack_animal

    # FIND AND SAVE PLAYER LLAMA
    if findtype 292 ground 0 any 5 as llama
        overhead 'scanning' 55 llama
        if noto llama == noto 'self'
            setvar pack_animal llama
            overhead 'found player llama' 55 pack_animal
        endif
    elseif findtype 291 ground 0 any 5 as horse
        overhead 'scanning' 55 horse
        if noto horse == noto 'self'
            setvar pack_animal horse
        endif
    endif

    if varexist pack_animal
        sysmsg "FOUND PLAYER PACK LLAMA" 55
        overhead "found and saving llama!" 55 'self'
        overhead 'found player llama' 55 pack_animal

        overhead '*renaming*' 55 pack_animal
        rename pack_animal 'i'
        menu pack_animal 4
        sysmsg '>>> Comanding pack animal to follow char...' 56
        say 'i follow me'
    else
        sysmsg '>>> COULD NOT FIND ANY PACK ANIMALS CLOSE.' 56
        sysmsg '****** IMPORTANT: ******' 90
        sysmsg '>>> YOU SHOULD BE WITHIN 5 TILES OF YOUR LLAMA SO THE SCRIPT CAN FIND AND SAVE IT!' 56
    endif
endif


# >>> START MAIN LOOP <<<
while not dead 'self'
    # sysmsg 'Summoner Bot - Iteration: {{index}} '

    if timer 'script_running_warning_msg_timer' >= 8000
        sysmsg 'ðŸ¤– SUMMONER BOT by gugutz â™¥' 20
        settimer 'script_running_warning_msg_timer' 0
    endif


    ########################################
    # TOGGLE OPTIONS IN GAME BY TURNING CHAR LANTERN ON/OFF


    if findtype 'lantern' backpack
        if not varexist auto_get_new_targets
            sysmsg '>>> TURNING AUTO GET NEW TARGETS ON' 65
            clearlist 'auto_get_new_targets'
            pushlist 'auto_get_new_targets' 'yes'
        endif
    elseif findlayer self earrings
        if varexist auto_get_new_targets
            sysmsg '>>> TURNING AUTO GET NEW TARGETS OFF' 41
            clearlist 'auto_get_new_targets'
            pushlist 'auto_get_new_targets' 'no'
        endif
    elseif findtype 2594 earrings or findtype 2578 earrings
        if varexist auto_get_new_targets
            overhead 'TURNONG AUTO GET NEW TARGETS ON' 41
            clearlist 'auto_get_new_targets'
            pushlist 'auto_get_new_targets' 'yes'
        endif

    # LIGHT SOURCES OFF
    elseif findtype 2597 earrings or findtype 3947 earrings
        overhead 'FOUND LANTERN OFF'
        if varexist auto_get_new_targets
            overhead 'TURNONG AUTO GET NEW TARGETS OFF' 41
            clearlist 'auto_get_new_targets'
            pushlist 'auto_get_new_targets' 'no'
        endif
    endif

    ########################################
    # IF THERE ARE TARGETS OPENED, SPEND THEM
    # ACCORDING TO THEIR TYPE

    if targetexists 'beneficial'
        overhead 'holding beneficial target'
        while targetexists 'beneficial'
            if timer 'holding_target_warning_msg' > 2000
                settimer 'holding_target_warning_msg' 0
                sysmsg '>>> HOLDING BENEFICIAL TARGET' 99
                # hotkey 'Target Self'
            endif
        endwhile
    endif

    if targetexists 'neutral'
        overhead 'holding neutral target'
        while targetexists 'neutral'
            if timer 'holding_target_warning_msg' > 2000
                settimer 'holding_target_warning_msg' 0
                sysmsg '>>> HOLDING NEUTRAL TARGET' 99
                # targetrelloc 0 1
            endif
        endwhile
    endif

    if targetexists 'harmful'
        if not dead 'last'
            if noto 'last' == hostile or noto 'last' == murderer or noto 'last' == criminal
                overhead 'spending harmful target'
                sysmsg '>>> SPENDING HARMFUL TARGET' 99
                hotkey 'Last Target'
            endif
        else
            overhead 'holding harmful target'
            while targetexists 'harmful'
                if timer 'holding_target_warning_msg' > 2000
                    settimer 'holding_target_warning_msg' 0
                    sysmsg '>>> HOLDING HARMFUL TARGET' 99
                    # targetrelloc 0 1
                endif

                # copied this part from bottom of script to get new targets if option is set
                if varexist auto_get_new_targets and timer 'auto_get_new_targets_timer' > 1000
                    overhead 'searching monsters...' 55
                    sysmsg '> Searching Monters Targets...' 55
                    hotkey 'Next Non-Friendly Monster Target'
                    if varexist get_humanoids_on_auto_target
                        overhead 'searching humanoids...'
                        sysmsg '> Searching Humanoid Targets...' 56
                        hotkey 'Next Grey Humanoid Target'
                        # wait action_delay
                    endif
                    settimer 'auto_get_new_targets_timer' 0
                elseif not varexist auto_get_new_targets and timer 'get_new_target_msg' > 4000
                    overhead 'waiting for new target' 55
                    settimer 'get_new_target_msg' 0
                endif

                if not dead 'last' and noto 'last' == hostile or noto 'last' == criminal
                    hotkey 'Last Target'
                endif
            endwhile

        endif
    endif


    ########################################
    # MOST IMPORTANT ROUTINE - KEEP SUMMONS UP!


    # CHOOSE CHARACTER SUMMONS

    # this is just a print to inform char is under summon_limit and is getting ready to summon up
    if varexist auto_summon and followers < summon_max_limit
        sysmsg 'Under Summon Limit. Preparing To Summon...' 67




        sysmsg 'ENTRANDO DE NOVO?' 45
        foreach summon in 'summons'

            sysmsg 'CHECKING {{summon}}: {{index}}' 18

            if list 'summons' == 1 and followers == 4
                sysmsg 'BREAKING ON CHECKING SUMMON {{summon}}' 99
                break
            elseif list 'summons' > 1 and followers == 5
                sysmsg 'BREAKING ON CHECKING SUMMON {{summon}}' 99
                break
            endif

            unsetvar! found_summon
            unsetvar! cast_summon_spell
    
            # determine summon name by whether char has necromancy or not
            if skill 'necromancy' >= 50
                # on each iteration, clear and populate this list to set the undead alternative summon based on current summon
                if not listexists 'undead_summons'
                    createlist 'undead_summons'
                endif
                clearlist 'undead_summons'
    
                if 'earth' in summon
                    pushlist 'undead_summons' 'an ancient mummy'
                elseif 'fire' in summon
                    pushlist 'undead_summons' 'a lich'
                elseif 'air' in summon
                    pushlist 'undead_summons' 'a skeletal fiend'
                elseif 'daemon' in summon
                    pushlist 'undead_summons' 'a vampire thrall'
                elseif 'water' in summon
                    pushlist 'undead_summons' 'a rag witch'
                endif
            endif
    
            # if char is necro, make sure vengeful spirit is activated before summoning
            if list 'undead_summons' > 0
                if timer 'vengeful_spirit_timer' >= 30000
                    sysmsg '>>> VENGEFUL SPIRIT...' 95
                    say '[vengefulspirit'

                    wait action_delay

                    sysmsg '>>> WITHER...' 95
                    say '[wither'
                    settimer 'vengeful_spirit_timer' 0
                else
                    sysmsg 'Vengeful Spirit Cooldown..' 41
                endif
            endif
    
            #########################
            # try to find a similir creature summon to see if we need to cast this one or skip to next one
    
    
            if findtype summon ground any 1 find_summon_range as summonserial
                if noto summonserial = friend
                    overhead '*found {{summon}}*' 91 summonserial
                    setvar! found_summon true
                endif
            # if the 'undead_summons' list has elements, we need to check for those undead summons as well
            elseif list 'undead_summons' > 0
                foreach necro_summon in 'undead_summons'
                    if findtype necro_summon ground any 1 find_summon_range as summonserial
                        if noto summonserial = friend
                            overhead '*found {{summon}}*' 91 summonserial
                            setvar! found_summon true
                            # break
                        endif
                    endif
                endfor
            endif
    
            sysmsg 'DEBUG 1' 19
            # even if we found this summon type, we still might need to cast it (if only 1 summon in list for instance)
            if varexist found_summon
                # clearlist 'found_summon'

                # if theres only one summon in the 'summons' list, even if we found the summon above,
                # we still need to cast it again
                if list 'summons' == 1 and followers < 4
                    setvar! cast_summon_spell true
                # if one of our summons is summ creature and we only have 3 followers, we can still cast another more usefull
                # summon before actually casting summ.creature
                elseif list 'summons' == 2 and followers <= 3 and inlist 'summons' 'summ. creature'
                    setvar! cast_summon_spell true
                elseif list 'summons' >= 3 and followers <= 1
                    setvar! cast_summon_spell true
                # else if theres more than one summon in 'summons' list and we didnt find this specific summon
                # we need to cast ig
                endif
            else
                sysmsg 'DEBUG ElSE 333' 19
                setvar! cast_summon_spell true
            endif
    
            #########################
            # prepare to cast the summon spell for this summon
            if varexist cast_summon_spell

    
                foreach summonspell in 'summonspells'

                    if summonspell in summon

                        unsetvar! 'summon_spell_required_mana'
    
                        # first make a mana check for this specific summon spell
                        if 'creature' in summonspell or 'blade' in summonspell
                            setvar! 'summon_spell_required_mana' 14
                        else
                            setvar! 'summon_spell_required_mana' 50
                        endif
    
                        if varexist summon_spell_required_mana
                            if mana < summon_spell_required_mana

                                if timer 'low_mana_for_summoning_warning_msg_timer' >= 2000
                                    sysmsg ">>> LOW MANA FOR SUMMONING {{summon}}. (NEEDS {{summon_spell_mana}})..." 41
                                    overhead "low mana for summoning" 41
                                    settimer 'low_mana_for_summoning_warning_msg_timer' 0
                                endif
                            else
                                sysmsg ">>> SUMMONING {{summon}}..." 56
                                cast summonspell
                                if 'blade' in summonspell or 'vortex' in summonspell
                                    overhead 'Waiting for {{summonspell}} target..'
    
                                    @settimer 'waiting_summon_timer' 0
                                    while not targetexists 'neutral' and timer 'waiting_summon_timer' < 3500
                                        wft 100
                                        if insysmsg 'disturbed'
                                            @settimer 'waiting_summon_timer' 0
                                            sysmsg 'BREAKING WHILE WAITING FOR RESS TARGET'
                                            wait 150
                                            break
                                        endif
                                    endwhile
    
                                    if targetexists 'neutral'
                                        sysmsg 'CASTING BLADE OR VORTEX IN GROUND' 55
                                        targetrelloc 0 1
                                        break
                                    endif
                                else
                                    @settimer 'waiting_summon_timer' 0
                                    while timer 'waiting_summon_timer' < 3500
                                        wait 100
                                        if insysmsg 'disturbed'
                                            @settimer 'waiting_summon_timer' 0
                                            sysmsg 'BREAKING WHILE WAITING FOR RESS TARGET'
                                            wait 150
                                            break
                                        endif
                                    endwhile
                                    say 'all guard me'
                                endif

                                break

                            endif
                        endif


                    endif

                endfor
            endif


        endfor
    endif

    # keep draining corpses within 10 tile range
    if skill 'necromancy' = 0 and varexist auto_spirit_speak and skill 'spirit' > 0
        if timer 'spirit_speak_cooldown_timer' > 1000
            settimer 'spirit_speak_cooldown_timer' 0
            if findtype 'corpse' ground any any 10 as body
                if not inlist 'drained_corpses' body
                    sysmsg '>>> DRANING NEARBY CORPSES' 95
                    skill 'spirit'
                    # avoiding using global ignore here, switching to list control to avoid imprevisiblities
                    @pushlist 'drained_corpses' body
                    wait action_delay
                    # @ignore body
                endif
            endif
        endif
    endif

    # keep draining corpses within 10 tile range
    if skill 'necromancy' > 0 and varexist auto_vampiric_embrace
        if timer 'vampiric_embrace_cooldown_timer' >= 30000
            if findtype 'corpse' ground any any 10 as body
                say '[vampiricembrace'
                wft action_delay
                if targetexists 'neutral'
                    sysmsg '>>> CONSUMING NEARBY CORPSE...' 95
                    overhead '*consuming corpse*' 95
                    target body
                    settimer 'vampiric_embrace_cooldown_timer' 0
                endif

                wait action_delay
            endif
        endif
    endif


    ########################################
    # KEEP CHAR BUFFED UP

    # reflect buff
    if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
        if varexist auto_magic_reflection_buff and not findbuff 'magic reflection'
            sysmsg '>>> MAGIC REFLECT' 88
            cast 'magic reflection'

            @settimer 'waiting_spell_timer' 0
            while not findbuff 'magic reflection' and timer 'waiting_spell_timer' < 3000
                wait 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR MAGIC REFLECTION'
                    wait 150
                    break
                endif
            endwhile

            if findbuff 'magic reflection'
                overhead '+magic reflect'
            endif
        endif
    endif

    # reactive armor buff
    if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
        if varexist auto_reactive_armor_buff and not findbuff 'reactive armor'
            sysmsg '>>> REACTIVE ARMOR' 88
            cast 'reactive armor'
            @settimer 'waiting_spell_timer' 0
            while not findbuff 'reactive armor' and timer 'waiting_spell_timer' < 2000
                wait 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR REACTIVE ARMOR'
                    wait 150
                    break
                endif
            endwhile

            wait 150

            if findbuff 'reactive armor'
                overhead '+reactive'
            endif
        endif
    endif

    # PROTECTION ARMOR BUFF
    if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
        if varexist auto_protection_buff and not findbuff 'protection'
            sysmsg '>>> PROTECTION' 88
            cast 'protection'

            @settimer 'waiting_spell_timer' 0
            while not findbuff 'protection' and timer 'waiting_spell_timer' < 1500
                wait 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR PROTECTION'
                    wait 150
                    break
                endif
            endwhile
            wait 150

            if findbuff 'protection'
                overhead '+protection'
            endif
        # OR ARCH PROTECTION
        elseif varexist auto_arch_protection_buff and not findbuff 'protection'

            @settimer 'waiting_target_timer' 0
            while not targetexists 'neutral' and timer 'waiting_spell_timer' < 4000
                wft 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR ARCH PROTECTION TARGET'
                    wait 150
                    break
                endif
            endwhile

            if targetexists 'neutral'
                hotkey 'Target Self'
                wait 100
            endif

            wait 150
            if findbuff 'protection'
                overhead '+arch protection'
            endif
        endif
    endif

    # keep str above 100
    if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
        if varexist auto_strength_buff and not findbuff 'Strength' or str < 110
            sysmsg '>>> STRENGHT' 88
            cast 'Strength'

            @settimer 'waiting_target_timer' 0
            while not findbuff 'strength' and not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                wft 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR STRENGTH TARGET'
                    wait 150
                    break
                endif
            endwhile

            if targetexists 'beneficial'
                hotkey 'Target Self'
            endif

            wait 150
            if findbuff 'strength'
                overhead '+strength'
            endif
        endif
    endif


    # CAMPFIRE BUFF
    if varexist auto_campfire_buff and not findbuff 'campfire visit'
        if not findtype campfire ground any any 7
            if findtype 'kindling' backpack as firestarter
                sysmsg '>>> STARTING CAMPFIRE*' 88
                overhead '*starting campfire*' 65
                dclick firestarter
            else
                overhead 'no kindling' 41
            endif
        elseif findtype campfire ground any any 2 as fire
            dclick fire
        endif
    endif


    # DELECTABLE
    if varexist auto_delectable and not findbuff 'food satisfaction'
        if findtype 'tray' as delectable
            overhead '*eating deletable*'
            dclick delectable
        else
            overhead '*no delectable*'
        endif
    endif


    # TASTE BUFF
    if varexist auto_tasteid_buff and skill 'taste identification' > 50
        if skill 'taste identification' > 50
            if not findbuff 'herbal poultice'
                skill 'tasteid'
                wft 600
                if findtype 'dagger' backpack as knife
                    overhead '*herbal poultice*'
                    target knife
                elseif findtype 'spellbook' backpack as book
                    overhead '*herbal poultice*'
                    target book
                else
                    targetrelloc 1 1
                endif

                wait 200
                if findbuff 'herbal poultice'
                    sysmsg '>>> CREATED HERBAL POULTICE'
                    overhead '*herbal poultice*'
                endif
            endif

        endif
    endif


    # AUTO CURE POISON
    if varexist auto_cure_poison and poisoned
        # if diffhits is too high, better guarantee and use a gheal pot
        if findtype 'orange potion' backpack as pot
            sysmsg '>>> CURE POTION! ' 56
            useobject pot
        else
            sysmsg '>>> ARCH CURING!' 56
            cast 'arch cure'

            @settimer 'waiting_spell_timer' 0
            while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4000
                wft 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere'
                    sysmsg 'BREAKING WHILE WAITING FOR ARCH CURE TARGET'
                    wait 150
                    break
                endif
            endwhile

            if targetexists 'neutral'
                hotkey 'Target Self'
                wait 150
            endif
        endif
    endif


    # KEEP CHAR HITS UP
    if varexist auto_heal and diffhits > 0
        # if diffhits is too high, better guarantee and use a gheal pot
        if diffhits >= 40
            if findtype 'yellow potion' backpack as pot
                sysmsg '>>> LOW HP! USING HEAL POTION.. ' 56
                useobject pot
                wait 150
            endif
        endif

        if diffhits >= 20
            sysmsg '>>> GREATER HEALING...' 76
            cast 'greater heal'

            @settimer 'waiting_target_timer' 0
            while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4000
                wft 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                    wait 150
                    break
                endif
            endwhile

            if targetexists 'beneficial' and hits < maxhits
                hotkey 'Target Self'
                wait 150
            endif
        elseif diffhits >= min_diffhits_to_attack
            sysmsg '>>> MINI HEALING...' 78
            cast 'heal'

            @settimer 'waiting_target_timer' 0
            while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                wft 100
                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                    sysmsg 'BREAKING WHILE WAITING FOR HEAL TARGET'
                    wait 150
                    break
                endif
            endwhile

            if targetexists 'beneficial' and hits < maxhits
                hotkey 'Target Self'
                wait 150
            endif
        endif
    endif

    # keep mushrooms stock in backpack
    if counttype 'mushroom' < 15
        cast 'create food'
        wait action_delay
    endif

    # eat mushroom on its timer
    if mana < mana_limit_to_eat_mushroom and timer 'use_mushroom_timer' >= use_mushroom_delay
        if findtype 'mushroom' backpack as mush
            useobject mush
            wait action_delay
            if insysmsg 'you consume'
                sysmsg '>>> Used mushroom! Resetting timer...' 55
                settimer 'use_mushroom_timer' 0
            endif
        else
            sysmsg ">>> NO MUSHROOM!" 41
        endif
    endif

    # meditate when needed
    if varexist auto_meditate and mana < mana_limit_to_medit and not findbuff 'actively meditating'
        skill 'meditation'
        waitforsysmsg 'You start meditating' 200

        if findbuff 'actively meditating'
            if varexist use_script_overhead_messages
                overhead 'meditating' 56
            endif
            while findbuff 'actively meditating' and mana < min_mana_to_attack
                if mana < mana_limit_to_eat_mushroom and timer 'use_mushroom_timer' >= use_mushroom_delay
                    if findtype 'mushroom' backpack as mush
                        useobject mush
                        if insysmsg 'you consume'
                            sysmsg '>>> Used mushroom! Resetting timer...' 55
                            settimer 'use_mushroom_timer' 0
                        endif
                    else
                        sysmsg ">>> NO MUSHROOM!" 41
                    endif
                endif
                if timer 'medit_msg_timer' >= 2000
                    sysmsg '>>> MEDITATING...'
                    if varexist use_script_overhead_messages
                        overhead 'meditating...'
                    endif
                    settimer 'medit_msg_timer' 0
                endif
            endwhile
        endif
    endif


    if varexist auto_skin_corpses and skill 'forensic' > 0 and not findbuff 'Actively Meditating'
        # trying this: only skin if there isnt a active target to attack
        if dead 'last' or not find 'last' ground any any 20
            if findtype corpse ground any any 2 as body
                if not inlist 'skinned_corpses' body
                    if findtype 'elven spellblade' backpack as knife
                        useobject knife
                        wft 500
                        if targetexists 'neutral'
                            hotkey 'Target Self'
                            if varexist use_script_overhead_messages
                                overhead '*skinning corpses*' 12 'self'
                                overhead '*skinning*' 12 body
                            endif
                            sysmsg '>>> SKINNING CORPSES NEARBY'
                            wait action_delay
                            @pushlist 'skinned_corpses' body
                        endif



                        if insysmsg 'dont see anything'
                            if varexist use_script_overhead_messages
                                overhead '*NOTHING TO CARVE*' 41
                            endif
                            @ignore body
                        elseif insysmsg 'carve materials'
                            if varexist use_script_overhead_messages
                                overhead '*carved materials!*' 16
                            endif
                            # switched ignore to list control cause spirit speak will ignore corpses and then razor wont skin it anymore since it would be globally ignored
                            # @ignore body
                            @pushlist 'skinned_corpses' body
                        elseif insysmsg 'must wait'
                            sysmsg '>>> SKILL COOLDOWN (FORENSIC)' 45
                            headmsg ':: forensic cooldown ::' 45
                            wait action_delay
                        endif
                    endif
                endif
            endif
        endif
    endif


    # monitor sysmsgs to know when we can reset time spell timers
    if insysmsg 'You may now cast a wizardry harm'
        sysmsg '>>> RESETING HARM TIMER...' 51
        settimer 'harm_timer' 0
    endif
    if insysmsg 'You may now cast a wizardry magic arrow'
        sysmsg '>>> RESETING MAGIC ARROW TIMER...' 51
        settimer 'magic arrow_timer' 0
    endif
    if insysmsg 'You may now cast a wizardry fireball'
        sysmsg '>>> RESETING FIREBALL TIMER...' 51
        settimer 'fireball_timer' 0
    endif
    if insysmsg 'You may now cast a wizardry lightning'
        sysmsg '>>> RESETING LIGHTNING TIMER...' 51
        settimer 'lightning_timer' 0
    endif

    if varexist auto_escape_pks
        if findtype 400 ground any any 20 as player
            if noto player = murderer
                sysmsg '>>> FOUND PK! TRYING TO ESCAPE...'
                sysmsg '>>> FOUND PK! TRYING TO ESCAPE...'
                sysmsg '>>> FOUND PK! TRYING TO ESCAPE...'
                sysmsg '>>> FOUND PK! TRYING TO ESCAPE...'
                say 'time to go...'
                cast 'recall'
                wft 3500
                target escape_runetome
                wait 2000
                sysmsg '>>> STOPPING MACRO'
                stop
            endif
        elseif insysmsg 'Attacking you'
            sysmsg '>>> BEING ATTACKED! TRYING TO ESCAPE...'
            sysmsg '>>> BEING ATTACKED! TRYING TO ESCAPE...'
            sysmsg '>>> BEING ATTACKED! TRYING TO ESCAPE...'
            sysmsg '>>> BEING ATTACKED! TRYING TO ESCAPE...'
            say 'time to go...'
            cast 'recall'
            wft 3500
            target escape_runetome
            wait 2000
            sysmsg '>>> STOPPING MACRO'
            stop
        endif
    endif


    if varexist auto_unload_items_in_pack_animal or varexist auto_unload_reagents_in_pack_animal
        if diffweight <= 15 or weight >= maxweight

                #####################################
                # MOVE ITEMS TO PACKIE MAIN ROUTINE
                #####################################

                if varexist pack_animal and find pack_animal 'ground' 0 any 7
                    unsetvar! found_item_to_unload
                    # make one more search with range reduced to 2 to see if we need to call the llama close
                    while not find pack_animal 'ground' 0 1 2

                        if timer 'call_packie_closer_timer' >= 1000
                            settimer 'call_packie_closer_timer' 0
                            overhead "calling packie closer!" 40 'self'
                            overhead "too far to unload!" 40 pack_animal

                            # say 'all follow me'
                            menu pack_animal 5
                        endif
                        wait 10
                    endwhile


                    dclick pack_animal
                    wait action_delay

                    sysmsg 'Searching Items To Unload...' 56
                    foreach item_graphic in 'items_to_move_to_llama'
                        if findtype item_graphic backpack as item
                            # set the variable so we know we found something in the end of the script
                            # if this var is not set, we warn the player there was nothing to move
                            if not varexist found_item_to_unload
                                setvar! found_item_to_unload true
                            endif

                            sysmsg ">  Unloading Items In Pack Animal..." 56 'self'
                            sysmsg item_graphic 56

                            # overhead "*unloading char*" 56 'self'
                            # overhead "*unloading items*" 56 pack_animal
                            while findtype item_graphic backpack as item
                                getlabel item item_label
                                overhead "unloading {{item_label}}" 56 pack_animal
                                lift item 60000
                                wait action_delay
                                drop pack_animal -1 -1 0
                                wait action_delay
                            endwhile
                        endif
                    endfor

                    if varexist move_reagents
                        sysmsg 'Searching Reagents To Unload...' 56
                        foreach reagent_graphic in 'reagents'

                            # set an initial amount of regs to leave on char
                            setvar! min_amount_to_leave_on_char 100
                            if counttype reagent_graphic backpack > min_amount_to_leave_on_char
                                # set the variable so we know we found something in the end of the script
                                # if this var is not set, we warn the player there was nothing to move
                                sysmsg ">  Unloading {{reagent_graphic}} In Pack Animal..." 56 'self'
                                # overhead "*unloading reagents*" 56 'self'
                                while counttype reagent_graphic > min_amount_to_leave_on_char

                                    # set the variable so we know we found something in the end of the script
                                    # if this var is not set, we warn the player there was nothing to move
                                    setvar! found_item_to_unload true

                                    # decide how many regs to move based on current amount
                                    unsetvar! 'amount_of_regs_to_move'
                                    if counttype reagent_graphic backpack >= 600
                                        setvar! amount_of_regs_to_move 500
                                    elseif counttype reagent_graphic backpack >= 500
                                        setvar! amount_of_regs_to_move 400
                                    elseif counttype reagent_graphic backpack >= 400
                                        setvar! amount_of_regs_to_move 300
                                    elseif counttype reagent_graphic backpack >= 300
                                        setvar! amount_of_regs_to_move 200
                                    elseif counttype reagent_graphic backpack >= 200
                                        setvar! amount_of_regs_to_move 100
                                    elseif counttype reagent_graphic backpack >= 200
                                        setvar! amount_of_regs_to_move 100
                                    elseif counttype reagent_graphic backpack > min_amount_to_leave_on_char
                                        setvar! amount_of_regs_to_move 40
                                    endif

                                    # get packie closer if it gets too far to unload (more than 2 tiles away)
                                    if not find pack_animal ground any any 2
                                        while not find pack_animal ground any any 2
                                            if timer 'call_packie_closer_timer' >= 1000
                                                settimer 'call_packie_closer_timer' 0
                                                overhead "calling packie closer!" 40 'self'
                                                overhead "too far to unload!" 40 pack_animal

                                                # say 'all follow me'
                                                menu pack_animal 5
                                            endif
                                            wait 10
                                        endwhile
                                        # break
                                    endif

                                    overhead 'unloading {{amount_of_regs_to_move}} {{reagent_graphic}}' 53 pack_animal

                                    lifttype reagent_graphic amount_of_regs_to_move backpack
                                    wait action_delay
                                    drop pack_animal -1 -1 0
                                    wait action_delay

                                endwhile
                            endif
                        endfor
                    endif

                    if not varexist found_item_to_unload
                        overhead '*no items to unload*' 41
                        sysmsg '> No Items To Unload To Packie' 41
                        stop
                    endif

                else
                    if varexist use_script_overhead_messages
                        overhead "CHAR HEAVY AND CAN FIND CHAR LLAMA NEARBY"
                        sysmsg "CHAR HEAVY AND CAN FIND CHAR LLAMA NEARBY" 56
                    endif
                endif
            endif
        endif
    endif

    # keep mobs guarding player
    if varexist auto_all_guard and timer 'all_guard_timer' >= all_guard_delay
        say 'all guard me'
        settimer 'all_guard_timer' 0
    endif

    #########################################
    # MAIN ATTACK ROUTINE
    #########################################

    # make sure char has high hp and mana for FS before attacking
    if varexist auto_attack_targets and diffhits < min_diffhits_to_attack and mana >= min_mana_to_attack
        # make sure char is always max summon before attacking
        if not dead 'last' and followers >= summon_max_limit and find 'last' ground any any find_last_target_max_range
            sysmsg 'ENTROU 001'
            if noto 'last' == hostile or noto 'last' == murderer or noto 'last' == criminal
                sysmsg 'ENTROU 002'

                # save last target serial on custom var to re-set after healing friendly targets
                if not dead 'last' and noto 'last' == hostile or noto 'last' == murderer or noto 'last' == criminal

                    if find 'last' 'ground' 'any' 1 find_last_target_max_range as last_target_serial_found
                        unsetvar last_target_serial
                        setvar! last_target_serial last_target_serial_found
                    endif
                endif

                # use crook on last target
                if not dead 'last' and find 'last' ground any any find_last_target_max_range and noto 'last' == hostile or noto 'last' == murderer or noto 'last' == criminal
                    if varexist last_target_serial
                        if not inlist 'herdened_mobs' last_target_serial
                            if skill 'herding' > 20
                                if findtype 3713 as crook
                                    while not targetexists 'harmful'
                                        useobject crook
                                        wft action_delay
                                    endwhile
    
                                    if targetexists 'harmful'
                                        sysmsg 'USING CROOK' 55
                                        hotkey 'Last Target'
                                        wait action_delay
                                        if insysmsg 'You focus your follower'
                                            overhead 'focused pets agression' 41 'last'
                                            pushlist 'herdened_mobs' last_target_serial
                                        endif
                                    endif
    
                                    say 'all kill'
                                    wft action_delay
                                    if targetexists 'harmful'
                                        hotkey "Last Target"
                                        wait action_delay
                                    endif
    
                                else
                                    overhead "no sheperd's crook!"
                                endif
                            endif
                        endif
                    endif
                endif


                # kill close to loot mob easier
                if varexist kill_close_to_loot and timer 'bring_mob_closer_timer' > 5000
                    sysmsg '>>> ATTRACTING MOB CLOSE TO CHAR'
                    say 'all kill'
                    wft 600
                    target 'last'
                    wait 500
                    say 'all follow me'
                    hotkey 'Toggle Peace Only'
                    settimer 'bring_mob_closer_timer' 0
                endif

                # safe clause to not attack self or friendsa
                if noto 'last' = friend or noto 'last' = friendly or noto 'last' = innocent
                    sysmsg 'CANCELING FRIENDLY TARGET'
                    target 'clear'
                    hotkey 'Cancel Current Target'
                endif

                # before each spell and every attack, we first check 2 things:
                # if the followers number is 4 or more, indicating we have all summons up
                # and if diffhits is under 10, indicating character is not under threat of dying and doenst need heal
                # only if all checks pass we start attacking the target
                if followers >= summon_max_limit and noto 'last' != friend and noto 'last' != friendly
                    if noto 'last' == hostile or noto 'last' == murderer or noto 'last' == criminal
                        attack 'last'

                        if timer 'reset_all_timers_once' >= 5000000
                            settimer 'mana_drain_timer' mana_drain_delay
                            settimer 'curse_timer' mana_drain_delay
                            settimer 'medit_msg_timer' 6000
                            settimer 'auto_get_new_targets_timer' 1000
                            settimer 'all_guard_timer' 5000
                            settimer 'reset_all_timers_once' 0
                        endif


                        if varexist auto_all_guard and timer 'all_guard_timer' >= 5000
                            say 'all guard me'
                            settimer 'all_guard_timer' 0
                        endif


                        # GRIMOIRE SPELLS
                        # mana drain
                        if not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
                                if varexist use_mana_drain and timer 'mana_drain_timer' >= mana_drain_delay
                                    overhead 'mana drain'
                                    sysmsg '>>> MANA DRAIN' spell_msg_color
                                    cast 'mana drain'

                                    @settimer 'waiting_target_timer' 0
                                    while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 3500
                                        wait 100
                                        if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                            sysmsg 'BREAKING WHILE WAITING FOR MANA DRAIN TARGET'
                                            wait 150
                                            break
                                        endif
                                    endwhile

                                    if targetexists 'harmful'
                                        hotkey 'Last Target'
                                        wait 150
                                        settimer 'mana_drain_timer' 0
                                    endif
                                endif
                            endif
                        endif

                        # curse
                        if not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if followers >= summon_max_limit and diffhits < min_diffhits_to_attack
                                if varexist use_curse and timer 'curse_timer' >= curse_delay
                                    sysmsg '>>> Curse' spell_msg_color
                                    cast 'curse'

                                    @settimer 'waiting_target_timer' 0
                                    while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 3500
                                        wait 100
                                        if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                            sysmsg 'BREAKING WHILE WAITING FOR CURSE TARGET'
                                            wait 150
                                            break
                                        endif
                                    endwhile

                                    if targetexists 'harmful'
                                        hotkey 'Last Target'
                                        wait 150
                                        settimer 'curse_timer' 0
                                    endif
                                endif
                            endif
                        endif

                        # energy bolt
                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_eb and timer 'eb_timer' > eb_delay
                                sysmsg '>>> ENERGY BOLT' spell_msg_color
                                cast 'energy bolt' 40

                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4000
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        sysmsg 'BREAKING WHILE WAITING FOR EB TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 100
                                    settimer 'eb_timer' 0
                                endif
                            endif
                        endif

                        # magic arrow
                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_ma and timer 'ma_timer' > ma_delay
                                sysmsg '>>> Magic Arrow' spell_msg_color
                                cast 'magic arrow'

                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        sysmsg 'BREAKING WHILE WAITING FOR MA TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 150
                                    settimer 'ma_timer' 0
                                endif
                            endif
                        endif

                        # harm
                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_harm and timer 'harm_timer' > harm_delay
                                sysmsg '>>> HARM' spell_msg_color
                                cast 'harm'

                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        sysmsg 'BREAKING WHILE WAITING FOR HARM TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 150
                                    settimer 'harm_timer' 0
                                endif
                            endif
                        endif

                        # fireball
                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_fireball and timer 'fireball_timer' > fireball_delay
                                sysmsg '>>> FIREBALL' spell_msg_color
                                cast 'fireball'

                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        sysmsg 'BREAKING WHILE WAITING FOR FIREBALl TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 150
                                    settimer 'fireball_timer' 0
                                endif
                            endif
                        endif

                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_lightning and timer 'lightning_timer' > lightning_delay
                                sysmsg '>>> LIGHTNING' spell_msg_color
                                cast 'lightning'

                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 2500
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        sysmsg 'BREAKING WHILE WAITING FOR LIGHTNING TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 150
                                    settimer 'lightning_timer' 0
                                endif
                            endif
                        endif


                        # USE MIND BLAST
                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            if varexist use_mind_blast
                                sysmsg '>>> MIND BLAST' spell_msg_color
                                cast 'mind blast'
    
                                @settimer 'waiting_target_timer' 0
                                while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4500
                                    wft 100
                                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                        if varexist debug_mode
                                            sysmsg 'BREAKING WHILE WAITING FOR MIND BLAST TARGET'
                                        endif
                                        wait 150
                                        break
                                    endif
                                endwhile
    
                                if targetexists 'harmful'
                                    hotkey 'Last Target'
                                    wait 400
                                endif
                            endif
                        endif

                        if followers >= summon_max_limit and diffhits < min_diffhits_to_attack and not dead 'last' and find 'last' ground any any find_last_target_max_range
                            sysmsg '>>> FLAMESTRIKE' spell_msg_color
                            cast 'flamestrike'

                            @settimer 'waiting_target_timer' 0
                            while not targetexists 'harmful' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4500
                                wft 100
                                if insysmsg 'disturbed' or insysmsg 'cannot cast a spell'
                                    if varexist debug_mode
                                        sysmsg 'BREAKING WHILE WAITING FOR FLAMESTRIKE TARGET'
                                    endif
                                    wait 150
                                    break
                                endif
                            endwhile

                            if targetexists 'harmful'
                                hotkey 'Last Target'
                                wait 400
                            endif
                        endif

                    endif

                else
                    if varexist debug_mode
                        sysmsg 'NAO ENTROU NOTO AQUI 2'
                        overhead 'LAST BUGADO' 90 'last'
                    endif
                endif
            else
                if varexist debug_mode
                    sysmsg 'NAO ENTROU NOTO AQUI 1'
                    overhead 'LAST BUGADO' 90 'last'
                endif
            endif
        endif
    endif


    if findtype 'an elusive rabbit' ground any any 30 as rabbit
        overhead '!! RABBIT !!' 90
        overhead '!! RABBIT !!' 90 rabbit
    endif

    if dead 'last' or insysmsg 'Target cannot be seen'
        # if noto 'last' != friend and noto 'last' != innocent and noto 'last' != hostile and noto 'last' != criminal and noto 'last' != enemy and noto 'last' != murderer and noto 'last' != invulnerable
        # if dead 'last' or not find 'last' ground any any find_last_target_max_range
        if varexist debug_mode
            sysmsg 'DEAD LAST OR NOT FOUND' 58
        endif

        # when an enemy dies, set the reset_all_timers timer to its trigger value
        # so it will cause all spell timers to reset when getting a new target
        # @unsetvar 'last'

        @settimer 'reset_all_timers_once' 5000001

        if varexist auto_get_new_targets and timer 'auto_get_new_targets_timer' > 1000
            overhead 'searching monsters...' 55
            sysmsg '> Searching Monters Targets...' 55
            hotkey 'Next Non-Friendly Monster Target'
            if varexist get_humanoids_on_auto_target
                overhead 'searching humanoids...' warning_msg_color
                sysmsg '> Searching Humanoid Targets...' warning_msg_color
                hotkey 'Next Grey Humanoid Target'
                wait 500
            endif
            settimer 'auto_get_new_targets_timer' 0
        else
            if timer 'get_new_target_msg' > 4000
                overhead 'waiting for new target' warning_msg_color
                settimer 'get_new_target_msg' 0
            endif
        endif
    
        # endif
    endif

    # putting the clear here to reset all possible msgs i caught during the previous iteration
    clearsysmsg

#random 10

endwhile


