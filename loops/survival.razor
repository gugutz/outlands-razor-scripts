##########################
# SURVIVAL LOOP
# by gugutz
# version 0.0.2
#################################


#################################
# CREDITS :)
#################################

sysmsg '♥ SURVIVAL LOOP by gugutz ♥' 91


#################################
# CHANGELOG
#################################
#
# v0.0.2
# - Fixed some types in monitor list
#
# v0.0.1
# - Initial release


#################################
# CREATE ALL TIMERS
#################################

if not varexist survival_first_run_done
    createlist 'survival_script_timers'
    pushlist 'survival_script_timers' 'loop_on_warning_msg'
    pushlist 'survival_script_timers' 'waiting_save'
    pushlist 'survival_script_timers' 'last_sysmsg_timer'
    pushlist 'survival_script_timers' 'out_of_item_warning_msg'
    pushlist 'survival_script_timers' 'waiting_dress_timer'



    foreach timer in 'survival_script_timers'
        if not timerexists timer
            createtimer timer
            settimer timer 99999
        endif
    endfor


    ################################
    # SCRIPT CONFIGURATION
    ################################

    # if char is dexxer we want to undress and re-arm our main weaopn
    # if mage its probably a pvp char so we want to switch to 1 hand (if no target)
    if dex == 100
        setvar! prefer_switching_to_1hand_weapon_instead_of_unequiping 1
    else
        setvar! prefer_switching_to_1hand_weapon_instead_of_unequiping 0
    endif

    setvar! unequip_hands_to_drink_potion 1
    setvar! monitor_items_amounts 0
    setvar! warn_when_items_amount_is_bellow 5
    setvar! action_delay 600
    setvar! max_diffhits_before_healing_with_bandages 0
    setvar! max_diffhits_before_healing_with_magery 20
    setvar! max_diffhits_before_healing_with_potion 40
    setvar! max_diffhits_before_healing_with_chivalry 40
    setvar! max_diffweight_before_unloading 50
    setvar! monitor_items_warning_delay 6000
    setvar! use_razor_native_undress_hands_for_potions 1

    setvar! chivalry_gump_id 1387930325

    # sysmsg '[DEBUG] 1'


    ################################
    # CODE
    ################################

    # SEED SOME LISTS BASED ON SCRIPT CONFIGURATION

    if monitor_items_amounts == 0
        if not listexists 'items_to_monitor'
            createlist 'items_to_monitor'
            if skill 'healing' >= 50
                pushlist 'items_to_monitor' 'clean bandage%s%'
                pushlist 'items_to_monitor' 'Yellow Potion'
                pushlist 'items_to_monitor' 'Orange Potion'
                pushlist 'items_to_monitor' 'Red Potion'
                pushlist 'items_to_monitor' 'Black Potion'
                pushlist 'items_to_monitor' 'Purple Potion'
                pushlist 'items_to_monitor' 'White Potion'
                pushlist 'items_to_monitor' 'Blue Potion'
            endif

            if skill 'magery' >= 20
                pushlist 'items_to_monitor' 'Blood Moss'
                pushlist 'items_to_monitor' 'Black Pearl%s%'
                pushlist 'items_to_monitor' 'Garlic'
                pushlist 'items_to_monitor' 'Mandrake Root%s%'
                pushlist 'items_to_monitor' 'Ginseng'
                pushlist 'items_to_monitor' 'Nightshade'
                pushlist 'items_to_monitor' 'Sulfurous Ash'
                pushlist 'items_to_monitor' "Spider's Silk"
            endif

            if skill 'archery' >= 20
                pushlist 'items_to_monitor' 'arrow'
                pushlist 'items_to_monitor' 'crossbow bolt'
            endif
        endif
    endif

    if prefer_switching_to_1hand_weapon_instead_of_unequiping == 1
        # this list  is created separatedely cause we also seed it with values
        if not listexists 'one_handed_weapons'
            createlist 'one_handed_weapons'
            pushlist 'one_handed_weapons' 'kryss'
            pushlist 'one_handed_weapons' 'war fork'
            pushlist 'one_handed_weapons' 'katana'
            pushlist 'one_handed_weapons' 'viking sword'
            pushlist 'one_handed_weapons' 'broadsword'
            pushlist 'one_handed_weapons' 'longsword'
        endif
    endif
endif




while not dead 'self'

    # open chivarly hotbar on start
    
    if skill 'chivalry' > 0 and not gumpexists chivalry_gump_id
        sysmsg 'Opening chivalry Hotbar...' 100
        say '[chivalryhotbar'
    endif

    # remember player the loop is active every 5s
    if timer 'loop_on_warning_msg' >= 5000
        settimer 'loop_on_warning_msg' 0
        sysmsg 'Survival Loop On...' 89
    endif



    ####################################
    # MONITOR ITEM AMMOUNTS AND WARN PLAYER WHEN LOW OR OUT (ONLY WHEN NOT IN DANGER - LOW HP)

    if monitor_items_amounts  == 1 and diffhits == 0
        if timer 'out_of_item_warning_msg' >= monitor_items_warning_delay
            sysmsg '##############################' 92
            sysmsg 'Scanning item ammounts...' 92

            # first we reset the warning msg timer
            if timer 'out_of_item_warning_msg' >= 5000
                settimer 'out_of_item_warning_msg' 0
            endif

            # foreach amount in 'warn_when_items_amount_is_bellow'
            foreach item in 'items_to_monitor'
                if not findtype item backpack any
                    sysmsg 'WARNING: Out of {{item}}!' 41
                elseif counttype item backpack <= warn_when_items_amount_is_bellow
                    sysmsg 'WARNING: {{item}} LOW AMMOUNT!' 45
                endif
            endfor
            # endfor
        endif
    endif


    ####################################
    # HANDLE PARALYZE
    ####################################

    if paralyzed
        if findtype pouch backpack 38 as trap_pouch
            sysmsg '>>> Using Trapped Pouch!'
            overhead 'TRAP POUCH' 54
            useobject trap_pouch
        else
            overhead '!! NO TRAP POUCHES !!' 37
        endif
    endif


    ####################################
    # CHIVALRY HOLY LIGHT
    ####################################

    if skill 'chivalry' > 10 and diffhits > max_diffhits_before_healing_with_chivalry
        overhead 'HOLY LIGHT' 89
        whisper '[holylight'
    endif

    ####################################
    # BANDAGE SEFL
    ####################################

    if counttype 'clean bandage%s%' == 0
        overhead '!!☠ NO BANDAGES ☠!!' 39
    endif

    if skill 'healing' > 10 and diffhits > max_diffhits_before_healing_with_bandages
        # heal with healing
        if counttype 'clean bandage%s%' > 0
            if not bandaging
                overhead 'START BANDAGING' 91
                hotkey 'Bandage Self'
                wait action_delay
            endif
        endif

    endif


    ####################################
    # POTIONS!!!
    ####################################


    # decide first which potion char needs to use this iteration
    if not targetexists 'any'
        # CURE POISONS BEFORE ANYTHING
        if poisoned
            setvar drink_cure_potion 1
        # if have to choose between stam and hits, depends on the diffs
        elseif diffhits > max_diffhits_before_healing_with_potion
            sysmsg 'HEAL 1: ONLY DIFFHITS'
            setvar! drink_heal_potion 1
        # IF STAM IS 0, CHAR IS LOCKED, SO UNLESS CHAR IS ALMOST DYING,
        # USE STAM SO CHAR CAN MOVE AGAIN
        elseif stam == 0
            sysmsg 'PREFERRING STAM 111111...'
            setvar! drink_refresh_potion 1
        elseif diffstam >= 10
            sysmsg 'PREFERRING STAM 222222...'
            setvar! drink_refresh_potion 1
        endif
    endif

    ####################################
    # UNDRESS HANDS FOR POTIONS
    ####################################

    if use_razor_native_undress_hands_for_potions == 0
        if not targetexists 'any'
            if drink_cure_potion == 1 or drink_heal_potion == 1 or drink_refresh_potion == 1
                if not lhandempty
                    if findlayer self lefthand as lhanditem
                        setvar! lhand_item lhanditem
                    endif

                    if prefer_switching_to_1hand_weapon_instead_of_unequiping == 1
                        foreach weapon in 'one_handed_weapons'
                            if findtype weapon backpack as weap
                                overhead "*switching to 1-handed*"
                                dclick weap
                                wait 400
                                break
                            endif
                        endfor
                    elseif unequip_hands_to_drink_potion == 1
                        hotkey "Undress Left Hand"
                        wait 400
                    endif
                endif
            endif
        endif
    endif

    ####################################
    # CURE POTS
    ####################################

    if not targetexists 'any'
        if drink_cure_potion == 1
            unsetvar drink_cure_potion
            if findtype 'orange potion' as pot
                overhead 'cure pot' 56
                useobject pot

            # elseif magery >= 50 and counttype 'Garlic' > 0 and counttype 'Ginseng' > 0
            #     sysmsg 'Curing poison with magery...' 90
            #     cast 'Cure'

            #     @settimer 'waiting_target_timer' 0
            #     while not targetexists 'beneficial' and timer 'waiting_target_timer' < 1500
            #         wait 100
            #         if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
            #             sysmsg 'BREAKING WHILE WAITING FOR CURE TARGET'
            #             break
            #         endif
            #     endwhile

            #     if targetexists 'beneficial'
            #         target 'self'
            #         wait action_delay
            #     endif

            elseif healing >= 50 and counttype 'clean bandage%s%' > 0
                if not bandaging
                    sysmsg 'Curing poison with healing...' 90
                    hotkey 'Bandage Self'
                    wait action_delay
                endif
            endif
        endif
    endif

    ####################################
    # REFRESH POTS
    ####################################

    if not targetexists 'any'
        if drink_refresh_potion == 1
            unsetvar drink_refresh_potion
            if findtype 'red potion' backpack
                sysmsg 'Refresh Potion...' 90
                hotkey 'Drink Refresh'
                wait action_delay
            endif
        endif
    endif

    ####################################
    # HEAL POTS
    ####################################

    if not targetexists 'any'
        if drink_heal_potion == 1
            unsetvar drink_heal_potion
            if findtype 'yellow potion' backpack as pot

                getlabel heal_pot heal_pot_label
                if 'next usable in' in heal_pot_label
                    sysmsg '> Heal Pot Cooldown!' 53
                    overhead '! heal pot cooldown !' 53
                else
                    clearsysmsg
                    sysmsg 'Heal Potion...' 90
                    hotkey 'Drink Heal'
                    wait action_delay
                    if not insysmsg 'You must wait'
                        cooldown "Heal Potion"
                    endif
                endif
            endif
        endif
    endif

    ####################################
    # HEAL SPELL OR BANDAGE
    ####################################

    if not targetexists 'any'
        if diffhits > 0
            # # heal with magery
            # if diffhits > max_diffhits_before_healing_with_magery and skill 'magery' >= 50 and mana >= 11
            #     cast 'greater heal'

            #     @settimer 'waiting_target_timer' 0
            #     while not targetexists 'beneficial' and timer 'waiting_target_timer' < 3500
            #         wait 100
            #         if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
            #             sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
            #             break
            #         endif
            #     endwhile

            #     if targetexists 'beneficial'
            #         hotkey 'Target Self'
            #         wait action_delay
            #     endif
            endif
        endif
    endif

    if not targetexists 'any'
        if str <= 100 and counttype 'White Potion' > 2
            hotkey 'Drink Strength'
        endif
    endif

    if not targetexists 'any'
        if skill 'chivalry' > 20 or skill 'fencing' > 20 or skill 'archery' > 20 or skill 'swordsmanship' > 20 or skill 'mace fightning' > 20
            if dex <= 100 and counttype 'Blue Potion' > 1
                hotkey 'Drink Agility'
            endif
        endif
    endif

    if not targetexists 'any'
        if skill 'chivalry' > 20 or dex == 100
            if not findbuff 'Magic Resist Potion' and findtype 'Black Potion' backpack as mrpot
                dclick mrpot
                wait action_delay
            endif
        endif
    endif

    ####################################
    # RE-EQUIP LEFT HAND ITEM (IF CHAR HAD TO CAST OR DRINK POTION)
    ####################################

    if varexist lhand_item
        while lhandempty

            if targetexists
                lift lhand_item 1
                drop 'self' LeftHand
            else
                dclick lhand_item
            endif

            wait 100

            sysmsg 'Trying to re-equip left hand...'
            if timer 'waiting_dress_timer' >= 400
                sysmsg 'BREAKING WHILE WAITING TO EQUIP HALBERD'
                break
            endif
        endwhile

        if not lhandempty
            unsetvar lhand_item
        endif

    endif

    ####################################
    # HANDLE WORLD SAVES
    ####################################

    if insysmsg "The world is saving"
        overhead '*WORLD SAVING*' 90
        while not insysmsg 'World save complete'
            wait 100
            if timer 'waiting_save' >= 5000
                sysmsg 'BREAKING WAITING FOR SAVE' 49
                break
            endif
        endwhile

        if insysmsg 'World save complete'
            overhead '*save complete!*' 90
            setvar! reset_last_sysmsg_timer 1
        endif
    endif


    # check if we got any sysmsg that should reset the timer in each iteration
    if varexist reset_last_sysmsg_timer
        if debug == 1
            sysmsg '[DEBUG]: RESETTING LAST SYSMSG TIMER' debug_msg_color
        endif
        unsetvar! reset_last_sysmsg_timer
        # RESET THE TIMER
        settimer 'last_sysmsg_timer' 0

        clearsysmsg
    elseif varexist entered_combat_mode
        unsetvar! entered_combat_mode

        if debug == 1
            sysmsg '[DEBUG]: RESETING SYSMSG TIMER AFTER FLAGING COMBAT MODE' debug_msg_color
        endif
        sysmsg 'Reseting sysmsg timer after flaging combat mode...' 52
        settimer 'last_sysmsg_timer' 0

        clearsysmsg
    endif

endwhile
