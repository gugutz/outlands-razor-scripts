##########################
# SURVIVAL LOOP
# by gugutz
# version 0.0.2
#################################


#################################
# CREDITS :)
#################################

sysmsg '♥ SURVIVAL LOOP by gugutz ♥' 91


#################################
# CHANGELOG
#################################
#
# v0.0.2
# - Fixed some types in monitor list
#
# v0.0.1
# - Initial release


#################################
# CREATE ALL LISTS FOR CONFIG
#################################


if not listexists 'survival_config_lists'
    createlist 'survival_config_lists'
else
    clearlist 'survival_config_lists'
endif

pushlist 'survival_config_lists' 'unequip_hands_to_drink_potion'
pushlist 'survival_config_lists' 'prefer_switching_to_1hand_weapon_instead_of_unequiping'

# routine-related lists
pushlist 'survival_config_lists' 'action_delay'
pushlist 'survival_config_lists' 'packies'
pushlist 'survival_config_lists' 'beta_client'
pushlist 'survival_config_lists' 'reset_last_sysmsg_timer'
pushlist 'survival_config_lists' 'warn_when_items_amount_is_bellow'

pushlist 'survival_config_lists' 'monitor_items_amounts'
pushlist 'survival_config_lists' 'monitor_items_warning_delays'

pushlist 'survival_config_lists' 'use_charges'
pushlist 'survival_config_lists' 'use_recall_scroll'

pushlist 'survival_config_lists' 'drink_refresh_potion'
pushlist 'survival_config_lists' 'drink_heal_potion'
pushlist 'survival_config_lists' 'drink_cure_potion'
pushlist 'survival_config_lists' 'bandage_delay'

pushlist 'survival_config_lists' 'max_diffhits_before_healing_with_potion'
pushlist 'survival_config_lists' 'max_diffhits_before_healing_with_bandages'
pushlist 'survival_config_lists' 'max_diffhits_before_healing_with_magery'
pushlist 'survival_config_lists' 'max_diffweight_before_unloading'
pushlist 'survival_config_lists' 'monitor_items_warning_delay'
pushlist 'survival_config_lists' 'lhand_item'

foreach list in 'survival_config_lists'
    if not listexists list
        createlist list
    else
        clearlist list
    endif
endfor


#################################
# CREATE ALL TIMERS
#################################

createlist 'survival_script_timers'
pushlist 'survival_script_timers' 'loop_on_warning_msg'
pushlist 'survival_script_timers' 'waiting_save'
pushlist 'survival_script_timers' 'last_sysmsg_timer'
pushlist 'survival_script_timers' 'out_of_item_warning_msg'
pushlist 'survival_script_timers' 'bandage_timer'
pushlist 'survival_script_timers' 'waiting_dress_timer'



foreach timer in 'survival_script_timers'
    if not timerexists timer
        createtimer timer
        settimer timer 99999
    endif
endfor


################################
# SCRIPT CONFIGURATION
################################

# if char is dexxer we want to undress and re-arm our main weaopn
# if mage its probably a pvp char so we want to switch to 1 hand (if no target)
if dex == 100
    pushlist 'prefer_switching_to_1hand_weapon_instead_of_unequiping' 'yes'
else
    pushlist 'prefer_switching_to_1hand_weapon_instead_of_unequiping' 'yes'
endif

pushlist 'unequip_hands_to_drink_potion' 'yes'
pushlist 'monitor_items_amounts' 'yes'
pushlist 'beta_client' 'yes'

pushlist 'warn_when_items_amount_is_bellow' 5
pushlist 'action_delay' 600
pushlist 'max_diffhits_before_healing_with_potion' 20
pushlist 'max_diffhits_before_healing_with_bandages' 10
pushlist 'max_diffhits_before_healing_with_magery' 20
pushlist 'max_diffweight_before_unloading' 50
pushlist 'monitor_items_warning_delay' 5000

# set bandage_delay based on char dex
if skill 'healing' >= 40
    if dex >= 100
        pushlist 'bandage_delay' 10000
    elseif dex <= 80
        pushlist 'bandage_delay' 15000
    endif
endif

#################################
# INITIALIZE THE CONFIGURATION VALUES AS VARIABLES BUT FROM LIST SO WE DONT NEED TO ITERATE TO GET THE INDEX 0
if atlist 'action_delay' 0 as action_delay
if atlist 'bandage_delay' 0 as bandage_delay
if atlist 'warn_when_items_amount_is_bellow' 0 as amount
if atlist 'max_diffhits_before_healing_with_potion' 0 as max_diffhits_before_healing_with_potion
if atlist 'max_diffhits_before_healing_with_bandages' 0 as max_diffhits_before_healing_with_bandages
if atlist 'max_diffhits_before_healing_with_magery' 0 as max_diffhits_before_healing_with_magery
if atlist 'max_diffweight_before_unloading' 0 as max_diffweight_before_unloading
if atlist 'monitor_items_warning_delay' 0 as monitor_items_warning_delay



################################
# CODE
################################

# SEED SOME LISTS BASED ON SCRIPT CONFIGURATION

if inlist 'monitor_items_amounts' 'yes'
    if not listexists 'items_to_monitor'
        createlist 'items_to_monitor'
        if skill 'healing' >= 50
            pushlist 'items_to_monitor' 'clean bandage%s%'
            pushlist 'items_to_monitor' 'Yellow Potion'
            pushlist 'items_to_monitor' 'Orange Potion'
            pushlist 'items_to_monitor' 'Red Potion'
            pushlist 'items_to_monitor' 'Black Potion'
            pushlist 'items_to_monitor' 'Purple Potion'
            pushlist 'items_to_monitor' 'White Potion'
            pushlist 'items_to_monitor' 'Blue Potion'
        endif

        if skill 'magery' >= 20
            pushlist 'items_to_monitor' 'Blood Moss'
            pushlist 'items_to_monitor' 'Black Pearl%s%'
            pushlist 'items_to_monitor' 'Garlic'
            pushlist 'items_to_monitor' 'Mandrake Root%s%'
            pushlist 'items_to_monitor' 'Ginseng'
            pushlist 'items_to_monitor' 'Nightshade'
            pushlist 'items_to_monitor' 'Sulfurous Ash'
            pushlist 'items_to_monitor' "Spider's Silk"
        endif

        if skill 'archery' >= 20
            pushlist 'items_to_monitor' 'arrow'
            pushlist 'items_to_monitor' 'crossbow bolt'
        endif
    endif
endif

if inlist 'prefer_switching_to_1hand_weapon_instead_of_unequiping' 'yes'
    # this list  is created separatedely cause we also seed it with values
    if not listexists 'one_handed_weapons'
        createlist 'one_handed_weapons'
        pushlist 'one_handed_weapons' 'katana'
        pushlist 'one_handed_weapons' 'viking sword'
        pushlist 'one_handed_weapons' 'broadsword'
        pushlist 'one_handed_weapons' 'longsword'
    endif
endif




while not dead 'self'

    # remember player the loop is active every 5s
    if timer 'loop_on_warning_msg' >= 5000
        settimer 'loop_on_warning_msg' 0
        sysmsg 'Survival Loop On...' 89
    endif



    ####################################
    # MONITOR ITEM AMMOUNTS AND WARN PLAYER WHEN LOW OR OUT

    if inlist 'monitor_items_amounts' 'yes'
        if timer 'out_of_item_warning_msg' >= monitor_items_warning_delay
            # first we reset the warning msg timer
            if timer 'out_of_item_warning_msg' >= 5000
                settimer 'out_of_item_warning_msg' 0
            endif

            # foreach amount in 'warn_when_items_amount_is_bellow'
            if atlist 'warn_when_items_amount_is_bellow' 0 as amount
                foreach item in 'items_to_monitor'
                    if not findtype item backpack any
                        if inlist 'beta_client' 'yes'
                            sysmsg 'WARNING: Out of {{item}}!' 41
                        else
                            sysmsg 'ITEM AMMOUNT 0' 37
                        endif
                    elseif counttype item backpack <= amount
                        if inlist 'beta_client' 'yes'
                            sysmsg 'WARNING: {{item}} LOW AMMOUNT!' 45
                        else
                            sysmsg 'ITEM AMMOUNT LOW' 39
                        endif
                    endif
                endfor
            endif
            # endfor
        endif
    endif


    ####################################
    # HANDLE PARALYZE
    ####################################

    if paralyzed
        if findtype pouch backpack 38 as trap_pouch
            sysmsg '>>> Using Trapped Pouch!'
            useobject trap_pouch
        else
            overhead 'no trap pouches!' 37
        endif
    endif


    ####################################
    # POTIONS!!!
    ####################################


    # decide first which potion char needs to use this iteration
    if not targetexists 'any'
        if poisoned
            pushlist 'drink_cure_potion' 'yes'
        # restore stamina (pot or food)
        elseif stam == 0
            pushlist 'drink_refresh_potion' 'yes'
            sysmsg 'PREFERRING STAM 0...'
        elseif diffstam > 0
        # elseif diffstam > 0 and diffhits < 10
            sysmsg 'PREFERRING STAM 1...'
            pushlist 'drink_refresh_potion' 'yes'
        elseif diffstam > 0 and diffhits < 30
            pushlist 'drink_heal_potion' 'yes'
            sysmsg 'PREFERRING HEAL 0...'
        elseif diffhits > 0
            pushlist 'drink_heal_potion' 'yes'
            sysmsg 'HEAL 1: ONLY DIFFHITS'
        endif
    endif

    ####################################
    # UNDRESS HANDS FOR POTIONS

    if not targetexists 'any'
    if inlist 'drink_cure_potion' 'yes' or inlist 'drink_heal_potion' 'yes' or inlist 'drink_refresh_potion' 'yes'
        if not lhandempty
            if findlayer self lefthand as lhanditem
                pushlist 'lhand_item' lhanditem
            endif

            if inlist 'prefer_switching_to_1hand_weapon_instead_of_unequiping' 'yes'
                foreach weapon in 'one_handed_weapons'
                    if findtype weapon backpack as weap
                        overhead "*switching to 1-handed*"
                        dclick weap
                        wait 400
                        break
                    endif
                endfor
            elseif inlist 'unequip_hands_to_drink_potion' 'yes'
                hotkey "Undress Left Hand"
                wait 400
            endif
        endif
    endif

    ####################################
    # CURE POTS
    if not targetexists 'any'
        if inlist 'drink_cure_potion' 'yes'
            clearlist 'drink_cure_potion'
            if findtype 'orange potion' as pot
                overhead 'cure pot' 56
                useobject pot

            # elseif magery >= 50 and counttype 'Garlic' > 0 and counttype 'Ginseng' > 0
            #     sysmsg 'Curing poison with magery...' 90
            #     cast 'Cure'

            #     @settimer 'waiting_target_timer' 0
            #     while not targetexists 'beneficial' and timer 'waiting_target_timer' < 1500
            #         wait 100
            #         if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
            #             sysmsg 'BREAKING WHILE WAITING FOR CURE TARGET'
            #             break
            #         endif
            #     endwhile

            #     if targetexists 'beneficial'
            #         target 'self'
            #         wait action_delay
            #     endif

            elseif healing >= 50 and counttype 'clean bandage%s%' > 0
                if timer 'bandage_timer' >= bandage_delay
                    sysmsg 'Curing poison with healing...' 90
                    settimer 'bandage_timer' 0
                    hotkey 'Bandage Self'
                    wait action_delay
                endif
            endif
        endif
    endif

    ####################################
    # REFRESH POTS
    if not targetexists 'any'
        if inlist 'drink_refresh_potion' 'yes'
            clearlist 'drink_refresh_potion'
            if findtype 'red potion' backpack
                sysmsg 'Refresh Potion...' 90
                hotkey 'Drink Refresh'
                wait action_delay
            endif
        endif
    endif

    ####################################
    # HEAL POTS
    if not targetexists 'any'
        if inlist 'drink_heal_potion' 'yes'
            clearlist 'drink_heal_potion'
            if findtype 'yellow potion' backpack
                sysmsg 'Heal Potion...' 90
                hotkey 'Drink Heal'
                wait action_delay
            endif
        endif
    endif

    ####################################
    # HEAL SPELL OR BANDAGE
    ####################################

    if not targetexists 'any'
        if diffhits > 0
            # heal with healing
            if diffhits > max_diffhits_before_healing_with_bandages and skill 'healing' >= 50 and counttype 'clean bandage%s%' > 0
                if timer 'bandage_timer' >= bandage_delay
                    settimer 'bandage_timer' 0
                    hotkey 'Bandage Self'
                    wait action_delay
                endif

            # heal with magery
            elseif diffhits > max_diffhits_before_healing_with_magery and skill 'magery' >= 50 and mana >= 11
                cast 'greater heal'

                @settimer 'waiting_target_timer' 0
                while not targetexists 'beneficial' and timer 'waiting_target_timer' < 3500
                    wait 100
                    if insysmsg 'disturbed' or insysmsg 'cannot cast a spell while frozen'
                        sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                        break
                    endif
                endwhile

                if targetexists 'beneficial'
                    hotkey 'Target Self'
                    wait action_delay
                endif
            endif
        endif
    endif


    ####################################
    # RE-EQUIP LEFT HAND ITEM (IF CHAR HAD TO CAST OR DRINK POTION)
    ####################################

    if atlist 'lhand_item' 0 as lhanditem
        while lhandempty

            if targetexists
                hotkey 'Drop What You Are Currently Holding'
                lift lhanditem 1
                drop 'self' LeftHand
            else
                dclick lhanditem
            endif

            wait 100

            sysmsg 'Trying to re-equip left hand...'
            if timer 'waiting_dress_timer' >= 400
                sysmsg 'BREAKING WHILE WAITING TO EQUIP HALBERD'
                break
            endif
        endwhile

        if not lhandempty
            clearlist 'lhand_item'
        endif

    endif

    ####################################
    # HANDLE WORLD SAVES
    ####################################

    if insysmsg "The world is saving"
        overhead '*WORLD SAVING*' 90
        while not insysmsg 'World save complete'
            wait 100
            if timer 'waiting_save' >= 5000
                sysmsg 'BREAKING WAITING FOR SAVE' 49
                break
            endif
        endwhile

        if insysmsg 'World save complete'
            overhead '*save complete!*' 90
            pushlist 'reset_last_sysmsg_timer' 'yes'
        endif
    endif

endwhile

# closing the ifs that initialized the config variables on top of tile
endif
endif
endif
endif
endif
endif
endif
endif
