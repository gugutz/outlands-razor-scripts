sysmsg '#########################' 90
sysmsg '>>> ARENA HEAL BOT' 55
sysmsg 'by gugutz' 45
sysmsg 'version 0.0.1' 31
sysmsg '########################' 90

#################################
# BOOTSTRAPING
#################################

# CREATE ALL LISTS FOR CONFIG

if not listexists 'arena_heal_bot_config_lists'
    createlist 'arena_heal_bot_config_lists'
else
    clearlist 'arena_heal_bot_config_lists'
endif

pushlist 'arena_heal_bot_config_lists' 'prefer_switching_to_1hand_weapon_instead_of_unequiping'

# routine-related lists
pushlist 'arena_heal_bot_config_lists' 'reset_last_sysmsg_timer'
pushlist 'arena_heal_bot_config_lists' 'drink_refresh_potion'
pushlist 'arena_heal_bot_config_lists' 'drink_heal_potion'
pushlist 'arena_heal_bot_config_lists' 'drink_cure_potion'
pushlist 'arena_heal_bot_config_lists' 'unequip_hands_to_drink_potion'
pushlist 'arena_heal_bot_config_lists' 'dont_unequip_if_already_equiped'
pushlist 'arena_heal_bot_config_lists' 'auto_walk_randomly'

pushlist 'arena_heal_bot_config_lists' 'bigspells'
pushlist 'arena_heal_bot_config_lists' 'smallspells'

pushlist 'arena_heal_bot_config_lists' 'current_hp'
pushlist 'arena_heal_bot_config_lists' 'current_max_hp'

pushlist 'arena_heal_bot_config_lists' 'ready_match'
pushlist 'arena_heal_bot_config_lists' 'cancel_current_match'
pushlist 'arena_heal_bot_config_lists' 'first_run_done'
pushlist 'arena_heal_bot_config_lists' 'last_max_hp'



foreach list in 'arena_heal_bot_config_lists'
    if not listexists list
        createlist list
    else
        clearlist list
    endif
endfor


################################
# CREATE ALL TIMERS
################################

createlist 'arena_heal_bot_script_timers'
pushlist 'arena_heal_bot_script_timers' 'loop_on_warning_msg'
pushlist 'arena_heal_bot_script_timers' 'waiting_save'
pushlist 'arena_heal_bot_script_timers' 'last_sysmsg_timer'
pushlist 'arena_heal_bot_script_timers' 'waiting_target_timer'
pushlist 'arena_heal_bot_script_timers' 'waiting_spell_timer'
pushlist 'arena_heal_bot_script_timers' 'bandage_timer'
pushlist 'arena_heal_bot_script_timers' 'refresh_pot_timer'

pushlist 'arena_heal_bot_script_timers' 'debuff_enemy_timer'
pushlist 'arena_heal_bot_script_timers' 'attack_enemy_timer'
pushlist 'arena_heal_bot_script_timers' 'combo_exp_eb_poison_timer'
pushlist 'arena_heal_bot_script_timers' 'disrupt_chain_timer'
pushlist 'arena_heal_bot_script_timers' 'walk_timer'
pushlist 'arena_heal_bot_script_timers' 'meditating_msg_warning_timer'
pushlist 'arena_heal_bot_script_timers' 'match_duration_print_timer'

pushlist 'arena_heal_bot_script_timers' 'bigspells_timer'
pushlist 'arena_heal_bot_script_timers' 'smallspells_timer'

pushlist 'arena_heal_bot_script_timers' 'cancel_current_match_timer'

pushlist 'arena_heal_bot_script_timers' 'print_match_stats_timer'


foreach timer in 'arena_heal_bot_script_timers'
    if not timerexists timer
        createtimer timer
        settimer timer 0
    endif
endfor

# advance some timers so their related function runs immediately the first time
settimer 'loop_on_warning_msg' 99999
settimer 'attack_enemy_timer' 99999
settimer 'debuff_enemy_timer' 99999
settimer 'disrupt_chain_timer' 1000


################################
# SCRIPT CONFIGURATION
################################

pushlist 'prefer_switching_to_1hand_weapon_instead_of_unequiping' 'yes'
pushlist 'unequip_hands_to_drink_potion' 'yes'
pushlist 'auto_walk_randomly' 'yes'

@setvar action_delay_var 600

# HEALING SETTINGS
@setvar min_hp_before_miniheal 20
@setvar min_hp_before_gheal 70

@setvar min_mana_before_curing_poison 6
@setvar min_mana_before_miniheal 4
@setvar min_mana_before_gheal 11

@setvar min_stam_before_refresh_pot 18
@setvar max_diffhits_before_healing_with_bandages 10


# hp limit so bot keeps performing offensive actions
@setvar min_hp_for_offense 40

@setvar min_mana_before_debuffing_enemy 40
@setvar min_mana_before_disrupt_chains 70
@setvar min_mana_before_meditating 15

@setvar min_mana_before_smallspells 40
@setvar min_mana_before_bigspells 70
@setvar min_mana_before_exp_eb_poison_combo 80

@setvar smallspell_interval 10000
@setvar bigspell_interval 30000
@setvar combo_exp_eb_poison_interval 45000
@setvar disrupt_chain_interval 20000


###################################
# SCRIPT (NON CONFIG RELATED) LISTS AND VARIABLES
###################################

pushlist 'last_max_hp' 100

@setvar arena_stone_serial '0x40000ECB'
@setvar arena_stone_gump_id 4264910157
@setvar arena_match_results_gump_id 838769207

if not listexists 'heal_bot_weapons'
    createlist 'heal_bot_weapons'
    pushlist 'heal_bot_weapons' 'heavy crossbow'
    pushlist 'heal_bot_weapons' 'crossbow'
    pushlist 'heal_bot_weapons' 'bow'
endif
# set each weapon's timer
foreach weapon in 'heal_bot_weapons'
    createtimer weapon

    if weapon == 'heavy crossbow'
        sysmsg 'Setting Heavy Xbow timer to 7s'
        settimer weapon  8000
    elseif weapon == 'crossbow'
        sysmsg 'Setting Crossbow timer to 5s'
        settimer weapon  3000
    elseif weapon == 'bow'
        sysmsg 'Setting Bow timer to 3s'
        settimer weapon  0
    endif
endfor


# LIST OF DEBUFF SPELLS, CAST EVERY 60s
if not listexists 'debuff_spells'
    createlist 'debuff_spells'
    pushlist 'debuff_spells' 'weaken'
    pushlist 'debuff_spells' 'feeblemind'
    pushlist 'debuff_spells' 'clumsy'
endif
foreach spell in 'debuff_spells'
    if not timerexists spell
        createtimer spell
        settimer spell  5001
    endif
endfor


if not listexists 'disrupt_spells'
    createlist 'disrupt_spells'
    pushlist 'disrupt_spells' 'harm'
    pushlist 'disrupt_spells' 'magic arrow'
    pushlist 'disrupt_spells' 'fireball'
    pushlist 'disrupt_spells' 'lightning'
endif
foreach spell in 'disrupt_spells'
    if not timerexists spell
        createtimer spell
        settimer spell  5001
    endif
endfor

if not listexists 'combo_exp_eb_poison_spells'
    createlist 'combo_exp_eb_poison_spells'
    pushlist 'combo_exp_eb_poison_spells' 'explosion'
    pushlist 'combo_exp_eb_poison_spells' 'energy bolt'
    pushlist 'combo_exp_eb_poison_spells' 'poison'
endif
foreach spell in 'combo_exp_eb_poison_spells'
    if not timerexists spell
        createtimer spell
        settimer spell  10001
    endif
endfor

###################################
# create a timer for each spell


################################
# CODE
################################

while not dead 'self' or dead 'self'


    if diffhits < 10 and not inlist 'first_run_done' 'yes'
        clearsysmsg

        pushlist 'cancel_current_match' 'yes'

        overhead 'FIRST RUN FINISHED' 91
        pushlist 'first_run_done' 'yes'
    endif


    # WHILE MATCH DOENST START, MAKE SURE TO KEEP A MATCH IN THE ARENA STONE
    while find arena_stone_serial ground any any 10


        ##########################
        # DRESS BOT WHEN IT DIES
        while not findlayer self innertorso or not findlayer self head or not findlayer self gloves or not findlayer self neck or not findlayer self arms or not findlayer self pants
            sysmsg 'Dressing Bot...'
            dress 'heal-bot'
            wait 1000
            # while queued
            #     sysmsg 'Waiting for dress to finish...'
            #     wait 1500
            # endwhile
        endwhile


        ##########################
        # CLICK ARENA STONE TO OPEN ARENA GUMP
        while find arena_stone_serial ground any any 10 and not gumpexists arena_stone_gump_id
            sysmsg 'Opening Arena Stone Gump'
            dclick arena_stone_serial
            waitforgump arena_stone_gump action_delay_var
        endwhile


        ##########################
        # CHECK AND CREATE MATCH IF NEEDED
        if gumpexists arena_stone_gump_id
            # CHANGE TO MATCHES PAGE IF NEEDED
            while gumpexists arena_stone_gump_id and not ingump 'Refresh Listings' 4264910157
                sysmsg 'Clicking Matches Page...'
                gumpresponse 2 4264910157
                waitforgump 4264910157 600
            endwhile

            # cancel current match when needed (like oponent going AFK...)
            if inlist 'cancel_current_match' 'yes'
                clearlist 'cancel_current_match'

                if ingump 'Cancel Current Match' 4264910157
                    sysmsg 'Canceling current match...'
                    gumpresponse 12 4264910157
                    waitforgump 4264910157 600
                endif
            endif

            # if name of char is not in gump, match is not created
            if not ingump 'Ready' 4264910157
                sysmsg 'Creating Match...' 90
                gumpresponse 12 4264910157
                waitforgump 4264910157 600

                if insysmsg 'You may not create a new match while a tournament is about to begin or in already underway'
                endif
                gumpresponse 12 4264910157
                waitforgump 4264910157 600

                pushlist 'ready_match' 'yes'
            endif

            # click 'Ready' btn on new matches or when a match ends
            if inlist 'ready_match' 'yes'
                sysmsg 'READYING MATCH...'
                clearlist 'ready_match'
                gumpresponse 20 4264910157
                waitforgump 4264910157 600
            endif

            if ingump 'Ready' 4264910157

                # keep announcing in-game that the match is up for joins
                if timer 'loop_on_warning_msg' >= 10000
                    settimer 'loop_on_warning_msg' 0
                    overhead '[ARENA TRAINER] Match UP' 89
                endif
            endif
        endif

        if timer 'cancel_current_match_timer' >= 120000
            # say 'Cancelling currrent match due to innactivity...'
            settimer 'cancel_current_match_timer' 0
            pushlist 'cancel_current_match' 'yes'
        endif
    endwhile


    ##################################
    # IN-MATCH ROUTINE...
    ##################################

    while not 'dead' and not find arena_stone_serial ground any any 10

        if gumpexists arena_match_results_gump_id
            if dead 'self'
                say 'Match Over! Congratulations!'
            else
                say 'Match Ended.'
            endif

            break
        endif

        while findtype 128 ground any any 5
            sysmsg 'Waiting For Match to Start...'
            wait 500
        endwhile


        ##################################
        # KEEP TRACK OF MATCH DURATION AND PRINT IT SOMETIMES
        ##################################

        if not timerexists 'match_duration_timer'
            sysmsg 'Creating Match Duration Timer...'
            createtimer 'match_duration_timer'
            settimer 'match_duration_timer' 0
        endif

        if timer 'match_duration_print_timer' > 900000
            settimer 'match_duration_print_timer' 0

            if timer 'match_duration_timer' > 900000
                say 'Match is over 15 minutes now!'
            elseif timer 'match_duration_timer' > 600000
                say 'Match is over 10 minutes now!'
            elseif timer 'match_duration_timer' > 300000
                say 'Match is over 5 minutes now!'
            endif
        endif



        ##################################
        # MONITOR BOT STATUS AND KEEP IT ALIVE
        ##################################

        if poisoned and hp >= min_hp_before_miniheal
            if mana >= min_mana_before_curing_poison
                sysmsg '>>> CURING...' 76
                cast 'cure'

                @settimer 'waiting_target_timer' 0
                while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4000
                    wft 100
                    if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                        sysmsg 'BREAKING WHILE WAITING FOR CURE TARGET'
                        wait 150
                        break
                    endif
                endwhile

                if targetexists 'beneficial' and hits < maxhits
                    hotkey 'Target Self'
                    wait 150
                endif
            else
                sysmsg 'NO MANA FOR CURE!' 43
            endif
        endif

        # mini heal if diffhits is small
        if hits <= min_hp_before_miniheal
            if mana >= min_mana_before_miniheal
                sysmsg '>>> MINI HEALING...' 78
                cast 'heal'

                @settimer 'waiting_target_timer' 0
                while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 1500
                    wft 100
                    if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                        sysmsg 'BREAKING WHILE WAITING FOR HEAL TARGET'
                        wait 150
                        break
                    endif
                endwhile

                if targetexists 'beneficial' and hits < maxhits
                    hotkey 'Target Self'
                    wait 150
                endif
            else
                sysmsg 'NO MANA FOR MINIHEAL!' 43
            endif

        # greater heal if diffhits is big
        elseif hits <= min_hp_before_gheal
            if mana >= min_mana_before_gheal
                sysmsg '>>> GREATER HEALING...' 76
                cast 'greater heal'

                @settimer 'waiting_target_timer' 0
                while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 4000
                    wft 100
                    if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                        sysmsg 'BREAKING WHILE WAITING FOR GREATER HEAL TARGET'
                        wait 150
                        break
                    endif
                endwhile

                if targetexists 'beneficial' and hits < maxhits
                    hotkey 'Target Self'
                    wait 150
                endif

            else
                sysmsg 'NO MANA FOR GREATER HEAL!' 43
            endif
        endif


        ##################################
        # MEDIDATE WHEN NEEDED
        ##################################

        if hp > min_hp_before_gheal and mana <= min_mana_before_meditating
            while hp > min_hp_before_gheal and mana < 50
                while hp > min_hp_before_gheal and not findbuff 'Actively Meditating'
                    useskill 'meditation'
                    wait 1000
                endwhile

                while hp > min_hp_before_gheal and findbuff 'Actively Meditating' and mana < maxmana
                    wait 100

                    if timer 'meditating_msg_warning_timer' >= 4000
                        say '*meditating*'
                        settimer 'meditating_msg_warning_timer' 0
                    endif
                endwhile
            endwhile
        endif


        ##################################
        # ARM THE BOT
        ##################################

        if not findbuff 'Actively Meditating' and hp >= min_hp_for_offense
            if lhandempty and rhandempty
                createlist 'weapon_swing_timer'
                clearlist 'weapon_swing_timer'

                foreach weapon in 'heal_bot_weapons'

                    if weapon == 'heavy crossbow'
                        pushlist 'weapon_swing_timer' 7500
                    elseif weapon == 'crossbow'
                        pushlist 'weapon_swing_timer' 5500
                    elseif weapon == 'bow'
                        pushlist 'weapon_swing_timer' 4000
                    endif

                    if atlist 'weapon_swing_timer' 0 as weapon_swing_timer

                        if timer weapon < weapon_swing_timer
                            sysmsg 'Not time to equip {{weapon}} yet...'
                        else
                            if findtype weapon backpack as weap
                                # overhead 'equiping weapon' 48
                                if targetexists 'any'

                                    lift weap 1
                                    wait 100
                                    drop 'self' LeftHand
                                    # tab out, good for duels
                                    overhead 'attacking!' 41 'last'
                                    hotkey 'Attack Last Target'
                                    settimer weapon 0
                                else
                                    dclick weap
                                    overhead 'attacking!' 41 'last'
                                endif
                            endif
                        endif
                    endif

                endfor
                wait action_delay_var
            endif
        endif


        ##################################
        # KEEP STAMINA IN CHECK
        ##################################

        if hp > min_hp_before_gheal and mana > min_mana_before_meditating
            if stam < min_stam_before_refresh_pot and timer 'refresh_pot_timer' >= 1000
                @settimer 'refresh_pot_timer' 0
                potion 'refresh'
                say '*using refresh pot*'
                sysmsg 'Refresh Pot!' 90
            endif
        endif


        ##################################
        # ATTACK ENEMY
        ##################################

        if timer 'attack_enemy_timer' >= 3000
            @settimer 'attack_enemy_timer' 0
            hotkey 'Next Enemy Humanoid Target'
            hotkey 'Attack Last Target'
        endif





        ##################################
        # DEBUFF ENEMY EVERY 1 MINUTE
        ##################################

        if mana >= min_mana_before_debuffing_enemy
            if hp > min_hp_for_offense and timer 'debuff_enemy_timer' >= 60000
                foreach spell in 'debuff_spells'
                    if hp <= min_hp_before_gheal
                        sysmsg 'Breaking on casting {{spell}} for heal...'
                        break
                    endif

                    if timer spell < 1600
                        sysmsg 'Not time for {{spell}} yet...'
                    else
                        sysmsg 'Casting {{spell}}...'
                        @settimer 'waiting_spell_timer' 0
                        cast spell

                        while not targetexists 'harmful' and timer 'waiting_spell_timer' < 2500
                            wait 100
                            if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                                sysmsg 'BREAKING WHILE WAITING FOR SPELL'
                                wait 150
                                break
                            endif
                        endwhile

                        wait 100

                        if targetexists 'harmful'
                            target 'last'
                            sysmsg 'Reseting {{spell}} timer...'
                            settimer spell 0
                        endif
                        wait 100
                    endif

                endfor

                sysmsg 'Reseting Debuff Timer...'
                settimer 'debuff_enemy_timer' 0
                wait action_delay_var
            endif
        else
            sysmsg 'No Mana for Debuffing yet...'
        endif

        ##################################
        # CAST A SMALL SPELL EVERY ONCE IN A WHILE
        #
        ##################################

        # determine which bigspell the bot is gonna use (33% being exp, 66% for EB)
        clearlist 'smallspells'

        clearsysmsg
        random 6
        if insysmsg 'Random: 1'
            pushlist 'smallspells' 'weaken'
        elseif insysmsg 'Random: 2'
            pushlist 'smallspells' 'harm'
        elseif insysmsg 'Random: 3'
            pushlist 'smallspells' 'magic arrow'
        elseif insysmsg 'Random: 4'
            pushlist 'smallspells' 'poison'
        elseif insysmsg 'Random: 5'
            pushlist 'smallspells' 'fireball'
        elseif insysmsg 'Random: 6'
            pushlist 'smallspells' 'lightning'
        endif

        if mana >= min_mana_before_smallspells
            if hp >= min_hp_for_offense and timer 'smallspells_timer' >= smallspell_interval

                if atlist 'smallspells' 0 as spell

                    if hp <= min_hp_before_gheal
                        sysmsg 'BREAKING ON CASTING {{spell}}...'
                        break
                    endif

                    if timer spell < 10000
                        sysmsg 'NOT TIME FOR {{spell}} yet...'
                    elseif mana < 20
                        sysmsg 'NO MANA FOR {{spell}} yet...'
                    else
                        sysmsg 'CASTING {{spell}}...'
                        @settimer 'waiting_spell_timer' 0
                        cast spell

                        while not targetexists 'harmful' and timer 'waiting_spell_timer' < 3500
                            wait 100
                            if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                                sysmsg 'BREAKING WHILE WAITING FOR SPELL'
                                wait 150
                                break
                            endif
                        endwhile

                        wait 100

                        if targetexists 'harmful'
                            target 'last'
                            sysmsg 'RESETING {{spell}} TIMER...'
                            settimer spell 0
                        endif

                        wait 100
                    endif

                    if index == 3 or spell == 'poison'
                        sysmsg 'LAST COMBO INDEX RESETING COMBO ENEMY TIMER' 90
                        settimer 'smallspells_timer' 0
                        wait action_delay_var
                    endif

                endif

            endif
        else
            sysmsg 'No Mana for Combos...'
        endif


        ##################################
        # CYCLE DISRUPT CHAIN COMBO
        ##################################

        if mana >= min_mana_before_disrupt_chains
            if hp >= min_hp_for_offense and timer 'disrupt_chain_timer' >= disrupt_chain_interval
                sysmsg 'STARTING DISRUPT CHAIN...' 90

                foreach spell in 'disrupt_spells'

                    if hp <= min_hp_before_gheal
                        sysmsg 'Breaking on casting {{spell}} for heal...'
                        break
                    endif

                    # check for current spell timer
                    if timer spell < 5000
                        sysmsg 'Not time for {{spell}} yet...'
                    else
                        sysmsg 'Casting {{spell}}...'
                        @settimer 'waiting_spell_timer' 0
                        cast spell

                        while not targetexists 'harmful' and timer 'waiting_spell_timer' < 2500
                            wait 100
                            if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                                sysmsg 'BREAKING WHILE WAITING FOR SPELL'
                                wait 150
                                break
                            endif
                        endwhile

                        wait 100

                        if targetexists 'harmful'
                            sysmsg 'Reseting {{spell}} timer...'
                            settimer spell 0
                            target 'last'
                        endif

                        wait 100
                    endif

                    if index == 3 or spell == 'lightning'
                        sysmsg 'LAST DISRUPT CHAIN INDEX RESETING DISRUPT ENEMY TIMER' 90
                        settimer 'disrupt_chain_timer' 0
                    endif

                    wait action_delay_var
                endfor

                # settimer 'disrupt_chain_timer' 0
                # wait action_delay_var
            endif
        else
            sysmsg 'No Mana for Disrupt Chain...'
        endif


        ##################################
        # COMBO ENEMY EVERY ONCE IN A WHILE
        #
        # PS: this is a continuos combo, the char will not stop between spells to re-arm
        ##################################

        # 1 in 7 chances of the bot casting the exp eb poison combo
        random 7
        if insysmsg 'Random: 1'
            if mana >= min_mana_before_exp_eb_poison_combo
                if hp >= min_hp_for_offense and timer 'combo_exp_eb_poison_timer' >= combo_exp_eb_poison_interval
                    foreach spell in 'combo_exp_eb_poison_spells'

                        if hp <= min_hp_before_gheal
                            sysmsg 'Breaking on casting {{spell}} for heal...'
                            break
                        endif

                        if timer spell < 10000
                            sysmsg 'Not time for {{spell}} yet...'
                        elseif mana < 20
                            sysmsg 'No mana for {{spell}} yet...'
                        else
                            sysmsg 'Casting {{spell}}...'
                            @settimer 'waiting_spell_timer' 0
                            cast spell

                            while not targetexists 'harmful' and timer 'waiting_spell_timer' < 3500
                                wait 100
                                if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                                    sysmsg 'BREAKING WHILE WAITING FOR SPELL'
                                    wait 150
                                    break
                                endif
                            endwhile

                            wait 100

                            if targetexists 'harmful'
                                target 'last'
                                sysmsg 'Reseting {{spell}} timer...'
                                settimer spell 0
                                # this break is so char will continue and equip weapon and hit in
                                # the middle of the combo. we control the iteration with timers
                                break
                            endif
                            wait 100
                        endif

                        if index == 3 or spell == 'poison'
                            sysmsg 'LAST COMBO INDEX RESETING COMBO ENEMY TIMER' 90
                            settimer 'combo_exp_eb_poison_timer' 0
                            wait action_delay_var
                        endif

                    endfor

                endif
            else
                sysmsg 'NO MANA FOR EXP + EB + NOX COMBO...'
            endif

        endif

        ##################################
        # CAST A BIG SPELL EVERY ONCE IN A WHILE
        #
        ##################################

        # determine which bigspell the bot is gonna use (33% being exp, 66% for EB)
        clearlist 'bigspells'
        random 3
        if insysmsg 'Random: 1'
            pushlist 'bigspells' 'explosion'
        else
            pushlist 'bigspells' 'energy bolt'
        endif

        if mana >= min_mana_before_bigspells
            if hp >= min_hp_for_offense and timer 'bigspells_timer' >= bigspell_interval
                if atlist 'bigspells' 0 as spell

                    if hp <= min_hp_before_gheal
                        sysmsg 'Breaking on casting {{spell}} for heal...'
                        break
                    endif

                    if timer spell < 10000
                        sysmsg 'NO TIME FOR {{spell}} YET...'
                    elseif mana < 20
                        sysmsg 'NO MANA FOR {{spell}} YET...'
                    else
                        sysmsg 'CASTING {{spell}}...'
                        @settimer 'waiting_spell_timer' 0
                        cast spell

                        while not targetexists 'harmful' and timer 'waiting_spell_timer' < 3500
                            wait 100
                            if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                                sysmsg 'BREAKING WHILE WAITING FOR SPELL'
                                wait 150
                                break
                            endif
                        endwhile

                        wait 100

                        if targetexists 'harmful'
                            target 'last'
                            sysmsg 'Reseting {{spell}} timer...'
                            settimer spell 0
                        endif

                        wait 100
                    endif

                    if index == 3 or spell == 'poison'
                        sysmsg 'LAST COMBO INDEX RESETING COMBO ENEMY TIMER' 90
                        settimer 'bigspells_timer' 0
                        wait action_delay_var
                    endif

                endif

            endif
        else
            sysmsg 'No Mana for Combos...'
        endif


        ##################################
        # MOVE THE BOT
        ##################################

        # play a dice to check if we must move at each iteratio (50% chance)
        if inlist 'auto_walk_randomly' 'yes' and timer 'walk_timer' > 2000
            clearsysmsg
            random 5
            if insysmsg 'Random: 1'
                # reset timer at random times, try to simulate realistic movement
                clearsysmsg
                random 4
                if insysmsg 'Random: 1'
                    sysmsg 'Reseting Walk Timer...' 90
                    settimer 'walk_timer' 0
                endif

                # choose direction to walk based on random number
                clearsysmsg
                random 8
                if insysmsg 'Random: 1'
                    walk 'south'
                    walk 'south'
                    walk 'south'
                elseif insysmsg  'Random: 2'
                    walk 'down'
                    walk 'down'
                    walk 'down'
                elseif insysmsg  'Random: 3'
                    walk 'east'
                    walk 'east'
                    walk 'east'
                elseif insysmsg  'Random: 4'
                    walk 'right'
                    walk 'right'
                    walk 'right'
                elseif insysmsg  'Random: 5'
                    walk 'north'    
                    walk 'north'
                    walk 'north'
                elseif insysmsg  'Random: 6'
                    walk 'up'
                    walk 'up'
                    walk 'up'
                elseif insysmsg  'Random: 7'
                    walk 'west'
                    walk 'west'
                    walk 'west'
                elseif insysmsg  'Random: 8'
                    walk 'left'
                    walk 'left'
                    walk 'left'
                endif
            endif
        endif


        ##################################
        # PRINT BOT STATS
        ##################################

        clearlist 'current_max_hp'
        if maxhp == 89
            pushlist 'current_max_hp' 89
        elseif maxhp == 100
            pushlist 'current_max_hp' 100
        elseif maxhp == 111
            pushlist 'current_max_hp' 111
        endif

        ##########################
        # initialize list variables (no indentation on purpose)
        if atlist 'last_max_hp' 0 as last_maxhp
        sysmsg 'INSIDE LASTMAXHP'
        if atlist 'current_max_hp' 0 as current_maxhp
        sysmsg 'INSIDE CURRENTMAXHP'


        # PRINT STATUS CHANGES
        if last_maxhp == 89 or last_maxhp == 100 or last_maxhp == 111
            if maxhp != last_maxhp
                # update 'last_max_hp'
                clearlist 'last_max_hp'
                if maxhp == 89
                    pushlist 'last_max_hp' 89
                elseif maxhp == 100
                    pushlist 'last_max_hp' 100
                elseif maxhp == 111
                    pushlist 'last_max_hp' 111
                endif

                if maxhp == 100
                    say 'MaxHP = {{current_maxhp}} | Weaken Expired!'
                else
                    # say '[HP] {{current_maxhp}}'
                    overhead '[MaxHP: {{current_maxhp}}]'
                    overhead '[HP] {{last_maxhp}}'
                endif
            endif
        endif

        ##################################
        # PRINT MATCH STATS
        ##################################

        if timer 'print_match_stats_timer' >= 60000
            settimer 'print_match_stats_timer' 0
            say '[HP] {{last_maxhp}}'
            overhead '[HP] {{last_maxhp}}'
            say '[HP] {{current_maxhp}}'
            overhead '[HP] {{current_maxhp}}'
        endif

        endif
        endif
        ##########################


        # if diffhits == 0 and maxhp < 110
        #     sysmsg '>>> Strenght' 76
        #     cast 'strength'

        #     @settimer 'waiting_target_timer' 0
        #     while not targetexists 'beneficial' and not insysmsg 'disturbed' and not insysmsg 'cannot cast a spell' and timer 'waiting_target_timer' < 2000
        #         wft 100
        #         if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
        #             sysmsg 'BREAKING WHILE WAITING FOR STRENGTH'
        #             wait 150
        #             break
        #         endif
        #     endwhile

        #     if targetexists 'beneficial' and maxhp < 110
        #         hotkey 'Target Self'
        #         wait 150
        #     endif
        # endif




    # END IN-MATCH ROUTINE...
    endwhile

    waitforgump arena_match_results_gump_id 3000

    # if there is a results gump with char name on it, a duel just finished
    if dead 'self'
        sysmsg 'Match Ended'

        # no need to recreate the match, player might want a rematch
        settimer 'cancel_current_match_timer' 0

        # set to ready the match as soon as char is back from pit
        pushlist 'ready_match' 'yes'

        if ingump 'learning arena' 838769207 or ingump 'Emacs' 838769207
            sysmsg 'FOUND CHAR NAME IN GUMP'
            gumpclose arena_match_results_gump_id
        endif
    endif

    clearsysmsg
endwhile
