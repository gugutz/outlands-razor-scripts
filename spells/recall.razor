# SMART RECALL
# v0.0.1
# by gugutz
#
# ################################
#
# DESCRICAO
#
# Este script use recall, e faz as seguintes verificações:
#
#  1. Se o char tem herding, verifica se tem um sheps crook na bag
#  2. Se o char é bardo, verifica se existe um instrumento mágico na bag
#  3. Se o char for skinner, verifica se existe uma skinning knife na bag
#  4. Se o char tem camping, verifica se tem kindling na backpack
#  5. Se um target neutral estiver aberto ao pressionar a hotkey,
#     da target automatico no primeiro runetome/runebook que encontrar na bag
#  6. Se existe pocao de heal, refresh e cure na backpack
#
#
#
#################################
# CREDITS :)
#################################


# 33% chance of printing credits, so we dont spam it
sysmsg '♥ SMART RECALL by gugutz ♥' 91
clearsysmsg

#################################
# CONFIGURATIOn
#################################

setvar! warning_msg_color 54
setvar! alert_msg_color 39


#############################
# SET THE HOME BOOK
#############################


# search runetomes first
if not varexist home_book
    while findtype 'runetome' backpack as found
        getlabel found rtome_label
        if 'home' in rtome_label or 'Home' in rtome_label or 'HOME' in rtome_label
            sysmsg 'FOUND HOME RUNETOME {{rtome}}' 54
            setvar! home_book found
            setvar! home_book_gump_id 167090027
            setvar! home_rune_btn_number runetome_rune_position
            # setvar! recall_by_charge_btn_number runetome_rune_position
            setvar! recall_by_charge_btn_number 100
    
        endif
    
        @ignore found
    endwhile
endif
@clearignore

# if we couldnt find a home runetome, search for a home runebook
if not varexist home_book
    while findtype 'runebook' backpack as found
        getlabel found rbook_label
        if 'home' in rbook_label or 'Home' in rbook_label or 'HOME' in rbook_label
            sysmsg 'FOUND HOME RUNEBOOK {{rtome}}' 54
            setvar! home_book found
            setvar! home_book_gump_id 1551740969
            setvar! home_rune_btn_number runetome_rune_position
            setvar! recall_by_charge_btn_number 2
        endif

        @ignore found
    endwhile
    @clearignore
endif


##################################
# TARGET RUNEBOOKS IF TARGET IS OPEN
##################################

if targetexists 'neutral'
    if not varexist home_book
        sysmsg 'entered 1'
        if findtype 'runetome' backpack as rtome
            overhead 'targetting runetome!' 90
            target rtome
            stop
        elseif findtype 'runebook' backpack as rbook
            overhead 'targetting runebook!' 90
            target rbook
            stop
        endif

        sysmsg 'HOME BOOK NOT SET!' 39
        overhead 'HOME BOOK NOT SET!' 39
        stop
    endif

    overhead '*targeting home book*' 90
    target home_book
    stop
endif



################################
# RECALL
################################


if skill 'chivalry' >= 20
    say '[sacredjourney'
    wait 200
    stop
endif


# check of char has reagents
if counttype "Black Pearl%s%" == 0 or counttype "Blood Moss" == 0 or counttype "Mandrake Root%s%" == 0

    if counttype "Recall" backpack == 0
        overhead 'no ways to recall' warning_msg_color

        if findtype 'bag' backpack as restock_bag
            overhead 'try to open reg bag' 58
            dclick restock_bag
            wait action_delay
        endif

        if findtype 'reagent satchel' as satchel
            overhead 'try to open satchel' 58
            dclick satchel
            wait action_delay
        endif

        stop
    endif

    overhead 'no regs. trying scroll...'
    setvar! use_scroll true
endif

if varexist use_scrolls
    sysmsg '>> Recall Scroll...' 56
    overhead '*using recall scroll*' 56
    usetype 'Recall'
endif

sysmsg '>> Recalling...' 56
hotkey 'Clear Target Queue'
hotkey 'Recall'

if followers > 0
    say 'all follow me'
endif




##################################
# CHECK IF PLAYER IS FORGETTING
# SOMETHING WHILE RECALL IS BEING CAST
##################################

if not findtype 'tray' backpack
    sysmsg 'NO DELECTABLE' warning_msg_color
    # overhead 'NO DELECTABLE' warning_msg_color
endif

# check if all potions are in
if not findtype 'yellow potion'
    # overhead '!NO HEAL POTS!' warning_msg_color
    sysmsg '!NO HEAL POTS!' warning_msg_color
endif

if not findtype 'orange potion'
    # overhead '!NO HEAL POTS!' warning_msg_color
    sysmsg '!NO HEAL POTS!' warning_msg_color
endif

if not findtype 'red potion'
    # overhead '!NO REFRESH POTS!' warning_msg_color
    sysmsg '!NO REFRESH POTS!' warning_msg_color
endif

# if herder, check for sheps crook
if skill 'herding' > 0 and not findtype 3713 backpack
    # overhead "NO SHEPERD'S CROOK!" warning_msg_color
    sysmsg "NO SHEPERD'S CROOK!" warning_msg_color
endif

# if skinner, check for skinning knife
if skill 'forensic' > 0 and not findtype 'elven spellblade' backpack
    # overhead 'NO SKINNING KNIFE!' warning_msg_color
    sysmsg 'NO SKINNING KNIFE!' warning_msg_color
endif

# if archer, check for arrows and bolts
if skill 'archery' > 0 and not findtype 'arrow' and not findtype 'crossbow bolt'
    # overhead 'NO ARROWS OR BOLTS!' warning_msg_color
    sysmsg 'NO ARROWS OR BOLTS!' warning_msg_color
endif

# if has camping, check for kindling
if skill 'camping' > 0 and not findtype 'kindling' backpack
    overhead 'NO KINDLING!' warning_msg_color
    sysmsg 'NO KINDLING!' warning_msg_color
endif

# if bard, check for magical (exceedingly or supremely) instruments
if skill 'musicianship' > 0
    if not listexists 'instruments'
        createlist 'instruments'
        pushlist 'instruments' 'lute'
        pushlist 'instruments' 'drum'
        pushlist 'instruments' 'bamboo flute'
        pushlist 'instruments' 'tambourine'
        pushlist 'instruments' 'lap harp'
    endif



    foreach type in 'instruments'
        # find all instruments and see if any of them are magical
        while findtype type backpack as instrument and not varexist char_instrument
            getlabel instrument instrument_label

            if exceedingly in instrument_label or supremely in instrument_label
                setvar! char_instrument instrument
                break
            endif
            @ignore instrument
        endwhile

        @clearignore

        if varexist char_instrument
            break
        endif
    endfor

    if not varexist char_instrument
        overhead 'CANT FIND INSTRUMENT!' warning_msg_color
        sysmsg 'CANT FIND INSTRUMENT!' warning_msg_color
    endif
endif

wait 2500
while targetexists 'neutral'
    wait 100
endwhile

cooldown 'Ship Launch'
sysmsg 'Finished'


#################################
# CHANGELOG
#################################
#
# v0.0.2
# - Adds arrows and bolts for archers (tkx Friendly Foe!)
#
# v0.0.1
# - Initial release
