# the idea here is to execute only 1 action per key press

interrupt

#script 'housing\rental-room-exit'


# if there is a ress gump up, we click in accept
if gumpexists 2957810225
    overhead 'RESSING...' 10
    gumpresponse 1
    stop
endif

# handle paralyze
if findbuff 'paralyze'
    if not findtype 'pouch' 'backpack' 38
        overhead '** NO POUCH **' 39
        stop
    endif
    overhead 'pouch' 54
    usetype 'pouch' 'backpack' 38 
    stop
endif

# if there is a moongate nearby, enter it
if findtype 'blue moongate' 'ground' any any 2
    script 'actions\enter-moongate'
    wait 10
    stop
endif

# open door if theres one close (1 tile)
if not listexists 'doors'
    createlist 'doors'
    pushlist 'doors' 'wooden door'
    pushlist 'doors' 'metal door'
endif

foreach door in 'doors'
    if findtype door ground any any 1 as doorx
        overhead 'clicking door...' 53
        useobject doorx
        stop
    endif
endfor


# keep campfire buff
if skill 'camping' > 20 and not findbuff 'campfire visit'
    if not findtype 'campfire' ground any any 2 and findtype 'kindling' backpack
        script 'skills\camping-create-campfire'
        stop
    endif
endif


# maintain food satisfaction buff
if not findbuff 'Food Satisfaction' and findtype 'tray' backpack
    script 'items\eat-delectable'
    stop
endif


# keep taste id buff
if skill 'taste identification' > 20 and not findbuff 'herbal poultice'
    script 'skills\taste-identification'
    stop
endif

# maintain str at 120 pots
if str < 120 and findtype 'white potion'
    script 'potions\pot-str-agil'
    stop
endif


# maintain dex up
if skill 'magery' > 49
    if dex < 25 and findtype 'blue potion'
      script 'potions\pot-str-agil'
      stop
    endif
# for dexxers
elseif skill 'magery' < 51
    if dex < 101 and findtype 'blue potion'
      script 'potions\pot-str-agil'
      stop
    endif
endif

# magic resist pot buff
if not findbuff 'magic resist potion' and findtype 'black potion'
    script 'potions\magic-resist'
    stop
endif


if skill 'spirit' > 20 and skill 'necromancy' == 0
    overhead 'turning SS loop' 55
    script 'skills\spirit-speak'
endif

# drop empty pots on ground
if findtype 3854 'backpack' as bottles and counttype 3854 > 3
    overhead '✩ droping bottles ✩' 55
    wait 600
    lift bottles 999
    #wait 600
    droprelloc 1 1
    stop
endif

# FROM DOWN HERE, ONLY NON IMPORTANT ACTIONS
# THAT WONT HOLD DOWN THE CHAR
# IN CASE OF DANGER


wait 10
if name = 'Tau'
   script 'loops\town-struggle'
endif

overhead 'nothing to do...' 55
