###################################################
# 1-KEY SUMMON MAINTAINANCE
# by gugutz
# v0.0.1
###################################################

#################################
# CHANGELOG
#################################
#
#
# v0.0.1
# - Initial release


#################################
# BOOTSTRAP
#################################

if not listexists 'config_lists'
    createlist 'config_lists'
else
    clearlist 'config_lists'
endif

pushlist 'config_lists' 'action_delay'
pushlist 'config_lists' 'auto_summon'
pushlist 'config_lists' 'summons'
pushlist 'config_lists' 'undead_summons'
pushlist 'config_lists' 'found_summon'
pushlist 'config_lists' 'found_undead_summon'
pushlist 'config_lists' 'cast_summon_spell'
pushlist 'config_lists' 'make_undead_summon'
pushlist 'config_lists' 'find_summon_range'
pushlist 'config_lists' 'summon_max_limit'
pushlist 'config_lists' 'summon_spell_required_mana'


#
# CREATE ALL LISTS FOR CONFIG
foreach list in 'config_lists'
    if not listexists list
        createlist list
    else
        clearlist list
    endif
endfor


###################################################
# CONFIG
###################################################

pushlist 'action_delay' 600
pushlist 'auto_summon' 'yes'
pushlist 'find_summon_range' 5

# pushlist 'summons' 'an earth elemental'
pushlist 'summons' 'a fire elemental'
pushlist 'summons' 'summ. creature'

# if summ creature is one of players summons, change limit to 5 so even with 4 followers, we still make the char cast to summon the fifth creature
# if not, then we just want 2 elementals so limit should be 4, since they take up 2 slots each and the fifth wouldnt be possible
#
if inlist 'summons' 'summ. creature'
    pushlist 'summon_max_limit' 5
    # sysmsg 'SUMMONS LIMIT SET TO 5' 56
else
    pushlist 'summon_max_limit' 4
    # sysmsg 'SUMMONS LIMIT SET TO 4' 56
endif

# INITIALIZE THE CONFIGURATION VALUES AS VARIABLES BUT FROM LIST SO WE DONT NEED TO ITERATE TO GET THE INDEX 0
if atlist 'action_delay' 0 as action_delay
if atlist 'find_summon_range' 0 as find_summon_range
if atlist 'summon_max_limit' 0 as summon_limit




###################################################
# ROUTINE RELATED LISTS
###################################################
if not listexists 'summonspells'
    @createlist 'summonspells'
    if not listexists 'summonspells'
        createlist 'summonspells'
    else
        clearlist 'summonspells'
    endif

    if inlist 'summons' 'an air elemental'
        @pushlist 'summonspells' 'air elemental'
    endif

    if inlist 'summons' 'an earth elemental'
        @pushlist 'summonspells' 'earth elemental'
    endif

    if inlist 'summons' 'a water elemental'
        @pushlist 'summonspells' 'water elemental'
    endif

    if inlist 'summons' 'a fire elemental'
        @pushlist 'summonspells' 'fire elemental'
    endif

    if inlist 'summons' 'a daemon'
        @pushlist 'summonspells' 'summon daemon'
    endif

    if inlist 'summons' 'an energy vortex'
        @pushlist 'summonspells' 'energy vortex'
    endif

    if inlist 'summons' 'a blade spirit'
        @pushlist 'summonspells' 'blade spirits'
    endif

    if inlist 'summons' 'summ. creature'
        @pushlist 'summonspells' 'summ. creature'
    endif
endif

if not timerexists 'vengeful_spirit_timer'
    createtimer 'vengeful_spirit_timer'
    settimer 'vengeful_spirit_timer' 30001
endif

if not timerexists 'low_mana_for_summoning_warning_msg_timer'
    createtimer 'low_mana_for_summoning_warning_msg_timer'
    settimer 'low_mana_for_summoning_warning_msg_timer' 30001
endif


if skill 'necromancy' > 0 and not listexists 'vampiric_embraced_corpses'
    sysmsg '>>> Creating list to control consumed (vamp embraced) corpses...' 74
    createlist 'vampiric_embraced_corpses'
endif

if skill 'spirit' > 0 and not listexists 'drained_corpses'
    sysmsg '>>> Creating list to control drained corpses...' 74
    createlist 'drained_corpses'
endif



###################################################
# CODE
###################################################



# this is just a print to inform char is under summon_limit and is getting ready to summon up
if inlist 'auto_summon' 'yes' and followers < summon_limit
    sysmsg 'Under Summon Limit. Preparing To Summon...' 67


    foreach summon in 'summons'

        sysmsg 'CHECKING {{summon}}: {{index}}' 18

        if list 'summons' == 1 and followers == 4
            sysmsg 'BREAKING ON CHECKING SUMMON {{summon}}' 99
            break
        elseif list 'summons' > 1 and followers == 5
            sysmsg 'BREAKING ON CHECKING SUMMON {{summon}}' 99
            break
        endif

        clearlist 'found_summon'
        clearlist 'cast_summon_spell'

        # determine summon name by whether char has necromancy or not
        if skill 'necromancy' >= 50
            # on each iteration, clear and populate this list to set the undead alternative summon based on current summon
            clearlist 'undead_summons'

            if 'earth' in summon
                pushlist 'undead_summons' 'an ancient mummy'
            elseif 'fire' in summon
                pushlist 'undead_summons' 'a lich'
            elseif 'air' in summon
                pushlist 'undead_summons' 'a skeletal fiend'
            elseif 'daemon' in summon
                pushlist 'undead_summons' 'a vampire thrall'
            elseif 'water' in summon
                pushlist 'undead_summons' 'a rag witch'
            endif
        endif

        # if char is necro, make sure vengeful spirit is activated before summoning
        if list 'undead_summons' > 0
            if timer 'vengeful_spirit_timer' >= 30000
                sysmsg '>>> VENGEFUL SPIRIT...' 95
                say '[vengefulspirit'

                wait action_delay

                sysmsg '>>> WITHER...' 95
                say '[wither'
                settimer 'vengeful_spirit_timer' 0
            else
                sysmsg 'Vengeful Spirit Cooldown..' 41
            endif
        endif

        #########################
        # try to find a similir creature summon to see if we need to cast this one or skip to next one


        if findtype summon ground any 1 find_summon_range as summonserial
            if noto summonserial = friend
                overhead '*found {{summon}}*' 91 summonserial
                pushlist 'found_summon' 'yes'
            endif
        # if the 'undead_summons' list has elements, we need to check for those undead summons as well
        elseif list 'undead_summons' > 0
            foreach necro_summon in 'undead_summons'
                if findtype necro_summon ground any 1 find_summon_range as summonserial
                    if noto summonserial = friend
                        overhead '*found {{summon}}*' 91 summonserial
                        pushlist 'found_summon' 'yes'
                        # break
                    endif
                endif
            endfor
        endif

        sysmsg 'DEBUG 1' 19
        # even if we found this summon type, we still might need to cast it (if only 1 summon in list for instance)
        if inlist 'found_summon' 'yes'
            # clearlist 'found_summon'

            # if theres only one summon in the 'summons' list, even if we found the summon above,
            # we still need to cast it again
            if list 'summons' == 1 and followers < 4
                pushlist 'cast_summon_spell' 'yes'
            # if one of our summons is summ creature and we only have 3 followers, we can still cast another more usefull
            # summon before actually casting summ.creature
            elseif list 'summons' == 2 and followers <= 3 and inlist 'summons' 'summ. creature'
                pushlist 'cast_summon_spell' 'yes'
            elseif list 'summons' >= 3 and followers <= 1
                pushlist 'cast_summon_spell' 'yes'
            # else if theres more than one summon in 'summons' list and we didnt find this specific summon
            # we need to cast ig
            endif
        else
            sysmsg 'DEBUG ElSE 333' 19
            pushlist 'cast_summon_spell' 'yes'
        endif

        #########################
        # prepare to cast the summon spell for this summon
        if inlist 'cast_summon_spell' 'yes'


            foreach summonspell in 'summonspells'

                if summonspell in summon

                    clearlist 'summon_spell_required_mana'

                    # first make a mana check for this specific summon spell
                    if 'creature' in summonspell or 'blade' in summonspell
                        pushlist 'summon_spell_required_mana' 14
                    else
                        pushlist 'summon_spell_required_mana' 50
                    endif

                    if atlist 'summon_spell_required_mana' 0 as summon_spell_mana
                        if mana < summon_spell_mana

                            if timer 'low_mana_for_summoning_warning_msg_timer' >= 2000
                                sysmsg ">>> LOW MANA FOR SUMMONING {{summon}}. (NEEDS {{summon_spell_mana}})..." 41
                                overhead "low mana for summoning" 41
                                settimer 'low_mana_for_summoning_warning_msg_timer' 0
                            endif
                        else
                            sysmsg ">>> SUMMONING {{summon}}..." 56
                            cast summonspell
                            if 'blade' in summonspell or 'vortex' in summonspell
                                overhead 'Waiting for {{summonspell}} target..'

                                @settimer 'waiting_summon_timer' 0
                                while not targetexists 'neutral' and timer 'waiting_summon_timer' < 3500
                                    wft 100
                                    if insysmsg 'disturbed'
                                        @settimer 'waiting_summon_timer' 0
                                        sysmsg 'BREAKING WHILE WAITING FOR RESS TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile

                                if targetexists 'neutral'
                                    sysmsg 'CASTING BLADE OR VORTEX IN GROUND' 55
                                    targetrelloc 0 1
                                    break
                                endif
                            else
                                @settimer 'waiting_summon_timer' 0
                                while timer 'waiting_summon_timer' < 3500
                                    wait 100
                                    if insysmsg 'disturbed'
                                        @settimer 'waiting_summon_timer' 0
                                        sysmsg 'BREAKING WHILE WAITING FOR RESS TARGET'
                                        wait 150
                                        break
                                    endif
                                endwhile
                                say 'all guard me'
                            endif

                            break

                        endif
                    endif


                endif

            endfor
        endif


    endfor
    stop
endif



###################################################
# CHECK IF WE CAN EXTEND / RESTORE SUMMONS
###################################################

foreach summon in 'summons'
    # determine summon name by whether char has necromancy or not
    if skill 'necromancy' >= 50
        # on each iteration, clear and populate this list to set the undead alternative summon based on current summon

        clearlist 'undead_summons'

        if earth in summon
            pushlist 'undead_summons' 'an ancient mummy'
        elseif fire in summon
            pushlist 'undead_summons' 'a lich'
        elseif air in summon
            pushlist 'undead_summons' 'a skeletal fiend'
        elseif daemon in summon
            pushlist 'undead_summons' 'a vampire thrall'
        elseif water in summon
            pushlist 'undead_summons' 'a rag witch'
        endif
    endif

    #########################
    # try to find a similir creature summon to see if we need to cast this one or skip to next one
    clearlist 'found_summon'
    clearlist 'found_undead_summon'
    clearlist 'cast_summon_spell'

    if findtype summon ground any 1 find_summon_range as summonserial
        if noto summonserial = friend
            overhead '*found summon*' 91 summonserial
            pushlist 'found_summon' 'yes'
        endif
    endif

    # if the 'undead_summons' list has elements, we need to check for those undead summons as well
    if list 'undead_summons' > 0
        foreach necro_summon in 'undead_summons'
            if findtype necro_summon ground any 1 find_summon_range as summonserial
                if noto summonserial = friend
                    overhead necro_summon 16 summonserial
                    pushlist 'found_undead_summon' 'yes'
                    break
                endif
            endif

        endfor
    endif

    if inlist 'found_summon' 'yes' or inlist 'found_undead_summon' 'yes'
        break
    endif
endfor


# drain energy to increse mage summons duration
if inlist 'found_summon' 'yes'
    if skill 'spirit' > 0
        if findtype 'corpse' ground any any 10 as body
            if not inlist 'drained_corpses' body
                sysmsg '>>> DRANING NEARBY CORPSES' 55
                skill 'spirit'
                @pushlist 'drained_corpses' body
                @ignore body
            endif
        else
            overhead '*no corpses to drain*' 56
            sysmsg 'No Corpses Nearby To Drain...' 56
        endif
    endif
else
    sysmsg '>>> No Summons To Extend*' 56
endif


# consume nearby corpses to increase undead summons hp
if inlist 'found_undead_summon' 'yes'
    if skill 'necromancy' > 0
        if findtype 'corpse' ground any any 10 as body
            say '[vampiricembrace'
            wft 400
            if targetexists 'neutral'
                sysmsg '>>> CONSUMING NEARBY CORPSE...' 95
                overhead '*consuming corpse*' 95
                target 'self'
            endif
        else
            overhead '*no corpses to consume*' 56
            sysmsg 'No Corpses Nearby To Consume...' 56
        endif
    endif
elseif not inlist 'found_undead_summon' 'yes'
    sysmsg '>>> No Undeads To Heal' 56
endif

if not inlist 'found_summon' 'yes' and not inlist 'found_undead_summon' 'yes'
    overhead '|* no followers to maintain *|' 56
    stop
endif



################################
# CLOSE CONFIG 'if atlist' STATEMENTS
################################

endif
endif
endif
