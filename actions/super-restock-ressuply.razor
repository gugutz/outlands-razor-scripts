#################################
# SUPER RESTOCK & RESSUPLY
# v1.6.0
# by gugutz
#
#################################
# CREDITS :)
#
sysmsg '♥ SUPER RESTOCK & RESSUPLY by gugutz ♥' 55
#
#

# unsetvar! gold_container_location
# unsetvar! tomes_container_location

#################################
# SCRIPT CONFIGURATION
#################################

if not timerexists 'execution_timer'
    createtimer 'execution_timer'
endif
settimer 'execution_timer' 0

if not varexist super_restock_ressuply_first_run_done
    setvar! super_restock_ressuply_first_run_done 1

    # print (or dont) informative sysmsgs and overhead msgs
    setvar! verbose_mode 1

    # choose if you also want to unload misc items (skill orbs,, rms, etc...)
    setvar! unload_misc_items 1

    # choose if links should be unloaded into the misc container (in case no links tome available)
    setvar! unload_links_in_misc_container 0

    # this will pop open your trapped loot pouch (from the hide-loot-in-trap-pouch script)
    setvar! open_loot_pouch_when_unloading 0

    setvar! unload_aspect_items_in_stockpile 0

    # 0: drop all items in recycler
    # 1: drop only unindentified items in recycler
    setvar! recycler_mode 1

    setvar! print_unloaded_items 1

    # set if shelf restock should target the char himself or a specific bag
    # set to 'self' to restock from char, or any other value to have the script
    # ask for the bag/container to use
    setvar! restock_shelf_using_self 1

    setvar! use_detect_hidden 0

    #################################
    # DELAYS AND COLORS

    setvar! action_delay 300
    setvar! sysmsgs_color 50
    setvar! debug_msgs_color 43


    #################################
    # CREATE ALL LISTS FOR CONFIG
    #################################

    createlist 'super_restock_ressuply_config_lists'
    pushlist 'super_restock_ressuply_config_lists' 'tomes_location'
    pushlist 'super_restock_ressuply_config_lists' 'gold_container_location'
    pushlist 'super_restock_ressuply_config_lists' 'unload_misc_items_container_location'
    pushlist 'super_restock_ressuply_config_lists' 'items_unloaded'

    foreach list in 'super_restock_ressuply_config_lists'
        if not listexists list
            createlist list
        endif
    endfor

    #################################
    # CONTAINERS CONFIGURATION
    #################################
    #
    # Possible Values:
    # - 'ground', -> if resource is located directly in the ground
    # - 'subcontainer' -> if resource should be placed in a container thats inside the master_container
    # - 'master_container' -> if resource should be placed inside the common/shared 'master_container'
    # - (TOMES ONLY!) 'ground_container' -> if resource is inside its own container in the ground
    #
    pushlist 'unload_misc_items_container_location' 'ground'
    pushlist 'gold_container_location' 'ground'
    pushlist 'tomes_location' 'ground'

endif

clearlist 'items_unloaded'


#################################
# GAME TYPES (GRAPHICS) CONSTANTS
#################################

setvar! ORE 6585
setvar! INGOT 6585

if not listexists 'instruments'
    createlist 'instruments'
    pushlist 'instruments' 'lute'
    pushlist 'instruments' 'drum'
    pushlist 'instruments' 'bamboo flute'
    pushlist 'instruments' 'tambourine'
    # pushlist 'instruments' 'lap harp'
    pushlist 'instruments' 3762
endif

if not listexists 'magical_properties'
    createlist 'magical_properties'
    pushlist 'magical_properties' 'eminently'
    pushlist 'magical_properties' 'exceedingly'
    pushlist 'magical_properties' 'supremely'
    pushlist 'magical_properties' 'invulnerability'
endif

if not listexists 'colored_materials_preffixes'
    createlist 'colored_materials_preffixes'
    pushlist 'colored_materials_preffixes' 'ava'
    pushlist 'colored_materials_preffixes' 'val'
    pushlist 'colored_materials_preffixes' 'ver'

endif

# LIST OF COLORS OF MATERIALS TO LOOT
if not listexists 'colored_materials'
    createlist 'colored_materials'
    pushlist 'colored_materials' 'avarhide'
    pushlist 'colored_materials' 'avarwood'
    pushlist 'colored_materials' 'avarite'
    pushlist 'colored_materials' 'valehide'
    pushlist 'colored_materials' 'valewood'
    pushlist 'colored_materials' 'valorite'
    pushlist 'colored_materials' 'verehide'
    pushlist 'colored_materials' 'verewood'
    pushlist 'colored_materials' 'verite'
    pushlist 'colored_materials' 'agapite'
    pushlist 'colored_materials' 'gold'
endif




if not listexists 'shields'
    createlist 'shields'
    pushlist 'shields' 'buckler'
    pushlist 'shields' 'wooden shield'
    pushlist 'shields' 'kite shield'
    pushlist 'shields' 'bronze shield'
    pushlist 'shields' 'metal shield'
endif



#################################
# CODE
#################################

# SEARCH FOR EVERY PACKANIMAL NEARBY WHOS NOTO IS SAME AS CHAR, TO RESTOCK FROM THEM
removelist 'packies'
createlist 'packies'

sysmsg 'Searching packies...' 49

while findtype 292 ground any any 2 as packie
    # if noto packie = noto 'self'
    if noto packie = friend
        pushlist 'packies' packie
        overhead '*opening bag*' 56 packie
        dclick packie
        wait action_delay
    endif
    @ignore packie
endwhile

while findtype 291 ground any any 2 as packie
    # if noto packie = noto 'self'
    if noto packie = friend
        pushlist 'packies' packie
        overhead '*opening bag*' 56 packie
        dclick packie
        wait action_delay
    endif
    @ignore packie
endwhile
@clearignore


# IF PLAYER CHOSE TO USE ANY SUBCONTAINER
# WE NEED TO ASK HIM FOR THE MAIN CONTAINER THAT
# WILL HOLD THOSE SUBCONTAINERS, IF WE DIDNT ALREADY
# OR CANT FIND THAT CONTAINER CLOSE

if inlist 'unload_misc_items_container_location' 'subcontainer' or inlist 'gold_container_location' 'subcontainer' or inlist 'tomes_location' 'subcontainer' or inlist 'tomes_location' 'master_container' or inlist 'gold_container_location' 'master_container'
   if not varexist main_unload_container_serial or not find main_unload_container_serial ground any any 2
        overhead 'select master unload container' sysmsgs_color
        sysmsg  '> Select Your Master Unload Container' sysmsgs_color
        setvar! main_unload_container_serial
    endif
endif


# if player did select a main unload container,
# we open right now to find the other subcontainers player might use
if varexist main_unload_container_serial
    useobject main_unload_container_serial
    wait action_delay
endif



#################################
# SET SHELF RESTOCK CONTAINER
#################################

if restock_shelf_using_self == 1
    setvar! shelf_restock_container_serial self
else
    # if player set the shelf restock to something else than 'self' and char backpack, we need to make sure
    # we have it saved, or else we need to ask player for the container
    if not varexist shelf_restock_container_serial
        @unsetvar shelf_restock_container_serial
        overhead 'select your shelf restock container' sysmsgs_color
        sysmsg  '> Select Your Shelf restock Container' sysmsgs_color
        setvar! shelf_restock_container_serial
    elseif not find shelf_restock_container_serial backpack
        @unsetvar shelf_restock_container_serial
        overhead 'cant find shelf restock container. select it again...' sysmsgs_color
        sysmsg  '> Shelf Restock Container Not Found In Backpack! Select It Again:' sysmsgs_color
        setvar! shelf_restock_container_serial
    endif
endif


#################################
# SET MISC ITEMS UNLOAD CONTAINER
#################################

if inlist 'unload_misc_items_container_location' 'ground' or inlist 'unload_misc_items_container_location' 'master_container'
    setvar! misc_items_container_location 'ground'
elseif inlist 'unload_misc_items_container_location' 'subcontainer'
    setvar! misc_items_container_location main_unload_container_serial
else
    setvar! misc_items_container_location 'ground'
endif

if inlist 'unload_misc_items_container_location' 'ground' or inlist 'unload_misc_items_container_location' 'subcontainer' or inlist 'unload_misc_items_container_location' 'master_container'
    if not varexist misc_items_container
        @unsetvar misc_items_container
        overhead 'select your misc items container' sysmsgs_color
        sysmsg  '> Select Your Misc Items Container' sysmsgs_color
        setvar! misc_items_container
    elseif not find misc_items_container misc_items_container_location any any 2
        @unsetvar misc_items_container
        overhead 'cant find misc items container. select it again...' sysmsgs_color
        sysmsg  '> Misc Items Container Not Found Within 2 Tiles! Select It Again:' sysmsgs_color
        setvar! misc_items_container
    endif
endif




#################################
# SET GOLD DEPOSIT CONTAINER IF NOT USING THE BANK DEPOSIT BOX
#################################

# first set the location of where the container should be
if inlist 'gold_container_location' 'ground' or inlist 'gold_container_location' 'master_container'
    setvar! gold_container_location_serial 'ground'
elseif inlist 'gold_container_location' 'subcontainer'
    setvar! gold_container_location_serial main_unload_container_serial
else
    setvar! gold_container_location_serial 'ground'
endif


# now we search for the gold deposit container where its location was settled in the above block
if inlist 'gold_container_location' 'ground' or inlist 'gold_container_location' 'subcontainer' or inlist 'gold_container_location' 'master_container'
    if not varexist gold_container
        @unsetvar gold_container
        overhead 'select your gold deposit container' sysmsgs_color
        sysmsg  '> Select Your Gold Deposit container' sysmsgs_color
        setvar! gold_container
    elseif not find gold_container gold_container_location_serial any any 2
        @unsetvar gold_container
        overhead 'cant find gold unload container. select it again...' sysmsgs_color
        sysmsg  '> Gold Unload Container Not Found Within 2 Tiles! Select It Again:' sysmsgs_color
        setvar! gold_container
    endif
endif


#################################
# SET TOMES CONTAINER LOCATION VARIABLE
#################################

if inlist 'tomes_location' 'ground'
    setvar! tomes_location_serial 'ground'

elseif inlist 'tomes_location' 'master_container'
        # setvar! tomes_container main_unload_container_serial
    setvar! tomes_location_serial main_unload_container_serial

    if varexist main_unload_container_serial and find main_unload_container_serial ground any any 2
        sysmsg 'Opening Main Unload Container For Tomes...' 90
        overhead 'Opening Main Unload Container For Tomes...' 90
        useobject main_unload_container_serial
        wait action_delay
    else
        overhead '[DEBUG] Couldnt find master container' 90
    endif

# if player chose subcontainer though, we still dont know were is the tomes_container
# so we need to eventually ask the player
elseif inlist 'tomes_location' 'ground_container'
    if not varexist tomes_location_serial
        # sysmsg '[DEBUG] tomes_location_serial variable doenst exist' debug_msgs_color
        setvar! reset_tomes_container true
    elseif not find tomes_location_serial ground any any 2
        # sysmsg '[DEBUG] couldnt find tomes_location_serial in the ground' debug_msgs_color
        setvar! reset_tomes_container true
    endif
elseif inlist 'tomes_location' 'subcontainer'

    if not varexist tomes_location_serial
        # sysmsg '[DEBUG] tomes_location_serial variable doenst exist' debug_msgs_color
        setvar! reset_tomes_container true
    elseif not find tomes_location_serial true
        # sysmsg '[DEBUG] couldnt find tomes_location_serial in the ground' debug_msgs_color
        setvar! reset_tomes_container true
    endif

    # always open the tomes container if its a subcontainer
    if varexist tomes_location_serial
        sysmsg 'Opening Tomes Container...'
        useobject tomes_location_serial
        wait action_delay
    endif


    # if atlist 'main_unload_container' 0 as main_unload_container_serial
    #     setvar! tomes_location main_unload_container_serial
    # endif
endif

if varexist reset_tomes_container
    unsetvar! reset_tomes_container
    sysmsg 'RESETING TOMES CONTAINER!' 43
    overhead 'select your tomes container' sysmsgs_color
    sysmsg  '> Select Your Tomes Container' sysmsgs_color
    setvar! tomes_location_serial
endif

# SET THE TOMES CONTAINER
# if inlist 'tomes_location' 'subcontainer' or inlist 'tomes_location' 'master_container'

#     # if tomes are in the master_container, open the container
#     if find main_unload_container_serial ground any any 2
#         sysmsg 'Opening Main Unload Container For Tomes...'
#         useobject main_unload_container_serial
#         wait action_delay
#     endif
#     # if inlist 'tomes_location' 'subcontainer'
#     # if not varexist tomes_container or not find tomes_container tomes_location any any 2
#     if not varexist tomes_location
#         overhead 'select your tomes container' sysmsgs_color
#         sysmsg  '> Select Your Tomes Container' sysmsgs_color
#         setvar! tomes_location
#     endif
# endif



#################################
# USE DETECT HIDDEN EVERY 50s
#################################

if use_detect_hidden == 1
    if not timerexists 'detect_hidden_timer'
        settimer 'detect_hidden_timer' 50001
    endif

    clearsysmsg

    if timer 'detect_hidden_timer' >= 50000
        sysmsg ">>> Detecting Hidden Thiefs..." sysmsgs_color
        while not insysmsg 'You search the home and find no one hiding within' and not insysmsg 'You reveal what was hidden'
            useskill 'detectinghidden'
            wft action_delay
            target 'self'
            wait action_delay
        endwhile
        settimer 'detect_hidden_timer' 0
    endif
endif

#################################
# IF SCRIPT IS SET TO RECYCLER ALL ITEMS (AND NOT ONLY UNINDENTIFIED)
# THEN SAVE EVERYTHING THAT WE NEED TO GET BACK AFTER RECYCILING
#################################

if recycler_mode == 0

    #################################
    # SAVE PICKAXES AND HATCHES FOR RESOURCES
    # TO GET THEM BACK AFTER RECYCILING
    #################################

    if skill 'mining' > 0
        if not listexists 'char_pickaxes'
            createlist 'char_pickaxes'
        endif

        while findtype 'pickaxe' backpack as pickaxe_serial
            getlabel pickaxe_serial pickage_label
            if 'unidentified' in pickaxe_label
            else
                pushlist 'char_pickaxes' pickaxe_serial
                @ignore pickaxe_serial
            endif
        endwhile
        @clearignore

        if list 'char_pickaxes' > 0
            sysmsg 'Saved char pickaxes'
        endif
    endif

    if skill 'lumberjacking' > 0
        if not listexists 'char_hatchets'
            createlist 'char_hatchets'
        endif

        while findtype 'hatchet' backpack as hatchet_serial
            getlabel hatchet_serial hatchet_label
            if 'unidentified' in hatchet_label
            else
                pushlist 'char_pickaxes' hatchet_serial
                @ignore hatchet_serial
            endif
        endwhile
        @clearignore

        if list 'char_hatchets' > 0
            sysmsg 'Saved char hatchets'
        endif
    endif

    #################################
    # SAVE BARDING INSTRUMENT TO GET IT FROM RECYCLER AFTER UNLOADING
    #################################

    if skill 'musicianship' > 50
        # if not listexists 'char_instrument'
        #     createlist 'char_instrument'
        # endif


        # check if we still have a saved and locatable instrument
        unsetvar! reset_char_instrument
        if varexist char_instrument
            if not find char_instrument backpack
                unsetvar! char_instrument
                # pushlist 'reset_char_instrument' 'yes'
            endif
        else
            unsetvar! char_instrument
            setvar! reset_char_instrument true
        endif

        if varexist reset_char_instrument
            foreach instrument_type in 'instruments'

                if findtype instrument_type backpack as instrument
                    getlabel instrument instrument_label

                    # unsetvar! found_char_instrument

                    foreach magical_instrument_type in 'magical_properties'
                        if magical_instrument_type in instrument_label
                            sysmsg '> FOUND INSTRUMENT: {{instrument_label}}' 90
                            overhead '*found and saved instrument*' 90

                            setvar! char_instrument instrument
                            break
                        endif
                    endfor
                endif

                if varexist char_instrument
                    break
                endif
            endfor
        endif
    endif


    #################################
    # SAVE SHIELD AND GET IT BACK FROM RECYCLER
    #################################

    if skill 'parrying' > 0
        # if not listexists 'char_instrument'
        #     createlist 'char_instrument'
        # endif

        # check if we still have a saved and locatable shield
        unsetvar! reset_char_shield
        if varexist char_shield
            if not find char_shield backpack
                unsetvar! char_shield
                # pushlist 'reset_char_instrument' 'yes'
            endif
        else
            unsetvar! char_shield
            setvar! reset_char_shield true
        endif

        if varexist reset_char_shield
            foreach shield_type in 'shields'

                if findtype shield_type backpack as shield
                    getlabel shield shield_label

                    # unsetvar! found_char_instrument


                    # if 'exceedingly' in item_label or 'supremely' in item_label or 'invulnerability' in item_label
                    #     pushlist 'loot_this_item' 'yes'
                    # elseif 'avar' in item_label or 'vale' in item_label or 'ver' in item_label or 'rosewood' in item_label or 'gold' in item_label
                    #     pushlist 'loot_this_item' 'yes'
                    # endif


                    # if item is worth taking, loot it
                    foreach magical_property in 'magical_properties'
                        if magical_property in shield_label
                            sysmsg '> FOUND SHIELD: {{shield_label}}' 90
                            overhead '*found and saved shield*' 90

                            setvar! char_shield shield
                            break
                        endif
                    endfor

                    # if item is worth taking, loot it
                    if not varexist char_shield
                        foreach material_color in 'colored_materials'
                            if material_color in shield_label
                                sysmsg '> FOUND SHIELD: {{shield_label}}' 90
                                overhead '*found and saved shield*' 90

                                setvar! char_shield shield
                                break
                            endif
                        endfor
                    endif

                endif

                if varexist char_shield
                    break
                endif
            endfor
        endif
    endif

# endif recycler_mode == 0
endif

#################################
# POP TRAPPED LOOT POUCH OPEN IF OPTION IS SET TO 'yes'
#################################

if open_loot_pouch_when_unloading  == 1
    if listexists 'char_loot_pouch'
        if atlist 'char_loot_pouch' 0 as loot_pouch
            sysmsg 'OPENING TRAPPED LOOT POUCH' sysmsgs_color
            dclick loot_pouch
            wait action_delay

            dclick loot_pouch
            wait action_delay
        endif
    endif
endif


#################################
# MISC ITEMS (ITEMS WITHOUT SHELF/TOME ORGANIZER FOR THEM
#################################

#
if unload_misc_items  == 1
    sysmsg 'Searching Misc Items...' sysmsgs_color

    if not listexists 'misc_items'
        createlist 'misc_items'
        # skill orb
        pushlist 'misc_items' 22336
        pushlist 'misc_items' 'research materials'
        # spell hue deeds
        pushlist 'misc_items' 5359
        # pushlist 'misc_items' 'bag of seeds'
        # pushlist 'misc_items' 'seed of renewal'

        # collectable cards
        pushlist 'misc_items' 'parchment'
        pushlist 'misc_items' 'carpet'
        if unload_links_in_misc_container == 1
            pushlist 'misc_items' 'chain link'
        endif
        # outlands event cards, like kings faire tickets or st patricks game cards
        pushlist 'misc_items' 'card'
        pushlist 'misc_items' 3712
        pushlist 'misc_items' 3648
        # greater paragon chests
        pushlist 'misc_items' 29833
        pushlist 'misc_items' 42517
        pushlist 'misc_items' 29040
        pushlist 'misc_items' 52615
        pushlist 'misc_items' 17083

        # ASPECT OILS, FOCIS AND BUNDLES - will only get dump if they dont go to the aspect tome item before this point
        # weaopn oils
        pushlist 'misc_items' 45285
        # aspect focis
        pushlist 'misc_items' 48261
        # arrow bundles
        pushlist 'misc_items' 39856

        # events vbattle comendeation from pvp eventgs
        pushlist 'misc_items' 15178
    endif

    if inlist 'unload_misc_items_container_location' 'ground' or inlist 'unload_misc_items_container_location' 'subcontainer'
        if not varexist misc_items_container
            overhead 'misc items container is not set!' 41
            sysmsg '> Misc Items Container Is Not Set!' 41
        elseif not find misc_items_container misc_items_container_location any any 2
            overhead 'cant find misc items container nearby' 41
            sysmsg '> Cant Find Misc Items Container Nearby!' 41
        else
            foreach item in 'misc_items'
                if findtype item backpack any any as item_to_unload

                    while findtype item backpack any any as item_to_unload
                        getlabel item_to_unload item_label

                        unsetvar! skip_this_item
                        # if its weapon oils or aspect focis, only drop in the misc container if its not fully charged and there is not aspect item tome nearby
                        if item == 45285 or item == 48261 or item == 39856
                            if findtype 'book' tomes_location_serial 2618 1 2
                                if '500 charges' in item_label
                                    # continue
                                    @ignore item_to_unload
                                    setvar! skip_this_item 1
                                endif
                            endif
                        endif

                        if not varexist skip_this_item
                            getlabel item_to_unload item_label

                            if not inlist 'items_unloaded' item_label
                                sysmsg 'Adding {{item_label}}'
                                pushlist 'items_unloaded' item_label
                            endif

                            overhead '*unloading {{item_label}}*' 49
                            lift item_to_unload 999
                            wait action_delay
                            drop misc_items_container -1 -1 0
                            wait action_delay
                        endif
                    endwhile


                endif
            endfor
        endif
    endif
endif

@clearignore


#################################
# UNLOAD GOLD
#################################

if not listexists 'currency_types'
    createlist 'currency_types'
    pushlist 'currency_types' 'gold coin'
    # doublons
    pushlist 'currency_types' 'muffins'
endif

sysmsg 'Searching for gold or doubloon...' sysmsgs_color

foreach currency_type in 'currency_types'
    while findtype currency_type backpack as currency
        getlabel currency currency_label

        if not varexist gold_container or not find gold_container gold_container_location_serial any any 2
            if findtype 'bank deposit safe' gold_container_location_serial any any 2 as deposit_box
                sysmsg '> Setting vault as gold deposit container' sysmsgs_color
                setvar! gold_container deposit_box

                while findtype 3821 deposit_box any as gold_found
                    getlabel gold_found gold_found_label
                    sysmsg 'FOUND {{gold_found_label}} INSIDE THE VAULT! GONNA TAKE IT OFF AND DEPOSIT IT PROPERLY!' 52
                    lift gold_found 60000
                    wait action_delay
                    drop ground
                    wait action_delay
                endwhile
            endif
        endif

        # finally, if either player did set a container to store gold, or we found a vault on ground, we use that to drop gold on it
        if not find gold_container gold_container_location_serial any any 2
            overhead 'cant find gold deposit container to unload char' 41
            sysmsg '> Cant Find gold deposit Container To Unload Char' sysmsgs_color
        elseif varexist gold_container and find gold_container gold_container_location_serial any any 2
            if verbose_mode  == 1
                overhead '*depositing {{currency_label}}*' 49
                sysmsg 'Deposited {{currency_label}} in vault!' 49
            endif

            lift currency 60000
            wait action_delay
            # gold here has to be droped at -1 -1 0, or else is ends up 'inside' the vault, and not in the bank
            drop gold_container -1 -1 0
            wait action_delay

            if verbose_mode  == 1
                if insysmsg 'You deposit'
                    overhead '*deposited {{currency_label}}*' 70
                    sysmsg 'Deposited {{currency_label}} in vault!' 49
                endif
            endif
        endif
    endwhile
endfor



#################################
# DEPOSIT GOLD FROM PACKIES
#################################

if list 'packies' > 0
    if not varexist gold_container or not find gold_container gold_container_location_serial any any 2
        overhead 'cant find gold deposit container to unload gold from packies' 38
        sysmsg '> Cant Find gold deposit Container To Unload Gold From Packies' sysmsgs_color
    elseif varexist gold_container and find gold_container gold_container_location_serial any any 2
        foreach packanimal in 'packies'
            while findtype 'gold coin' packanimal any any 2 as currency_from_packie
                getlabel currency_from_packie currency_from_packie_label
                if verbose_mode  == 1
                    overhead '*unloading {{currency_from_packie_label}}*' 49 packanimal
                    sysmsg '> Unloading {{currency_from_packie_label}} From Pack Animal' 49
                endif

                # we have to use 'lifttype' here cause only it supports specifying the item src, 'lift' doenst
                lift currency_from_packie 60000
                wait action_delay
                # gold here has to be droped at -1 -1 0, or else is ends up 'inside' the vault, and not in the bank
                drop gold_container -1 -1 0
                wait action_delay
            endwhile
        endfor
    endif
endif


#################################
# MINERS - SMELT ORES/INGOTS AND CUT LOGS
#################################

sysmsg 'Searching for ores to smelt...' sysmsgs_color

while findtype ORE backpack as ores_from_backpack
    if skill 'mining' <= 50 and hue ores_from_backpack != 0
        overhead 'char cant smelt colored ore' 52
        break
    endif

    if findtype 'forge' ground any any 2 or findtype 'large forge' ground any any 2
        if verbose_mode  == 1
            overhead '*smelting ores*' 49 packanimal
            sysmsg '> Smelting Ores In Backpack' 49
        endif
        dclick ores_from_backpack
        wait action_delay
    else
        sysmsg 'Found Ores in backpack but no forge nearby to smelt it...' 40
        break
    endif
endwhile

#################################
# LUMBER - UNLOAD LOGS/BOARDS
#################################

sysmsg 'Searching for logs to cut...' sysmsgs_color

# UNLOADING BOARDS AT HOME
# make boards from logs on backpack

while findtype 'log%s' backpack any any 2 as logs_from_backpack

    if verbose_mode  == 1
        overhead '*making boards from logs*' 49 packanimal
        sysmsg '> Making boards from logs in backpack' 49
    endif

    if findtype 'hatchet' backpack or findtype 'hatchet' lefthand
        dclick logs_from_backpack
        wait action_delay
    elseif findtype 'scissors' backpack as cutter
        dclick cutter
        wft action_delay
        if targetexists 'neutral'
            target logs_from_backpack
        endif
    else
        sysmsg 'NO SCISSORS TO MAKE BOARDS!' 44
    endif
endwhile


#################################
# RESOURCES IN PACK ANIMALS (MINER/LUMBER)
#################################

sysmsg 'Searching for ores or logs in packies...' sysmsgs_color

# smelt ores or make boards from resources inside packies to deposit them in resource stockpile
if list 'packies' > 0
    foreach packanimal in 'packies'

        # smelt ores
        while findtype ORE packanimal any any 2 as ores_from_packie
            if findtype 'forge' ground any any 2 or findtype 'large forge' ground any any 2
                if verbose_mode  == 1
                    overhead '*smelting ores*' 49 packanimal
                    sysmsg '> Smelting Ores In Pack Animal' 49
                endif

                dclick ores_from_packie
                wait action_delay
            else
                sysmsg 'Found Ores in pack animal but no forge nearby to smelt it...' 40
                overhead 'found ores but no forge' 40 packanimal
                break
            endif
        endwhile

        # make boards from logs
        while findtype 'log%s' packanimal any any 2 as logs_from_packie
            if verbose_mode  == 1
                overhead '*making boards from logs*' 49 packanimal
                sysmsg '> Making boards from logs in pack animal' 49
            endif

            if findtype 'hatchet' backpack
                dclick logs_from_packie
                wait action_delay
            elseif findtype 'scissors' backpack as cutter
                dclick cutter
                wft action_delay
                if targetexists 'neutral'
                    target logs_from_packie
                endif
            else
                sysmsg 'NO SCISSORS TO MAKE BOARDS!' 44
            endif
        endwhile
    endfor
endif


#################################
# REPAIR BENCH
#################################

if findtype 'repair bench' ground any any 2 as bench
    if verbose_mode  == 1
        sysmsg "> Repairing Items..." sysmsgs_color
    endif

    sysmsg 'Reparing items...' sysmsgs_color
    overhead '*reparing items*' 65
    dclick bench

    if not timerexists 'waiting_repair_timer'
        createtimer 'waiting_repair_timer'
    endif
    settimer 'waiting_repair_timer' 0
    clearsysmsg
    # from here we continue doing other unloads while the repair bench is repairing char items
    # we check in a sysmsg later if repair was successful
endif

#################################
# CHAIN LINKS
#################################

if unload_links_in_misc_container == 0
    if verbose_mode  == 1
        sysmsg '> Searching for Chain Links...' sysmsgs_color
    endif

    if findtype 'book' tomes_location_serial 2091 1 2 as links_map_tome
        if print_items_unloaded == 1
            while findtype 'chain link' backpack any any as item
                getlabel item item_label

                if not inlist 'items_unloaded' item_label
                    sysmsg 'Adding {{item_label}}'
                    pushlist 'items_unloaded' item_label
                endif

                @ignore item
            endwhile
            @clearignore
        endif

        if findtype 'chain link' backpack
            if verbose_mode  == 1
                sysmsg '> Unloading chain links...' sysmsgs_color
            endif
            menu links_map_tome 0
        endif
    endif
endif

#################################
# TREASURE MAPS
#################################

if findtype 'book' tomes_location_serial 2990 1 2 as treasure_map_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Treasure Maps...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'map' backpack 0 any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'map' backpack 0 any as tmap
        getlabel tmap tmap_label

        if "completed by" in tmap_label
            lift tmap 1
            wait action_delay
            overhead "dropping completed map"
            if findtype 'barrel' ground 0 1 2 as trashcan
                if verbose_mode  == 1
                    sysmsg "Dropping Completed Map On Trash Barrel" sysmsgs_color
                endif
                drop trashcan -1 -1 0
            else
                if verbose_mode  == 1
                    sysmsg "Dropping Completed Map On The Ground" sysmsgs_color
                endif
                droprelloc 0 0
            endif
        else
            sysmsg '> Unloading treasure maps...' sysmsgs_color
            menu treasure_map_tome 0
        endif



    endif
endif


#################################
# MINING MAPS
#################################

if findtype 'book' tomes_location_serial 2796 1 2 as mining_map_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Mining Maps...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'map' backpack 2796 any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'map' backpack 2796 any as mining_map
        if verbose_mode  == 1
            sysmsg '> Unloading mining maps...' sysmsgs_color
        endif
        menu mining_map_tome 0
    endif
endif


#################################
# LUMBER MAPS
#################################

if findtype 'book' tomes_location_serial 2799 1 2 as lumber_map_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Lumber Maps...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'map' backpack 2799 any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif


    if findtype 'map' backpack 2799 any as lumber_map
        if verbose_mode  == 1
            sysmsg '> Unloading lumber maps...' sysmsgs_color
        endif

        menu lumber_map_tome 0
    endif
endif


#################################
# SKINNING MAPS
#################################

if findtype 'book' tomes_location_serial 2651 1 2 as skinning_map_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Skinning Maps...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'map' backpack 2651 any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'map' backpack 2651 any as skinning_map
        if verbose_mode  == 1
            sysmsg '> Unloading skinning maps...' sysmsgs_color
        endif

        menu skinning_map_tome 0
    endif
endif


#################################
# FISHING MAPS
#################################

if findtype 'book' tomes_location_serial 2722 1 2 as fishing_map_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Fishing Maps...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'map' backpack 2904 any as item
            getlabel item item_label
            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif
            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'map' backpack 2904 any as fishing_map
        if verbose_mode  == 1
            sysmsg '> Unloading fishing maps...' sysmsgs_color
        endif

        menu fishing_map_tome 0
    endif
endif



#################################
# SKILL SCROLLS
#################################

if findtype 'book' tomes_location_serial 2963 1 2 as skill_scroll_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Skill Scrolls...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'scroll of calling' backpack any any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'scroll of calling' backpack any any as skill_scroll
        if verbose_mode  == 1
            sysmsg '> Unloading skill scrolls...' sysmsgs_color
        endif

        menu skill_scroll_tome 0
    endif
endif


#################################
# ASPECT ITEMS
#################################

if unload_aspect_items_in_stockpile == 0
    if verbose_mode  == 1
        sysmsg '> Searching for Aspect Items...' sysmsgs_color
    endif

    if findtype 'book' tomes_location_serial 2618 1 2 as aspect_item_tome

        if print_items_unloaded == 1
            createlist 'aspect_items_types'
            pushlist 'aspect_items_types' 'aspect core'
            pushlist 'aspect_items_types' 3836
            pushlist 'aspect_items_types' 17686
            pushlist 'aspect_items_types' phylactery

            foreach item_type in 'aspect_items_types'
                while findtype item_type backpack as item
                    getlabel item item_label

                    if not inlist 'items_unloaded' item_label
                        sysmsg 'Adding {{item_label}}'
                        pushlist 'items_unloaded' item_label
                    endif

                    @ignore item
                endwhile
                @clearignore
            endfor
        endif

        if findtype 'aspect core' backpack or findtype 3836 backpack or findtype 17686 backpack or findtype phylactery backpack or findtype 45285 backpack or findtype 48261 backpack or findtype 39856 backpack
            if verbose_mode  == 1
                sysmsg '> Unloading aspect items...' sysmsgs_color
            endif

            menu aspect_item_tome 0
        endif
    endif
endif


#################################
# RARE CLOTHES
#################################

if findtype 'book' tomes_location_serial 1495 1 2 as rare_cloth_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Rare Cloths...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype 'folded cloth' backpack any any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype 'folded cloth' backpack any any as piece_of_cloth
        if verbose_mode  == 1
            sysmsg '> Unloading Rare Cloths' sysmsgs_color
        endif

        menu rare_cloth_tome 0
    endif
endif


#################################
# DYES CLOTHES
#################################



if findtype 'book' tomes_location_serial 1494 1 2 as dyes_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Dyes...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        if not listexists 'dyes'
            createlist 'dyes'
            # runebook dye
            pushlist 'dyes' 3838
            # backpack dye
            pushlist 'dyes' 3839
            # headwear dye
            pushlist 'dyes' 3842
            # facial hair dye
            pushlist 'dyes' 3843
            # hair, shield and quiver dye
            pushlist 'dyes' 3843
            # fortuniture dye
            pushlist 'dyes' 29025
            # carpet dye
            pushlist 'dyes' 29036
        endif

        # foreach dye in 'dyes'
        while findtype '3838|3839|3842|3843|29025|29036' backpack any any as item
        # while findtype dye backpack any any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore

        # endfor
    endif

    if findtype '3838|3839|3842|3843|29025|29036' backpack any any as dye_bottle
        if verbose_mode  == 1
            sysmsg '> Unloading Dyes' sysmsgs_color
        endif

        menu dyes_tome 0
    endif
endif


#################################
# INCRIPTION RUNES
#################################


if findtype 'book' tomes_location_serial 2085 1 2 as runes_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Arcane Runes...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while findtype rune backpack any any as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        @clearignore
    endif

    if findtype rune backpack any any as magic_rune
        if verbose_mode  == 1
            sysmsg '> Unloading Magic Runes' sysmsgs_color
        endif

        menu runes_tome 0
    endif
endif

#################################
# MAGERY SCROLLS
#################################


if findtype 'book' tomes_location_serial 2227 1 2 as magery_scroll_tome
    if verbose_mode  == 1
        sysmsg '> Searching for Magery Scrolls...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        if not listexists 'magery_scrolls'
            createlist 'magery_scrolls'
            pushlist 'magery_scrolls' 'Curse Scroll'
            pushlist 'magery_scrolls' 'Recall'
            pushlist 'magery_scrolls' 'ManaDrain Scroll'
            pushlist 'magery_scrolls' 'Paralyze Scroll'
            pushlist 'magery_scrolls' 'Archcure Scroll'
            pushlist 'magery_scrolls' 'Fire Field Scrol'
            pushlist 'magery_scrolls' 'Magic Trap Scroll'
            pushlist 'magery_scrolls' 'Magic Arrow Scroll'
            pushlist 'magery_scrolls' 'Arch Protection Sc'
            pushlist 'magery_scrolls' 'Greater Heal Scrol'
            pushlist 'magery_scrolls' 'Fire Field Scroll'
            pushlist 'magery_scrolls' 'Lightning Scroll'
            pushlist 'magery_scrolls' 'Energy Bolt Scroll'
        endif

        # foreach scroll in 'magery_scrolls'
        # while findtype scroll backpack any any as item
        while findtype 'Curse Scroll|ManaDrain Scroll|Paralyze Scroll|Archcure Scroll|Fire Field Scroll|Magic Trap Scroll|Magic Arrow Scroll|Arch Protection Sc|Greater Heal Scrol|Fire Field Scroll|Lightning Scroll|Energy Bolt Scroll' backpack any any as item
            getlabel item item_label
            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif
            @ignore item
        endwhile
        @clearignore
        # endfor
    endif

    # foreach scroll in 'magery_scrolls'
    if findtype 'Curse Scroll|ManaDrain Scroll|Paralyze Scroll|Archcure Scroll|Fire Field Scroll|Magic Trap Scroll|Magic Arrow Scroll|Arch Protection Sc|Greater Heal Scrol|Fire Field Scroll|Lightning Scroll|Energy Bolt Scroll' backpack any any as item
    # if findtype scroll backpack any any as magery_scroll
        if verbose_mode  == 1
            sysmsg '> Unloading magery scrolls' sysmsgs_color
        endif

        menu magery_scroll_tome 0
        break
    endif
    # endfor
endif



#################################
# CHECK REPAIR BENCH SYSMSG
#################################

if findtype 'repair bench' ground any any 2 as bench
    if verbose_mode  == 1
        sysmsg '> Searching for Repair Bench status...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        while not insysmsg 'in need of repairs' and not insysmsg 'item' and not insysmsg 'items'
            if timer 'waiting_repair_timer' >= 3500
                if verbose_mode  == 1
                    sysmsg '[DEBUG] BREAKING WAITING FOR BENCH' sysmsgs_color
                endif
                break
            endif
            wait 100
        endwhile
    endif

    if insysmsg 'in need of repairs' or insysmsg 'item' or insysmsg 'items'
        if verbose_mode  == 1
            sysmsg '>> No items to be repaired.' sysmsgs_color
        endif
    elseif insysmsg 'in need of repairs' or insysmsg 'item' or insysmsg 'items'
        if verbose_mode  == 1
            sysmsg '>> Repaired All Items!' sysmsgs_color
        endif
    endif
endif


#################################
# STORAGE SHELF
#################################

# first search for a shelf, save it and use it to restock
if findtype 'storage shelf' ground any any 2 as shelf
    # setvar! storage_shelf storageshelf
    #overhead 'using shelf' 68 'shelf'
    if verbose_mode  == 1
        sysmsg '> Restocking Storage Shelf...' sysmsgs_color
    endif

    menu shelf 0
    wft action_delay
    if targetexists 'neutral'
        if varexist shelf_restock_container_serial
            if find shelf_restock_container_serial backpack
                target shelf_restock_container_serial
            else
                @unsetvar shelf_restock_container_serial
                target self
            endif
        else
            target self
        endif
    endif

    waitforgump 3232825965 600
    if gumpexists 3232825965
        gumpclose 3232825965
    endif

    # restock shelf from packies
    if list 'packies' > 0
        foreach packanimal in 'packies'
            if verbose_mode  == 1
                sysmsg '> Restocking Shelf From Pack Animal...' sysmsgs_color
            endif

            menu shelf 0
            wft action_delay
            if targetexists 'neutral'
                if verbose_mode  == 1
                    overhead "unloading on shelf" 56 packanimal
                endif

                target packanimal
            endif
        endfor
    endif

    if gumpexists 3232825965
        gumpclose 3232825965
    endif
else
    if varexist shelf
        @unsetvar shelf
    endif
    overhead 'no shelfs nearby'
    
endif



#################################
# RESOURCE STOCKPILE
#################################

if findtype 'resource container' ground any any 2 as stockpile
    if verbose_mode  == 1
        sysmsg '> Checking for Resource Stockpile items...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        if not listexists 'resource_stockpile_items'
            createlist 'resource_stockpile_items'

            # resources
            pushlist 'resource_stockpile_items' ORE
            pushlist 'resource_stockpile_items' 'log%s'
            pushlist 'resource_stockpile_items' 'cut up leather'


            # aspect items (if set to unload in stockpile)
            if unload_aspect_items_in_stockpile == 1
                pushlist 'resource_stockpile_items' 'aspect core'
                pushlist 'resource_stockpile_items' 3836
                pushlist 'resource_stockpile_items' 17686
                pushlist 'resource_stockpile_items' phylactery
            endif

            pushlist 'resource_stockpile_items' "empty bottle%s%"
            # arcane scrolls
            pushlist 'resource_stockpile_items' 'blank scroll%s%'
            # plant chemicals
            pushlist 'resource_stockpile_items' 29030

            pushlist 'resource_stockpile_items' 'bag of seeds'
            pushlist 'resource_stockpile_items' 'seed of renewal'

            @pushlist 'resource_stockpile_items' 'cut up leather'

            # gems
            @pushlist 'resource_stockpile_items' 'rub%ies/y%'
            @pushlist 'resource_stockpile_items' 'tourmaline%s%'
            @pushlist 'resource_stockpile_items' 'emerald%s%'
            @pushlist 'resource_stockpile_items' 'diamond%s%'
            @pushlist 'resource_stockpile_items' 'piece%s% of amber'
            @pushlist 'resource_stockpile_items' 'amethyst%s%'
            @pushlist 'resource_stockpile_items' 'star sapphire%s%'
            @pushlist 'resource_stockpile_items' 'citrine%s%'
            @pushlist 'resource_stockpile_items' 'sapphire%s%'
        endif

        unsetvar! found_stockpile_item
        foreach item_type in 'resource_stockpile_items'
            while findtype item_type backpack as item
                sysmsg 'aqui 1'
                setvar! found_stockpile_item 1
                getlabel item item_label

                if not inlist 'items_unloaded' item_label
                    sysmsg 'Adding {{item_label}}'
                    pushlist 'items_unloaded' item_label
                endif

                @ignore item
            endwhile
        endfor
        @clearignore
    endif

    # if varexist found_stockpile_item

    menu stockpile 0
    wft action_delay
    if targetexists 'neutral'
        if verbose_mode  == 1
            sysmsg '> Restocking Resource Stockpile...' sysmsgs_color
        endif

        target 'self'
    endif

    # restock stockpile from packies
    if list 'packies' > 0
        foreach packanimal in 'packies'
            if noto packanimal = noto 'self'
                if verbose_mode  == 1
                    sysmsg '> Restocking Stockpile From Pack Animal...' sysmsgs_color
                endif

                menu stockpile 0
                wft action_delay
                if targetexists 'neutral'
                    overhead "unloading on stockpile" 56 packanimal
                    target packanimal
                endif
            endif
        endfor
    endif

    # endif

    if gumpexists 1859005118
        gumpclose 1859005118
    endif
endif


#################################
# GARDEN SHELF
#################################

if findtype 'garden shelf' ground any any 2 as gardenshelf
    if verbose_mode  == 1
        sysmsg '> Checking for Garden Shelf items...' sysmsgs_color
    endif

    if print_items_unloaded == 1
        if not listexists 'garden_shelf_items'
            createlist 'garden_shelf_items'
            pushlist 'garden_shelf_items' 'seed'
            pushlist 'garden_shelf_items' 'bag of seeds'
            pushlist 'garden_shelf_items' 'seed of renewal'
        endif

        # foreach item in 'garden_shelf_items'
        while findtype 'seed|bag of seeds|seed of renewal' as item
        # while findtype item backpack as item
            getlabel item item_label

            if not inlist 'items_unloaded' item_label
                sysmsg 'Adding {{item_label}}'
                pushlist 'items_unloaded' item_label
            endif

            @ignore item
        endwhile
        # endfor
        @clearignore
    endif

    if findtype 'seed|bag of seeds|seed of renewal'
        menu gardenshelf 0
        wft action_delay
        if targetexists 'neutral'
            if verbose_mode  == 1
                sysmsg '> Restocking Garden Shelf...' sysmsgs_color
            endif

            target 'self'
        endif

        waitforgump 426984115 action_delay
        if gumpexists 426984115
            gumpclose 426984115
        endif
    endif
endif


#################################
# DUMP ITENS IN RECYCLER
#################################

if findtype 'ornate elven chest' ground any any 2 as recycler
    sysmsg 'Recycling unidentified items...' sysmsgs_color

    menu recycler recycler_mode
    wait action_delay
    if insysmsg 'recyclable items found'
        if verbose_mode  == 1
            sysmsg 'Recycled all items!' 71
        endif
    endif

    # useobject recycler
    # waitforgump 4045111101 600
    # if gumpexists 4045111101
    #     if verbose_mode  == 1
    #         sysmsg "> Dumping items in recycler" sysmsgs_color
    #     endif

    #     while not targetexists 'neutral'
    #         sysmsg 'Generating target to dump items in recycler...' 56
    #         gumpresponse 3 4045111101
    #         waitfortarget 600
    #     endwhile

    #     target backpack
    #     waitforgump 4045111101 600

    #     if gumpexists 4045111101
    #         gumpclose 4045111101
    #     endif

    #     if insysmsg 'recyclable items found'


    #         if verbose_mode  == 1
    #             sysmsg 'Recycled all items!' 71
    #         endif

    #     endif
    # else
    #     sysmsg 'Recycler gump did NOT open!' 52
    # endif

    # if gumpexists 4045111101
    #     gumpclose 4045111101
    # endif
endif


#################################
# FINALLY, RESSUPLY FROM SHELF
#################################

if findtype 'storage shelf' ground any any 2 as shelf
    sysmsg '> RESSUPLYING...' 90
    menu shelf 1

    waitforgump 3232825965 600

    if gumpexists 3232825965
        gumpclose 3232825965
    endif

endif

#################################
# GET BARDING INSTRUMENT BACK
# FROM RECYCLER IF NEEDED
#################################

if recycler_mode == 0
    if findtype 'ornate elven chest' ground any any 2 as recycler
        if skill 'mining' > 0
            if list 'char_pickaxes' > 0
                foreach pickaxe in 'char_pickaxes'
                    while find pickaxe recycler and not find pickaxe backpack
                        hotkey 'Grab Item'
                        wft action_delay
                        target pickaxe
                    endwhile

                    if find pickaxe backpack
                        setvar! got_char_pickaxes_back 1
                    endif
                endfor

                if varexist got_char_pickaxes_back
                    unsetvar! got_char_pickaxes_back
                    if verbose_mode  == 1
                        overhead 'got char pickaxe(s) back!' 90
                        sysmsg 'Got Char Pickaxe(s) Back From Recycler!' 90
                    endif
                endif
            endif
        endif


        if skill 'lumberjacking' > 0
            if list 'char_hatchets' > 0
                foreach hatchet in 'char_hatchets'
                    while find hatchet recycler and not find pickaxe backpack
                        hotkey 'Grab Item'
                        wft action_delay
                        target hatchet
                    endwhile

                    if find hatchet backpack
                        setvar! got_char_hatchets_back 1
                    endif
                endfor

                if varexist got_char_hachets_back
                    unsetvar! got_char_hachets_back
                    if verbose_mode  == 1
                        overhead 'got char hachets(s) back!' 90
                        sysmsg 'Got Char Hachets(s) Back From Recycler!' 90
                    endif
                endif
            endif
        endif

        if skill 'musicianship' > 50
            if varexist char_instrument
                # temporarily set the razor grab hotbag to char_loot_pouch if it exists so instrument gets retrieved into it
                if varexist char_loot_pouch
                    sysmsg 'Setting Razor Grab Hotbag to Char Loot Pouch...' 52
                    hotkey "Set Grab Item HotBag"
                    wft 600
                    target char_loot_pouch
                endif

                while not find char_instrument backpack
                    hotkey 'Grab Item'
                    wft action_delay
                    target char_instrument
                    wait action_delay
                endwhile

                if find char_instrument backpack
                    if verbose_mode  == 1
                        overhead 'got char instrument back!' 90
                        sysmsg 'Got Char Instrument Back From Recycler!' 90
                    endif

                    # reset razor grab hotbag to char main backpack
                    if varexist char_loot_pouch
                        sysmsg 'Re-setting Razor Grab Hotbag to Char Backpack...' 52
                        hotkey "Set Grab Item HotBag"
                        wft 600
                        target backpack
                    endif
                endif
            endif
        endif

        if skill 'parrying' > 50
            if varexist char_shield
                # temporarily set the razor grab hotbag to char_loot_pouch if it exists so instrument gets retrieved into it
                if varexist char_loot_pouch
                    sysmsg 'Setting Razor Grab Hotbag to Char Loot Pouch...' 52
                    hotkey "Set Grab Item HotBag"
                    wft 600
                    target char_loot_pouch
                endif

                while not find char_shield backpack
                    hotkey 'Grab Item'
                    wft action_delay
                    target char_shield
                    wait action_delay
                endwhile

                if find char_shield backpack
                    if verbose_mode  == 1
                        overhead 'got char shield back!' 90
                        sysmsg 'Got Char Shield Back From Recycler!' 90
                    endif

                    # reset razor grab hotbag to char main backpack
                    if varexist char_loot_pouch
                        sysmsg 'Re-setting Razor Grab Hotbag to Char Backpack...' 52
                        hotkey "Set Grab Item HotBag"
                        wft 600
                        target backpack
                    endif
                endif
            endif
        endif
    endif

# endif recycler_mode == 0
endif



#################################
# PRINT ITEMS UNLOADED
#################################

if print_items_unloaded == 1
    if list 'items_unloaded' > 0
        sysmsg '####################'
        sysmsg '>>> ITEMS UNLOADED:' sysmsgs_color

        foreach label in 'items_unloaded'
            sysmsg '> {{label}}' 49
        endfor

        sysmsg '####################'
    else
        sysmsg '>>> NO ITEMS UNLOADED:' sysmsgs_color
    endif
endif



#################################
# FINAL CLEANUP
#################################

@removelist 'packies'
@clearignore
@clearsysmsg


sysmsg '>> FINISHED IN {{execution_timer}} ms' 99

#################################
# CHANGELOG
#################################
#
# v1.7.0
# - Replaced all loop list iterations for inline list expressons
#
# v1.6.0
# - Only dump focis, oils and bundles into misc container if they are not fully charges or cant find a aspect items tome
# - Makes using detecting hidden a toggleable option and disables it by default
# - Cleans up commented code from previous approch to droping items in tomes
#
# v1.5.0
# - Uses new 'Adds all from backpack' option on all tomes
#
# v1.4.0
# - Adds check on deposit safe for 'locked in' money, that was droped inside by scripting bug, and takes it out
# - Adds while loop to force target generation on recycler dump items button when recycler gump is already open (gump has a bug where first click wont open target)
# - Simplifies a little logic behind getting bard instrument back from recycler and closing recycler gump
# - Improves shelf re-use throughout scripts and removes related setvars (improves performance a bit)
# - Moves garden shelf restock to before stockpile restock so plant chemicals go into garden shelf first
# - Fix bug where script would only move 1 stack of misc items of the same type and leave other stacks
# - Adds suport to restock shelf using a container (bag/pouch/backpack) instead of using 'self'
# - Switches list configs to non-persistent variables (simplifies code maintenance and should improve performance)
#
# v1.3.3
# - Adds missing 'bamboo flute' instrument to instruments list
# - Completed tmaps will be thrown in a trashcan instead of directly on the ground if a trashcan is found nearby
# - Improved some system messages related to flow errors
#
# v1.3.2
# - Fixed detection of supremely instruments for bards
# - Fix searching for tomes inside different types of containers
#
# v1.3.1
# - Fixed bug where char wouldnt deposit gold from packies
# - Switched pack notoriety from '== noto self' to '== friend', so blue chars will also have their packies detected
# - New option 'ground_container' for tomes
# - Script should now work for people who have their tomes inside the master_container or in an independant ground_container
#
# v1.3.0
# - Switches arcane rune list for regular generic type 'rune' for performance optimization
# - Detect bard templates and saves magical instruments (exceedingly and supremely) to get them back from recycler after dumping items
# - Changes script name to SUPER RESTOCK & RESSUPLY
#
# v1.2.0
# - Dump completed tmaps on ground instead of trying to put them inside tome
#
# v1.1.0
# - Adds currency list so script will also unload doublons along with gold
# - Adds 'verbose_mode' to control printing of messages
# - Greatly simplified script configuration for containers locations
#
# v1.0.4
# - Adds routine for smelting ores and making boards out of logs
#
# v1.0.0
# - First usable stable version release
#
# v0.0.1
# - Initial release
