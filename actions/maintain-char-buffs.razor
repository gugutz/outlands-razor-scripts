#################################
# MAINTAIN ALL CHAR BUFFS
# by gugutz
# version 0.0.1
#################################


#################################
# CONFIGURATION
#################################

if not varexist maintain_char_buffs_script_first_run_done
    setvar debug 0
    setvar! action_delay 350
    setvar! unequip_hands_to_drink_potion 1
    setvar! prefer_switching_to_1hand_weapon_instead_of_unequiping 1

    # this list  is created separatedely cause we also seed it with values
    if not listexists 'one_handed_weapons'
        createlist 'one_handed_weapons'
        if skill 'swordsmanship' >= 20
            pushlist 'one_handed_weapons' 'katana'
            pushlist 'one_handed_weapons' 'viking sword'
            pushlist 'one_handed_weapons' 'broadsword'
            pushlist 'one_handed_weapons' 'longsword'
        elseif skill 'archery' >= 20
            pushlist 'one_handed_weapons' 'crossbow'
        elseif skill 'mace fighting' >= 20
            pushlist 'one_handed_weapons' 'war mace'
            pushlist 'one_handed_weapons' 'maul'
            pushlist 'one_handed_weapons' 'mace'
            pushlist 'one_handed_weapons' 'club'
        elseif skill 'fencing' >= 20
            pushlist 'one_handed_weapons' 'kryss'
            pushlist 'one_handed_weapons' 'war fork'
        endif
    endif

    #################################
    # CREATE ALL TIMERS
    #################################

    if not listexists 'maintain_char_buffs_timers'
        createlist 'maintain_char_buffs_timers'
        pushlist 'maintain_char_buffs_timers' 'waiting_spell_timer'

        foreach timer in 'maintain_char_buffs_timers'
            if not timerexists timer
                createtimer timer
                settimer timer 99999
            endif
        endfor
    endif

    # flag first run done
    setvar! maintain_char_buffs_script_first_run_done 1
endif


######################################
#
# CODE START
#
######################################


unsetvar! done_something

clearsysmsg


######################################
# BARDING BUFFS
######################################

# BARDING BUFFS GO LAST CAUSE THEY HAVE A COOLDOWN BETWEEN ONE ANOTHER
# WHICH COULD DELAY THE APPLICATION OF OTHER MORE IMPORTANT BUFFS LIKE STR POT

# if not listexists 'barding_skills'
#     createlist 'barding_skills'
#     pushlist 'barding_skills' 'discordance'
#     pushlist 'barding_skills' 'peacemaking'
#     pushlist 'barding_skills' 'provocation'
#     pushlist 'barding_skills' 'lap harp'
# endif

# foreach skill_name in 'barding_skills'
    # if skill skill_name >= 50 and not findbuff 'song of {{skill_name}}'
    #     setvar temp skill_name
    #     useskill skill_name
    #     overhead 'PEGOU CONCAT {{temp}}'
    # endif
# endfor

if skill 'peacemaking' >= 50 or skill 'discordance' >= 50 or skill 'provocation' >= 50

    if not varexist char_instrument
        if not listexists 'instruments'
            createlist 'instruments'
            pushlist 'instruments' 'lute'
            pushlist 'instruments' 'drum'
            pushlist 'instruments' 'bamboo flute'
            pushlist 'instruments' 'tambourine'
            pushlist 'instruments' 'lap harp'
        endif


        # # overhead 'debug 00001'
        # if not listexists 'char_instrument'
        #     createlist 'char_instrument'
        # else
        #     clearlist 'char_instrument'
        # endif

        foreach type in 'instruments'
            # find all instruments and see if any of them are magical
            while findtype type backpack as instrument and not varexist char_instrument
                getlabel instrument instrument_label

                if exceedingly in instrument_label or supremely in instrument_label
                    setvar! char_instrument instrument
                    break
                endif
                @ignore instrument
            endwhile

            @clearignore

            if varexist char_instrument
                break
            endif
        endfor
    endif

    if varexist char_instrument

        # PEACEMAKING
        if skill 'peacemaking' >= 20 and not findbuff 'song of peacemaking'
            script 'buffs\peacemaking-buff'
        endif

        # PEACEMAKING
        if skill 'discordance' >= 20 and not findbuff 'song of discordance'
            setvar! done_something 1

            # taken from my discordance-buff script
            clearsysmsg
            useskill 'discordance'
            wft 600

            if insysmsg 'You must wait a few moments before performing another barding song'
                over 'barding cooldown. retrying...'
                replay
            endif

            if insysmsg 'What instrument shall you play?'
                overhead 'selecting instrument...' 54
                dclick instrument
                wait 600
            endif

            if targetexists 'harmful' and insysmsg 'What do you wish to discord?'

                if findtype 'dagger' backpack as knife
                    target knife
                elseif findtype 'spellbook' backpack as book
                    target book
                else
                    targetrelloc 1 1
                endif

                wait 200
                if findbuff 'song of discordance'
                    overhead '+ discordance buff' 91
                    stop
                endif
            endif

        endif

        # PROVOCATION
        if skill 'provocation' >= 20 and not findbuff 'song of provocation'
            setvar! done_something 1

            # taken from my provocation-buff script
            clearsysmsg
            useskill 'provocation'
            wft 600

            if insysmsg 'You must wait a few moments before performing another barding song'
                over 'barding cooldown. retrying...'
                replay
            endif

            if insysmsg 'What instrument shall you play?'
                overhead 'selecting instrument...' 54
                dclick instrument
                wait 600
            endif

            if targetexists 'harmful' and insysmsg 'What do you wish to incite?'

                if findtype 'dagger' backpack as knife
                    target knife
                elseif findtype 'spellbook' backpack as book
                    target book
                else
                    targetrelloc 1 1
                endif

                wait 200
                if findbuff 'song of provocation'
                    overhead '+ provocation buff' 91
                    stop
                endif
            endif

        endif
    else
        overhead '!CANT BUFF! NO INSTRUMENT!' 39
    endif

endif


######################################
# OTHER SKILL BUFFS
######################################

# keep campfire buff
if skill 'camping' >= 20 and not findbuff 'campfire visit'
    setvar! done_something 1
    # copied from my campfire buff script
    #
    if findtype campfire ground any any 7 as campfire_serial
        overhead 'found campfire nearby!' 53
        dclick campfire_serial
    endif

    if not findtype campfire ground any any 2 and findtype 'kindling' backpack
        if not findtype campfire ground any any 7
            if findtype kindling backpack as firestarter
                overhead '*lighting a campfire*' 53
                dclick firestarter
                wait 600

                # check if succeeded
                if findtype campfire ground any any 2
                    overhead '*created campfire*'
                endif
            else
                sysmsg 'Out of Kindling!' 39
                overhead '! no kindling !' 39
                wait 600
                break
            endif
        endif
    endif
endif

# TRACKING - BEGIN HUNTING
# if skill 'tracking' > 0 and not findbuff 'Tracking Hunting'
#     if not varexist tracking_gump_id
#         @setvar tracking_gump_id 4267467659
#     endif


#     if not findbuff 'Tracking Hunting'
#         while not gumpexists tracking_gump_id
#             sysmsg 'Opening tracking gump...' 53
#             skill 'tracking'
#             waitforgump tracking_gump_id 600
#         endwhile

#         if gumpexists tracking_gump_id
#             if not ingump 'Stop Hunting' 4267467659
#                 overhead 'STARTING HUNTING' 65
#                 gumpresponse 6 tracking_gump_id
#                 waitforgump tracking_gump_id action_delay
#             else
#                 overhead 'already hunting' 55
#             endif

#             gumpclose tracking_gump_id
#         endif
#     endif
# endif

# maintain food satisfaction buff
if not findbuff 'Food Satisfaction' and findtype 'tray' backpack
    setvar! done_something 1

    if not findtype 'tray' backpack
        overhead '! no delectable !' 39
    elseif findtype 'tray' backpack as food
        overhead '*eating delectable*' 65
        dclick food
        wait action_delay
    endif
endif



# keep taste id buff
if skill 'taste identification' > 0 and not findbuff 'herbal poultice'
    setvar! done_something 1

    skill 'tasteid'
    wft action_delay
    if findtype 'dagger' backpack as knife
        overhead '*herbal poultice*'
        target knife
    elseif findtype 'spellbook' backpack as book
        overhead '*herbal poultice*'
        target book
    else
        targetrelloc 1 1
    endif

    wait action_delay
    if findbuff 'herbal poultice'
        overhead '*herbal poultice*'
    endif
endif

if insysmsg 'disturbed'
    overhead 'disrupted before other buffs!'
    stop
endif




######################################
# STR POTION
######################################

# maintain str at 120 pots
unsetvar! drink_str_potion
if name != 'C A R O L'
    if str < 120 and findtype 'white potion'


        # DECISION FLOW
        if str < 120 or not findbuff 'Strength'
            if debug == 1
                sysmsg '[DEBUG] STR use case 1' 38
            endif
            setvar! drink_str_potion 1
        endif


    endif
endif

######################################
# AGIL POTION
######################################

unsetvar! drink_agility_potion

if skill 'magery' >= 80 and dex <= 25
    if skill 'swordsmanship' >= 50 or skill 'fencing' >= 50 or skill 'mace fighting' >= 50 or skill 'archery' >= 50 or skill 'wrestling' >= 50
        setvar! drink_agility_potion 1
    endif
# for dexxers
elseif dex <= 100
    setvar! drink_agility_potion 1
endif


# if we have to drink a potion, check if left hand has a weapon first
if varexist drink_str_potion or varexist drink_agility_potion
    if not lhandempty
        if prefer_switching_to_1hand_weapon_instead_of_unequiping == 1
            foreach weapon in 'one_handed_weapons'
                if findtype weapon backpack as weap
                    overhead "*switching to 1-handed*"
                    dclick weap
                    wait action_delay
                    break
                endif
            endfor
        elseif unequip_hands_to_drink_potion == 1
            hotkey "Undress Left Hand"
            wait action_delay
        endif
    endif

    # str pots
    if varexist drink_str_potion
        if not findtype 'white potion' 'backpack'
            overhead 'no str pots!' 39
        else
            overhead '■ str potion ■' 91
            potion 'strength' 91

            createtimer 'waitforsysmsg'
            settimer  'waitforsysmsg' 600

            while timer 'waitforsysmsg' < 600
                if insysmsg 'before you may use another strength potion'
                    overhead '! str potion !' 91
                    break
                endif
            endwhile

            # stop
        endif
    endif

    # agil pots
    if varexist drink_agility_potion
        if not findtype 'blue potion' 'backpack'
            overhead 'no agil pots!' 39
        else
            overhead '■ agility potion ■' 91
            potion 'agility'

            createtimer 'waitforsysmsg'
            settimer  'waitforsysmsg' 600

            while timer 'waitforsysmsg' < 600
                if insysmsg 'before you may use another strength potion'
                    overhead '! str potion !' 91
                    break
                endif
            endwhile

        endif
    endif
endif




######################################
# MR POT
######################################

if not findbuff 'magic resist potion'
    setvar! done_something 1

    ##############################
    # MR POT SCRIPT

    if findtype 'black potion' backpack
        overhead '*mr pot*' 90
        usetype 'black potion' 'backpack'
        stop
    else
        overhead '* NO MR POT *' 39
    endif
    # END MR POT SCRIPT
    ##############################


endif


######################################
# MAGIC REFLECTION
######################################

if skill 'magery' >= 40 and not findbuff 'Magic Reflection'
    setvar! done_something 1

    if counttype 'Garlic' > 0 and counttype 'Mandrake Root%s%' > 0 and counttype "Spider's Silk" > 0
        cast 'Magic Reflection'
        wait action_delay

        @settimer 'waiting_spell_timer' 0
        while not findbuff 'Magic Reflection' and timer 'waiting_spell_timer' < 1500
            wait 100
            if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
                sysmsg 'BREAKING WHILE WAITING FOR MAGIC REFLECT'
                wait 150
                break
            endif
        endwhile

        wait 150
    elseif counttype 'Magic Reflection' > 0
        cast 'Magic Reflection'
        wait action_delay
    else
        sysmsg 'No Reagents Or Scrolls for Magic Reflection'
        sysmsg 'no reflect regs or scroll!' 39
    endif

    if findbuff 'magic reflection'
        sysmsg '>>> MAGIC REFLECTION' 88
        overhead '+magic reflect' 72
        stop
    endif
endif


######################################
# PROTECTION
######################################

if skill 'magery' > 40 and not findbuff 'protection'
    setvar! done_something 1

    cast 'protection'

    @settimer 'waiting_spell_timer' 0
    while not findbuff 'protection' and timer 'waiting_spell_timer' < 1500
        wait 100
        if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
            sysmsg 'BREAKING WHILE WAITING FOR PROTECTION'
            wait 150
            break
        endif
    endwhile
    wait 150

    if findbuff 'protection'
        sysmsg '>>> PROTECTION' 88
        overhead '+protection' 72
        stop
    endif
endif

######################################
# REACTIVE ARMOR
######################################

if skill 'magery' > 40 and not findbuff 'reactive armor'
    setvar! done_something 1

    cast 'reactive armor'
    @settimer 'waiting_spell_timer' 0
    while not findbuff 'reactive armor' and timer 'waiting_spell_timer' < 2000
        wait 100
        if insysmsg 'disturbed' or insysmsg 'spell will not adhere' or insysmsg 'cannot cast a spell'
            sysmsg 'BREAKING WHILE WAITING FOR REACTIVE ARMOR'
            wait 150
            break
        endif
    endwhile
    
    wait 150
    
    if findbuff 'reactive armor'
        sysmsg '>>> REACTIVE ARMOR' 88
        overhead '+reactive'
    endif
endif




################################
# NO ACTION TO DO...
################################

if not varexist done_something
    overhead 'all buffs on!' 66
endif
