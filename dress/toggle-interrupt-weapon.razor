
#################################
# CREATE ALL LISTS FOR CONFIG
#

if not listexists 'config_lists'
    createlist 'config_lists'
else
    clearlist 'config_lists'
endif

pushlist 'config_lists' 'dont_unequip_if_already_equiped'

foreach list in 'config_lists'
    if not listexists list
        createlist list
    else
        clearlist list
    endif
endfor

pushlist 'dont_unequip_if_already_equiped' 'no'


#################################
# SCRIPT CONFIGURATION

if listexists 'disturb_weapons_2'
    if list 'disturb_weapons_2' == 0
        clearlist 'disturb_weapons_2'
    endif
endif

if not listexists 'disturb_weapons_2'
    createlist 'disturb_weapons_2'
    # PS: ARCHERY DONT HAVE INTERRUPT WEAPON
    if skill 'fencing' >= 50 or findtype 'kryss' backpack
        if findtype 'kryss' backpack
            pushlist 'disturb_weapons_2' 'kryss'
        endif
    elseif skill 'mace fighting' >= 50 or findtype 'club' backpack or findtype 'hammer pick' backpack or findtype 'war axe' backpack
        if findtype 'club' backpack
            pushlist 'disturb_weapons_2' 'club'
        elseif findtype 'hammer pick' backpack
            pushlist 'disturb_weapons_2' 'Hammer Pick'
        elseif findtype 'war axe' backpack
            pushlist 'disturb_weapons_2' 'war axe'
        endif
    elseif skill 'swordsmanship' >= 50 or findtype 'katana' backpack or findtype 'cutlass' backpack or findtype 'scimitar' backpack
        if findtype 'katana' backpack
            pushlist 'disturb_weapons_2' 'katana'
        elseif findtype 'cutlass' backpack
            pushlist 'disturb_weapons_2' 'cutlass'
        elseif findtype 'scimitar' backpack
            pushlist 'disturb_weapons_2' 'scimitar'
        endif
    else
        overhead 'char has no weapon skill'
    endif
endif

if list 'disturb_weapons_2' == 0
    overhead 'no disturb weapons in list'
endif

foreach weapon in 'disturb_weapons_2'
    if findtype weapon lefthand
        if inlist 'dont_unequip_if_already_equiped' 'yes'
            overhead 'equiped on left hand' 54
            break
        else
            hotkey 'Toggle Peace Only'
            hotkey 'Undress Left Hand'
            break
        endif
    elseif findtype weapon righthand
        if inlist 'dont_unequip_if_already_equiped' 'yes'
            overhead 'equiped on right hand' 54
            break
        else
            hotkey 'Toggle Peace Only'
            hotkey 'Undress Right Hand'
            break
        endif
    else
        if not rhandempty
            if targetexists 'any'
                overhead '*undressing right hand*'

                # toggle war before undress to avoid wasting swings with punches (arena usefull)
                hotkey 'Toggle Peace Only'

                hotkey 'Undress Right Hand'
                wait 200
            endif
        elseif not lhandempty
            if targetexists 'any'
                overhead '*undressing left hand*'

                # toggle war before undress to avoid wasting swings with punches (arena usefull)
                hotkey 'Toggle Peace Only'

                hotkey 'Undress Left Hand'
                wait 200
            endif
        endif

        if findtype weapon backpack as weap
            # overhead 'equiping weapon' 48
            if targetexists
                hotkey 'Drop What You Are Currently Holding'
                lift weap 1
                wait 100
                drop 'self' LeftHand
                # tab out, good for duels
            else
                dclick weap
                # tab out, good for duels
            endif


            settimer 'waiting_dress_timer' 0
            while lhandempty and rhandempty
                wait 10

                sysmsg 'waiting to equip weapon...'
                if timer 'waiting_dress_timer' >= 400
                    sysmsg 'BREAKING WHILE WAITING TO EQUIP HALBERD'
                    break
                endif
            endwhile

            if not lhandempty or not rhandempty
                overhead 'attacking!' 41 'last'
                hotkey 'Attack Last Target'
                break
            else
                hotkey 'Toggle War Only'
                hotkey 'Toggle Peace Only'
            endif


        endif
    endif
endfor
