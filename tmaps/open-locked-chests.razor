# Locked Chest Opener
# v0.0.3
# by gugutz
#
# This script will remove traps and unlock locked chests (paragon and treasure map chests)
#################################
#
# PREPARING TO START
#
createtimer 'waiting_tchest_msg_timer'
settimer 'waiting_tchest_msg_timer' 1501
createtimer 'wait_for_progress'
settimer 'wait_for_progress' 5000


@unsetvar chest
@setvar chest
wft 500
while targetexists 'neutral'
    if timer 'waiting_tchest_msg_timer' >= 1500
        overhead 'select t-chest to loot...'
        sysmsg 'Select T-Chest To Loot...'
        settimer 'waiting_tchest_msg_timer' 0
    endif
endwhile
#

if not varexist chest
    overhead 'no chest set'
    stop
endif


@setvar action_delay_var 600
@setvar locked_chest_gump 736038070

# # Open Metal Chest
# if findtype 29076 true
#     dclicktype 29076 true
# endif
# # Open Wooden Chest
# if findtype 29089 true
#     dclicktype 29089 true
# endif

clearsysmsg

while not gumpexists locked_chest_gump
    sysmsg 'Opening chest first time...' 53
    dclick chest
    waitforgump locked_chest_gump 600
endwhile

#################################
# REMOVE CHEST TRAPS
#################################


while not dead and not insysmsg 'You have successfully cleared it of traps'

    if skill 'hiding' >= 70
        while not hidden
            useskill 'hiding'
            wait 1000
        endwhile
    elseif skill 'magery' >= 75
        while not hidden
            cast 'invisibility'
            wft 3500
            if targetexists 'neutral'
                target 'self'
            endif
            wait action_delay_var
        endwhile
    endif


    # Skip ahead if devoid of traps
    if insysmsg 'You have successfully cleared it of traps'
        overhead '* TRAPS CLEARED * ' 59
        break
    endif

    # # Skip ahead if chest already open
    # if findtype 29076 ground any any 2 true or findtype 29089 ground any any 2
    #     overhead '* ALREADY OPEN * ' 59
    #     break
    # endif

    # Remove traps
    #
    if findtype drill as tool
        if not gumpexists locked_chest_gump
            sysmsg 'Clicking chest to open gump...'
            dclick chest
            waitforgump locked_chest_gump 1000
        else
            if ingump 'Cleared of Traps' 736038070 or insysmsg 'appears to be devoid'
                sysmsg 'CHEST ALREADY CLARED OF TRAPS....'
                break
            endif

            gumpresponse 7
            wait action_delay_var
            if insysmsg 'You do not have any trap tools of that material on hand'
                gumpresponse 5
                waitforgump locked_chest_gump 1000
            else
                while not insysmsg 'You make some progress' and not insysmsg 'You fail to make any progress'
                    wait 100
                    if timer 'wait_for_progress' >= 5000 or insysmsg 'You make some progress' or insysmsg 'You fail to make any progress' or insysmsg 'cleared it of traps'
                        settimer 'wait_for_progress' 0
                        break
                    endif
                endwhile
            endif
        endif
    else
        overhead 'NO MORE DRILLS' 55
        stop
    endif


endwhile


#################################
# LOCKPICK CHEST
#################################

while not dead and not insysmsg 'You successfully pick the lock.'
    #
    if findtype 'lockpicks' as tool
        if not gumpexists locked_chest_gump
            sysmsg 'Clicking chest to open gump...'
            dclick chest
            waitforgump locked_chest_gump 1000
        else
            gumpresponse 4
            wait action_delay_var
            if insysmsg 'You do not have any lockpicks of that material on hand'
                gumpresponse 2
                waitforgump locked_chest_gump 1000
            else
                settimer 'wait_for_progress' 0
                while not insysmsg 'You make some progress' and not insysmsg 'You fail to make any progress'
                    wait 100
                    if timer 'wait_for_progress' >= 5000 or insysmsg 'You make some progress' or insysmsg 'You fail to make any progress' or insysmsg 'You successfully pick the lock.'
                        settimer 'wait_for_progress' 0
                        break
                    endif
                endwhile
            endif
        endif
    else
        overhead 'NO MORE LOCKPICKS' 55
        stop
    endif

    if findtype 29076 ground any any 2 or findtype  29089 ground any any 2
        break
    endif

endwhile

if insysmsg 'You successfully pick the lock' or findtype 29076 ground any any 2 or findtype  29089 ground any any 2
    overhead 'chest opened!' 54
    sysmsg '>>> Chest Opened!' 54
    break
endif

sysmsg '>>> Open Locked Chests Script finished!' 53
